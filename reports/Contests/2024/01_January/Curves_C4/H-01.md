# High Severity

## Title
Users can grief the FeeSplitter contract and steal fees from real holders while they hold externalTokens or even if they sell all their curvesTokens

## Impact
Fees earned by curveTokens holders can be stolen by users who did not hold curveTokens during the period of the fees distribution.

## Proof of Concept
The existing implementation of how the fees are distributed has a bug that would allow malicious users to steal holders' fees.
There are two key variables when it comes to the holders' fees data:
1. The [`data.cumulativeFeePerToken`](https://github.com/code-423n4/2024-01-curves/blob/main/contracts/FeeSplitter.sol#L18) (which represents the total amount of fees that have ever been given to be distributed to the holders) 
2. The [`data.userFeeOffset`](https://github.com/code-423n4/2024-01-curves/blob/main/contracts/FeeSplitter.sol#L19) (which is intended to be used as a way to indicate from what point in time a given user should be considered in the fees distribution and also a way to reflect the last time a user claimed his fees).
- **The problem is that the `data.userFeeOffset` is not updated every time that the users' curvesBalance of a curveToken changes**, ***currently, the `data.userFeeOffset` is only updated when a user buys or sells curvesTokens from his account***, but, ***the users' curvesBalance do not only change when buying or selling, their curvesBalance can also change when transferring curveTokens between accounts, or when withdrawing their curveTokens for externalTokens***. The failure to update the `data.userFeeOffset` every time that the users' curvesBalance changes opens the doors for griefing attacks like the one we'll talk about in a bit.

**There are only two ways in which the `data.userFeeOffset` can be updated**
1. The first one is when the [`Curves::_transferFees() function`](https://github.com/code-423n4/2024-01-curves/blob/main/contracts/Curves.sol#L218-L261) calls the [`FeeSplitter:onBalanceChange() function`](https://github.com/code-423n4/2024-01-curves/blob/main/contracts/FeeSplitter.sol#L96-L100)
2. The second one which is when the user itself claims his fees by calling the [`FeeSplitter::claimFees() function`](https://github.com/code-423n4/2024-01-curves/blob/main/contracts/FeeSplitter.sol#L80-L87), or the [`FeeSplitter::batchClaiming() function`](https://github.com/code-423n4/2024-01-curves/blob/main/contracts/FeeSplitter.sol#L103-L117), and either of the two functions internally calls the [`FeeSplitter::updateFeeCredit() function`](https://github.com/code-423n4/2024-01-curves/blob/main/contracts/FeeSplitter.sol#L63-L71), if the account has any curvesTokenBalance at the moment of claiming the fees, the amount of unclaimedFees and the `data.userFeeOfsset` will be updated.

> FeeSplitter.sol
```solidity
contract FeeSplitter is Security {
  ...

  function updateFeeCredit(address token, address account) internal {
    ...
        ...
        //@audit-info => data.userFeeOffset is updated in this function and in the `onBalanceChange() function`
        data.userFeeOffset[account] = data.cumulativeFeePerToken;
    ...
  }

  function claimFees(address token) external {
    //@audit-info => When claiming fees, the updateFeeCredit() is called, therefore, the `data.userFeeOffset` is updated
    updateFeeCredit(token, msg.sender);
    ...
  }

  function onBalanceChange(address token, address account) public onlyManager {
      ...
      //@audit-info => When the Curves contract calls the `onBalanceChange()`, `data.userFeeOffset` is updated.
      //@audit-info => The curves contract calls this function only when a user has bought or sold a curvesToken!
      data.userFeeOffset[account] = data.cumulativeFeePerToken;
      ...
  }

  //@dev: this may fail if the the list is long. Get first the list with getUserTokens to estimate and prepare the batch
  function batchClaiming(address[] calldata tokenList) external {
      ...
          ...
          //@audit-info => When claiming fees, the updateFeeCredit() is called, therefore, the `data.userFeeOffset` is updated
          updateFeeCredit(token, msg.sender);
          ...
      ...
  }

  ...
}
```

> Curves.sol
```solidity
function _transferFees(
    ...
) internal {
    ...
        ...
        if (feesEconomics.holdersFeePercent > 0 && address(feeRedistributor) != address(0)) {
            //@audit-info => In the Curves contract, only the _transferFees() function calls the `feeRedistributor.onBalanceChange()`
            feeRedistributor.onBalanceChange(curvesTokenSubject, msg.sender);
            feeRedistributor.addFees{value: holderFee}(curvesTokenSubject);
        }
    ...
}

function _buyCurvesToken(address curvesTokenSubject, uint256 amount) internal {
    ...

    //@audit-info => When buying curvesTokens, the _transferFees() is called, thus, the `feeRedistributor.onBalanceChange()` is called too
    _transferFees(curvesTokenSubject, true, price, amount, supply);

    ...
}

function sellCurvesToken(address curvesTokenSubject, uint256 amount) public {
    ...

    //@audit-info => When selling curvesTokens, the _transferFees() is called, thus, the `feeRedistributor.onBalanceChange()` is called too
    _transferFees(curvesTokenSubject, false, price, amount, supply);
}
```


So, now that we know under which circumstances the `data.userFeeOffset` is updated, let's talk about the grieffing attacks that can be performed because of this vulnerability.
1. User buys curvesTokens in accountA, transfers them to accountB, and sells them in this account. Then, in the future, the user buys a large amount of curveTokens in the accountB and then transfers those curvesTokens to accountA. Now, when the `data.cumulativeFeePerToken` has grown significantly, the user will proceed to claim fees in accountA with a big balance, as a result of the bug, the user will be able to claim fees for all the time since he bought the first curveToken and transferred it to accountB (where those tokens were sold), and to make things worse, the user is claiming fees with a huge balance in accountA, so, it'll earn fees for all the time while accountA was not holding any curvesTokens and it will also earn fees using a huge balance that was just recently acquired in the accountB and transferred to accountA.

2. User buys curvesTokens in accountA, withdraws those curvesTokens for externalTokens, time passes and the user deposits back the externalTokens and gets curvesTokens, then, the user proceeds to claim fees and will be able to claim fees for all the time while he had the externalTokens instead of the curvesTokens. As per a sponsor's comment "Most of the erc20 will be in uniswap pools or bridge vaults then is a waste of eth pay fees to them", this scenario will also grief curveTokens holders.

- I ran a manual desk check where I go step by step simulating the grief vector number 1, where a User buys 1 curveToken in accountA, transfers the curveToken to accountB, sells the curveToken in accountB and let time pass, then, when the `data.cumulativeFeePerToken` has grown substantially, userB buys a huge amount of curveTokens in accountB, transfers those curveTokens to accountA and then proceeds to claim the fees in accountA. As a result, the user will be paid for all the time while he was not holding a single curveToken, and to make things worse, it will claim fees using a huge balance that had just been acquired.

### Desk Check simulating the grief attack number 1
![Desk Check simulating the grief attack number 1](https://res.cloudinary.com/djt3zbrr3/image/upload/v1705173608/Curves/griefing_attack_desk_check.png)

- To get a better resolution of the data displayed in the image, I published the desk check simulation in a Google spreadsheet, to access it, please use the next link: https://docs.google.com/spreadsheets/d/1orTZMJ3_3CNquB5ujrKj7xXjRz0kRzGAcWYd9I-40UQ/edit?usp=sharing

A brief explanation of what the attacker does in the griefing attack is:
1. Buys 1 curvesToken in an accountA. The buy function will end up calling the [`FeeSplitter::onBalanceChange() function`](https://github.com/code-423n4/2024-01-curves/blob/main/contracts/FeeSplitter.sol#L96-L100) and will set the accountA `data.userFeeOffset` to be the value of the current `data.cumulativeFeePerToken`
2. User transfers the curveToken to accountB. This means the accountA is not a holder anymore, now the holder is the accountB.
3. User sells the curveToken on the accountB. The sell function will end up calling the [`FeeSplitter::onBalanceChange() function`](https://github.com/code-423n4/2024-01-curves/blob/main/contracts/FeeSplitter.sol#L96-L100) too, but this doesn't really matter for this grieffing attack. So, at this point, neither AccountA nor AccountB are holders, none of those accounts own a curveToken.
4. User lets the time pass while the `data.cumulativeFeePerToken` grows as other users transact with the curvesToken of the subjectToken and the holders earn fees.
5. After the `data.cumulativeFeePerToken` has grown significantly, the user will buy a huge amount of curvesToken in accountB. This purchase will end up calling the [`FeeSplitter::onBalanceChange() function`](https://github.com/code-423n4/2024-01-curves/blob/main/contracts/FeeSplitter.sol#L96-L100) and updating the accountB `data.userFeeOffset`. But note, how the accountA `data.userFeeOffset` hasn't been modified since the step 1.
6. Now, the user will transfer the just recently huge amount of curvesTokens from accountB to accountA. 
7. Finally, the user will proceed to claim fees using accountA. When the [`FeeSplitter::claimFees() function`](https://github.com/code-423n4/2024-01-curves/blob/main/contracts/FeeSplitter.sol#L80-L87) calls the [`FeeSplitter::updateFeeCredit() function`](https://github.com/code-423n4/2024-01-curves/blob/main/contracts/FeeSplitter.sol#L63-L71) and computes the total amount of owed fees, it will read the accountA `data.userFeeOffset` which the last time it was updated was in step1. This will cause accountA to be able to claim fees for all the time while it was not holding any curveToken, plus, it will amplify the impact by claiming fees using a huge amount of curvesTokens that were just recently bought using accountB.


> FeeSplitter.sol
```solidity

    function updateFeeCredit(address token, address account) internal {
        TokenData storage data = tokensData[token];
        //@audit-issue => It will read how many curvesTokens owns at the moment of the call, which now it is a huge number that was inflated by transfering the curvesTokens from accountB to accountA.
        uint256 balance = balanceOf(token, account);
        if (balance > 0) {
            //@audit-issue => The `data.userFeeOffset` is a value that was set a long time ago, when accountA bought the 1 curveToken. But, that 1 curveToken was transferred to accountB where it was sold off.
            //@audit-issue => This incorrectly allows accountA to earn fees all the time while it was not holding a single curveToken, plus, it will be multiplied by a huge balance that was just recently bought using accountB.
            uint256 owed = (data.cumulativeFeePerToken - data.userFeeOffset[account]) * balance;
            //@audit-issue => The unclaimededFees will be set to a big value that will allow accountA to steal fees from real holders.
            data.unclaimedFees[account] += owed / PRECISION;
            data.userFeeOffset[account] = data.cumulativeFeePerToken;
        }
    }

    function getClaimableFees(address token, address account) public view returns (uint256) {
        TokenData storage data = tokensData[token];
        uint256 balance = balanceOf(token, account);
        //@audit-issue => The amount of owed fees that was computed is wrong, it allowed the accountA to earn fees during all the time while it was not holding a single curvesToken.
        uint256 owed = (data.cumulativeFeePerToken - data.userFeeOffset[account]) * balance;
        return (owed / PRECISION) + data.unclaimedFees[account];
    }

    function claimFees(address token) external {
        //@audit-info => When accountA claim fees, it will call the updateFeeCredit() function
        updateFeeCredit(token, msg.sender);
        //@audit-issue => The amount of owed fees that was computed is wrong, it allowed the accountA to earn fees during all the time while it was not holding a single curvesToken.
        uint256 claimable = getClaimableFees(token, msg.sender);
        if (claimable == 0) revert NoFeesToClaim();
        tokensData[token].unclaimedFees[msg.sender] = 0;
        //@audit-issue => AccountA was able to steal fees earned by real holders.
        payable(msg.sender).transfer(claimable);
        emit FeesClaimed(token, msg.sender, claimable);
    }
```

## Tools Used
Manual Audit

## Recommended Mitigation Steps
- The recommendation it is to make sure that each time the users' curvesToken balance changes, to update their fee's data accordingly.
- The user's curvesToken balance changes whenever they buy, sell, transfer, or withdraw their curveTokens. Currently, the fee's data is only updated when the curvesToken holders buy or sell. Make sure to also update their fee's data when they transfer and withdraw their curveTokens.
  - It is important to update the fee's data using the current users' curvesToken balance, which means, use their current balance as it is before updating the balances to reflect the operation that is been executed.

1. Make sure to call the[`FeeSplitter::updateFeeCredit() function`](https://github.com/code-423n4/2024-01-curves/blob/main/contracts/FeeSplitter.sol#L63-L71) inside the [`FeeSplitter::onBalanceChange() function`](https://github.com/code-423n4/2024-01-curves/blob/main/contracts/FeeSplitter.sol#L96-L100) before the `data.userFeeOffset` is updated

> FeeSplitter.sol
```solidity
function onBalanceChange(address token, address account) public onlyManager {
    //@audit-info => update user's fees data to reflect the unclaimedFees. If user had 0 curvesTokens before the `onBalanceChange()` was called, nothing is owed to him.
+   updateFeeCredit(token,account);


    //@audit-info => If user had 0 curvesTokens, the `updateFeeCredit()` did not update the `data.userFeeOffset`
    //@audit-info => If user had 0 curvesTokens, update here the `data.userFeeOffset` to the current value of the `data.cumulativeFeePerToken`, in this way, the new user's balance will correctly start to earn interests starting from this point onwards!
    
    //@audit-info => If user had already a balance, his `data.unclaimedFees` was updated accordingly, and also his `data.userFeeOffset` was already set to the current value of `data.cumulativeFeePerToken`
      //@audit-info => In this way, the fees earned with the previous balance were debited to him, and now, the user will start to earn interest with the new balance from this point onwards
+   if (balanceOf(token, account) == 0) {
+     TokenData storage data = tokensData[token];
+     data.userFeeOffset[account] = data.cumulativeFeePerToken;
+     return;
+   }


-   TokenData storage data = tokensData[token];
-   data.userFeeOffset[account] = data.cumulativeFeePerToken;

    if (balanceOf(token, account) > 0) userTokens[account].push(token);

}
```

2. It is crucial to call the [`FeeSplitter::updateCredit() function`](https://github.com/code-423n4/2024-01-curves/blob/main/contracts/FeeSplitter.sol#L63-L71) at the correct time, this is, using the current users' curvesToken balances, before doing any update to their balances to reflect the result of the current operation.

> Curves.sol
```solidity
function _buyCurvesToken(address curvesTokenSubject, uint256 amount) internal {
    uint256 supply = curvesTokenSupply[curvesTokenSubject];
    if (!(supply > 0 || curvesTokenSubject == msg.sender)) revert UnauthorizedCurvesTokenSubject();

    uint256 price = getPrice(supply, amount);
    (, , , , uint256 totalFee) = getFees(price);

    if (msg.value < price + totalFee) revert InsufficientPayment();

    //@audit-info => The call to the SplitterFees contract needs to be made before updating the curvesTokenBalance & curvesTokenSupply
+   _transferFees(curvesTokenSubject, true, price, amount, supply);

    curvesTokenBalance[curvesTokenSubject][msg.sender] += amount;
    curvesTokenSupply[curvesTokenSubject] = supply + amount;
-   _transferFees(curvesTokenSubject, true, price, amount, supply);

    // If is the first token bought, add to the list of owned tokens
    if (curvesTokenBalance[curvesTokenSubject][msg.sender] - amount == 0) {
        _addOwnedCurvesTokenSubject(msg.sender, curvesTokenSubject);
    }
}

function sellCurvesToken(address curvesTokenSubject, uint256 amount) public {
    uint256 supply = curvesTokenSupply[curvesTokenSubject];
    if (supply <= amount) revert LastTokenCannotBeSold();
    if (curvesTokenBalance[curvesTokenSubject][msg.sender] < amount) revert InsufficientBalance();

    uint256 price = getPrice(supply - amount, amount);

    //@audit-info => The call to the SplitterFees contract needs to be made before updating the curvesTokenBalance & curvesTokenSupply
+   _transferFees(curvesTokenSubject, false, price, amount, supply);

    curvesTokenBalance[curvesTokenSubject][msg.sender] -= amount;
    curvesTokenSupply[curvesTokenSubject] = supply - amount;

-   _transferFees(curvesTokenSubject, false, price, amount, supply);
}

// Transfers tokens from current owner to receiver. Can be used for gifting or distributing tokens.
function transferCurvesToken(address curvesTokenSubject, address to, uint256 amount) external {
    if (to == address(this)) revert ContractCannotReceiveTransfer();
    _transfer(curvesTokenSubject, msg.sender, to, amount);
}

//@audit-info => If it is a transfer from oneself, only update the fees data of the `from` address
//@audit-info => If transfering between different accounts, update the fees data of both accounts.
//@audit-info => If `from` or `to` are the address of the Curves contract `address(this)`, don't make the call to the onBalanceChange()!

function _transfer(address curvesTokenSubject, address from, address to, uint256 amount) internal {
    if (amount > curvesTokenBalance[curvesTokenSubject][from]) revert InsufficientBalance();

    // If transferring from oneself, skip adding to the list
    if (from != to) {
        _addOwnedCurvesTokenSubject(to, curvesTokenSubject);
        
+       if (feesEconomics.holdersFeePercent > 0 && address(feeRedistributor) != address(0) && to != address(this)) {
+           feeRedistributor.onBalanceChange(curvesTokenSubject, to);
+       }

    }

+   if (feesEconomics.holdersFeePercent > 0 && address(feeRedistributor) != address(0) && from != address(this)) {
+     feeRedistributor.onBalanceChange(curvesTokenSubject, from);
+   }

    curvesTokenBalance[curvesTokenSubject][from] = curvesTokenBalance[curvesTokenSubject][from] - amount;
    curvesTokenBalance[curvesTokenSubject][to] = curvesTokenBalance[curvesTokenSubject][to] + amount;

    emit Transfer(curvesTokenSubject, from, to, amount);
}

```
