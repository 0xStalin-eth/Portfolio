# Medium Severity

## Lines with Problems
- https://github.com/code-423n4/2024-12-secondswap/blob/main/contracts/SecondSwap_Marketplace.sol#L480-L484

## Title
referralFeeCost is incorrectly computing a bigger value than what it is expected to be.

## Finding description and impact
### Impact
FeeCollector will receive less fees because the referral will receive more fees than what it should because the referralFeeCost is computed bigger than what it is expected to be.

### Finding Description
The referralFee is expected to be computed as a percentage of the buyerFee. For example,
- referralFee set as a 10%, and, buyerFee was computed as 100, then, referralFee should be 10.

The problem with the current implementation it is that the referralFeeCost is computed as the result of discounting the `buyerFeeTotal` - `result of actually computing the referralFee`.
- This ends up computing the referralFee as all the remaining percentage that should be left for the buyerFee (90% on the previous example).

**[SecondSwap_Marketplace._handleTransfers()]()**
```solidity
function _handleTransfers(
    ...
) private returns (uint256 buyerFeeTotal, uint256 sellerFeeTotal, uint256 referralFeeCost) {
    ...

    referralFeeCost = 0;
    if (_referral != address(0) && listing.whitelist == address(0)) {
        referralFeeCost =
          //@audit-issue => Substracting from the buyerFeeTotal causes the referralFeeCost to be computed as what the buyerFee should be after discounting the actual referralFee!
            buyerFeeTotal -
            //@audit-info => Computing the actual referralFee
            (baseAmount * bfee * IMarketplaceSetting(marketplaceSetting).referralFee()) /
            (BASE * BASE);

    }
    ...
}
```

On the below PoC, with a referralFee of 10%, it is demonstrated that the computed referralFeeCost is actually a 90% instead of a 10% of the buyerFee.

## Proof of Concept
This PoC is runnable using foundry, therefore, let's install and setup foundry.
1. Go to the section ["Adding Foundry to a Hardhat project"](https://book.getfoundry.sh/config/hardhat#adding-foundry-to-a-hardhat-project), and make sure to do the 3 steps listed in there.
2. Open the `foundry.toml` file and replace its contents for the next data:
```
[profile.default]
src = 'contracts'
out = 'out'
libs = ['node_modules', 'lib']
test = 'test'
cache_path  = 'cache_forge'
remappings = [
  '@openzeppelin/contracts/=node_modules/@openzeppelin/contracts',
  '@openzeppelin/contracts-upgradeable/=node_modules/@openzeppelin/contracts-upgradeable'
]
```

3. Now, we are ready to test using foundry. **Create a new folder under the [`tests/ folder`](https://github.com/code-423n4/2024-12-secondswap/tree/main/test),** create a new file on that folder and add the next PoC.
4. Run the PoC with the command: `forge test --match-test test_referralFeeIsComputedWrongPoC -vvvv`

Note: On this PoC it is extracted the core logic to compute the referralFeeCost with a minimal setup.
- referralFeeCost is computed when handling the transfers from the buyer to seller and feeCollector, therefore, it is only needed to extract the logic involved on getting the referralFee and bFee from the MarketplaceSettings. The main point is that the formulas and values for variables used on this PoC are exactly the same as in the codebase.

```solidity
pragma solidity ^0.8.24;

import {Test, console2} from "forge-std/Test.sol";

contract ReferralFeeCostComputedWrong is Test {
  uint256 public constant BASE = 10000;

  //@audit-info => https://github.com/code-423n4/2024-12-secondswap/blob/main/contracts/SecondSwap_MarketplaceSetting.sol#L146
  uint256 referralFee = 1000; //10%
  
  //@audit-info => https://github.com/code-423n4/2024-12-secondswap/blob/main/contracts/SecondSwap_MarketplaceSetting.sol#L142
  uint256 buyerFee = 250;

  uint256 discountedPrice = 1e18;  //1 USDT per 1 full VestedToken

  function test_referralFeeIsComputedWrongPoC() public {
    //@audit-info => Simulate purchasing 100 full VestedToken
    uint256 _amount = 100e18;

    //@audit-info => https://github.com/code-423n4/2024-12-secondswap/blob/main/contracts/SecondSwap_Marketplace.sol#L459-L473
    //@audit-info => Extracted the logic to compute the baseAmount and buyerFeeTotal without requiring to setup other contracts.
    //@audit => The core logic remains the same => Computing the price to pay for the amount of tokens to purchase, and then compute the buyerFee!

    //@audit-info => Simulate the call to the decimals() of the VestedToken on the VestingPlan!
    uint256 vestedTokenDecimals = 18;
    uint256 baseAmount = (_amount * discountedPrice) / 10 ** vestedTokenDecimals;
    
    uint256 buyerFeeTotal = (baseAmount * buyerFee) / BASE;

    //@audit-info => https://github.com/code-423n4/2024-12-secondswap/blob/main/contracts/SecondSwap_Marketplace.sol#L480-L484
    uint256 referralFeeCost =
      buyerFeeTotal -
      // (baseAmount * bfee * IMarketplaceSetting(marketplaceSetting).referralFee()) /
      (baseAmount * buyerFee * referralFee) /
      (BASE * BASE);
    
    //@audit => Verify the formula use on the codebase computes the referralFee as a 90% of the buyerFee
    assertEq(referralFeeCost, (buyerFeeTotal * 9_000) / BASE);
    
    //@audit-info => The correct referralFee should be a 10% of the buyerFee, not a 90%
    uint256 correctReferralFee = (buyerFeeTotal * referralFee) / BASE;

    assertGt(referralFeeCost, correctReferralFee);

    // console2.log("referralFeeCost: ", referralFeeCost);
    // console2.log("correctReferralFee: ", correctReferralFee);

  }
}
```

## Tools Used
Manual Audit && Foundry

## Recommended Mitigation Steps
The fix for this problem is to update the formula to compute the referralFeeCost as follows:

**[SecondSwap_Marketplace._handleTransfers()]()**
```diff
function _handleTransfers(
    ...
) private returns (uint256 buyerFeeTotal, uint256 sellerFeeTotal, uint256 referralFeeCost) {
    ...

    referralFeeCost = 0;
    if (_referral != address(0) && listing.whitelist == address(0)) {
-       referralFeeCost =
-           buyerFeeTotal -
-           (baseAmount * bfee * IMarketplaceSetting(marketplaceSetting).referralFee()) /
-           (BASE * BASE);

        //@audit-info => Correct formula to compute the referralFeeCost!
+       referralFeeCost =
+           (baseAmount * bfee * IMarketplaceSetting(marketplaceSetting).referralFee()) /
+           (BASE * BASE);
    }
    ...
}
```