# Medium Severity

## Lines with Problems
- https://github.com/code-423n4/2024-11-concrete/blob/main/src/claimRouter/ClaimRouter.sol#L207


## Title
Incorrect borrowed amount from the ClaimRouter when there is not a ProtectStrategy found for the requested token and is required to iterateover the tokenCascade list

## Finding description and impact
### Impact
Borrowing an incorrect amount when is required to borrow from a token of the tokenCascade list.
- The borrowed amount can be as big as 10x or even more (for certain tokens).

### Finding Description
When requesting assets from a vault, it is specified the token and amount of that token that wants to be borrowed. The claim router first checks if there is a ProtectStrategy (and its vault) with enough liquidity of the requested token, if there is not, it proceeds to iterate over a list of alternative assets (tokenCascade), but, since these assets are different than the original requested token, it is required to convert the original amount of tokens to its equivalent in USD value of the current tokenList being iterated.
- **The problem is that [the requested amount of the original token is only converted to stables](https://github.com/code-423n4/2024-11-concrete/blob/main/src/claimRouter/ClaimRouter.sol#L207) (a.k.a its total value in USD with 6 decimals), but, this amount is not converted to the actual token of the tokenList that will be used to look for a ProtectStrategy**
  - For example, if is requested to withdraw 1 WETH (when WETH is worth 2k USD). The returned value from `_convertFromTokenToStable` would be 2_000e6
    - Suppose `tokenCascade[i]` is WBTC (when WBTC is worth 75k USD). The current implementation will request to withdraw 2_000e6 WBTCs, which is 2e9 WBTC or 2 full WBTC (150k USD).
  - As we can see, **not converting the converted stable amount onto the actual token of the tokenList can cause to borrow more tokens by an order of magnitude bigger than what it should really be borrowed.**
    - ***In this example, the original USD value that was meant to be borrowed is 2k USD, but, in the end it was borrowed 150k USD.***


**[ClaimRouter.requestToken()](https://github.com/code-423n4/2024-11-concrete/blob/main/src/claimRouter/ClaimRouter.sol#L186-L226)**
```solidity
function requestToken(
    VaultFlags,
    address tokenAddress,
    uint256 amount_,
    address payable userBlueprint
) external onlyRole(BLUEPRINT_ROLE) {
    uint256 amount = amount_;
    (address protectionStrat, bool requiresFunds) = _getStrategy(tokenAddress, amount);
    if (protectionStrat == address(0x0)) {
        //iterates over array
        uint256 len = tokenCascade.length;
        for (uint256 i; i < len; ) {
            ...
            //@audit-info => Converting the original requested amount onto its equivalent in Stable
            amount = _convertFromTokenToStable(tokenAddress, amount_);

            //@audit-issue => The amount in stable is used to look for the strategy
            //We control both the length of the array and the external call
            // slither-disable-next-line calls-loop
            (protectionStrat, requiresFunds) = _getStrategy(tokenCascade[i], amount);
            ...
        }
        ...
    }
    ...
    //@audit-issue => If a ProtectionStrategy/Vault is found for the amount in stable, here is executed the borrow of the wrong amount!
    IProtectStrategy(protectionStrat).executeBorrowClaim(amount, userBlueprint);
}


```

## Proof of Concept
Explanation and example on the Finding Description Section. No coded PoC.

## Tools Used
Manual Audit

## Recommended Mitigation Steps
The converted amount from token to stable, convert it from stable to the current token of the tokenCascade list.
**[ClaimRouter.requestToken()](https://github.com/code-423n4/2024-11-concrete/blob/main/src/claimRouter/ClaimRouter.sol#L186-L226)**
```diff
function requestToken(
    VaultFlags,
    address tokenAddress,
    uint256 amount_,
    address payable userBlueprint
) external onlyRole(BLUEPRINT_ROLE) {
    uint256 amount = amount_;
    (address protectionStrat, bool requiresFunds) = _getStrategy(tokenAddress, amount);
    if (protectionStrat == address(0x0)) {
        //iterates over array
        uint256 len = tokenCascade.length;
        for (uint256 i; i < len; ) {
            ...
            amount = _convertFromTokenToStable(tokenAddress, amount_);
+           amount = _convertFromStableToToken(tokenCascade[i],amount)         

            //We control both the length of the array and the external call
            // slither-disable-next-line calls-loop
            (protectionStrat, requiresFunds) = _getStrategy(tokenCascade[i], amount);
            ...
        }
        ...
    }
    ...
}

```