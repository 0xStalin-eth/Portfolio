# Medium Severity

## Lines with Problems
- https://github.com/code-423n4/2024-11-concrete/blob/main/src/strategies/Aave/AaveV3Strategy.sol#L62-L64
- https://github.com/code-423n4/2024-11-concrete/blob/main/src/strategies/Radiant/RadiantV2Strategy.sol#L69-L71
- https://github.com/code-423n4/2024-11-concrete/blob/main/src/strategies/Silo/SiloV1Strategy.sol#L112-L116
- https://github.com/code-423n4/2024-11-concrete/blob/main/src/strategies/compoundV3/CompoundV3Strategy.sol#L70


## Title
getAvailableAssetsForWithdrawal() is computed wrong on AaveV3, RadiantV2, CompoundV3, and, SiloV1 Strategies, instead of querying the actual available liquidity to be withdrawn from the Protocols, it is queried the total assets owned by the Strategy

## Finding description and impact
### Impact
Incorrectly returning the assets owned by the Strategy instead of returning the actual assets that can be withdrawn causes the [`ConcreteMultiStrategyVault.getAvailableAssetsForWithdrawal()`](https://github.com/code-423n4/2024-11-concrete/blob/main/src/vault/ConcreteMultiStrategyVault.sol#L511-L524) to overestimate the Vault's available liquidity, as a result, available liquidity will be bigger than what it really is, causing **the withdrawal execution to attempt to withdraw from the Strategies instead of requesting a withdrawal on the WithdrawalQueue, so that the withdrawal request can be processed as soon as there is enough liquidity.**
- This bug makes withdrawal requests to fail instead of being added to the withdrawalQueue, this means, **the purpose of the WithdrawalQueue is completely broken since no withdrawal requests will be added to it because the withdrawal execution will be attempting to withdraw funds from strategies even though there is not enough liquidity.**

### Finding Description
The [`ConcreteMultiStrategyVault.getAvailableAssetsForWithdrawal()`](https://github.com/code-423n4/2024-11-concrete/blob/main/src/vault/ConcreteMultiStrategyVault.sol#L511-L524) calculates the available assets that can be withdrawn as the sum of the underlyingAsset held by the Vault itself, plus, all the available assets that can be withdrawn from all the vault's strategies.
- If the vault's `availableAssetsForWithdrawal` are enough to process the requested withdrawal amount, then, it proceeds to transfer the requested amount to the receiver, either directly from the vault's underlying balance (if enough), or, by withdrawing liquidity from all the vault's strategies.
- **If the vault's `availableAssetsForWithdrawal` are not enough to process the requested withdrawal amount, and the Vault has set a WithdrawalQueue, then, a new withdrawal request is added to the WithdrawalQueue**

**The problem this report is focusing on is the incorrect calculation of the vault's availableAssetsForWithdrawal because some Strategies (AaveV3, RadiantV2, CompoundV3, SiloV1) have an incorrect implementation of the `getAvailableAssetsForWithdrawal()`**, ***instead of returning the real amount that can be withdrawn from the Strategy, those functions are returning the amount of assets owned by the Strategies***, which, is incorrect because **the available assets to be withdrawn depends on the underlying protocol available liquidity, not the assets owned by the strategies.**

For example, the [`AaveV3Strategy.getAvailableAssetsForWithdrawal()`](https://github.com/code-423n4/2024-11-concrete/blob/main/src/strategies/Aave/AaveV3Strategy.sol#L62-L64) queries the aToken balance held by the Strategy, this returns the amount of underlyingAsset owned by the Strategy, but, this amount is not the available liquidity on the AavePool to be withdrawn.
```solidity
function getAvailableAssetsForWithdrawal() external view returns (uint256) {
    return aToken.balanceOf(address(this));
}
```

On the below PoC is demonstrated that the getAvailableAssetsForWithdrawal() for an AaveStrategy is in reality not the available liquidity on the Pool.
- To demonstrate, it is attempted to withdraw half the liquidity that [`ConcreteMultiStrategyVault.getAvailableAssetsForWithdrawal()`] returns (which, if it should be working correctly, the withdrawal should be processed fine). This withdraw will revert because the Pool will have even less liquidity than what was requested, this will prove that the available liquidity computed using the current approach is incorrect.
- For the purpose of demonstrating this vulnerability, the Vault has a single AaveV3Strategy with a 100% allocation.

## Proof of Concept
Create a new file under the [`test/`](https://github.com/code-423n4/2024-11-concrete/tree/main/test) and paste the below code in it:
Run the PoC with the next command `forge test --match-test test_AvailableLiquidityIsComputedWrongPoC --evm-version cancun -vvvv`
- Imporant to specify the evm-version, the forge compiler reverts everything if not specified ==> https://github.com/foundry-rs/foundry/issues/6228

```solidity
//SPDX-License-Identifier: MIT
pragma solidity 0.8.24;

import {Test} from "forge-std/Test.sol";
import {IERC20, IERC4626} from "@openzeppelin/contracts/token/ERC20/extensions/ERC4626.sol";
import {Math} from "@openzeppelin/contracts/utils/math/Math.sol";
import {Clones} from "@openzeppelin/contracts/proxy/Clones.sol";
import {ConcreteMultiStrategyVault} from "../src/vault/ConcreteMultiStrategyVault.sol";
import {Strategy, VaultFees, Allocation, GraduatedFee} from "../src/interfaces/IConcreteMultiStrategyVault.sol";
import {IStrategy} from "../src/interfaces/IStrategy.sol";
import {WithdrawalQueue} from "../src/queue/WithdrawalQueue.sol";

import {StrategyBase} from "../src/strategies/StrategyBase.sol";
import {AaveV3Strategy} from "../src/strategies/Aave/AaveV3Strategy.sol";
import {IAToken, ILendingPool, IProtocolDataProvider} from "../src/strategies/Aave/IAaveV3.sol";


contract IncorrectlyComputingAvailableLiquidityTest is Test {
    using Math for uint256;

    Strategy[] strategies;
    ConcreteMultiStrategyVault vault;

    GraduatedFee[] graduatedFees;
    GraduatedFee[] zeroFees;

    address implementation;

    address feeRecipient = address(0x1111);
    address jimmy = address(0x2222);
    address hazel = address(0x3333);
    address admin = address(0x4444);
    address ellie = address(0x5555);

    address public constant USDC = 0xA0b86991c6218b36c1d19D4a2e9Eb0cE3606eB48;
    address public constant LIDO_AaveProtocolDataProvider = 0x08795CFE08C7a81dCDFf482BbAAF474B240f31cD;
    // address public constant MAIN_AaveProtocolDataProvider = 0x41393e5e337606dc3821075Af65AeE84D7688CBD;

    address public constant USDC_WHALE = 0x37305B1cD40574E4C5Ce33f8e8306Be057fD7341;

    address public constant WSTETH = 0x7f39C581F595B53c5cb19bD0b3f8dA6c935E2Ca0;
    address public constant WSTETH_WHALE = 0x3c22ec75ea5D745c78fc84762F7F1E6D82a2c5BF;

    function setUp() public {
        uint256 forkId = vm.createFork(
            "https://eth-mainnet.g.alchemy.com/v2/THuQ6WJZ6orCusOEvVFb9baHWzk_KG_D",
            21_244_615 // @audit => Date: Nov-22-2024 04:58:59 PM +UTC
        );
        vm.selectFork(forkId);

        vm.label(feeRecipient, "feeRecipient");
        vm.label(jimmy, "jimmy");
        vm.label(hazel, "hazel");
        vm.label(USDC, "USDC");
        // Example performanceFee: [{0000, 500, 300}, {501, 2000, 1000}, {2001, 5000, 2000}, {5001, 10000, 5000}]

        graduatedFees.push(GraduatedFee({lowerBound: 0, upperBound: 500, fee: 300}));
        graduatedFees.push(GraduatedFee({lowerBound: 501, upperBound: 2000, fee: 1000}));
        graduatedFees.push(GraduatedFee({lowerBound: 2001, upperBound: 5000, fee: 2000}));
        graduatedFees.push(GraduatedFee({lowerBound: 5001, upperBound: 10000, fee: 5000}));

        zeroFees.push(GraduatedFee({lowerBound: 0, upperBound: 10000, fee: 0}));

        implementation = address(new ConcreteMultiStrategyVault());
        address vaultAddress = Clones.clone(implementation);
        vault = ConcreteMultiStrategyVault(vaultAddress);

        AaveV3Strategy lido_usdc_AAVE_strategy = new AaveV3Strategy(IERC20(USDC), feeRecipient, admin, 1000, LIDO_AaveProtocolDataProvider, vaultAddress);

        strategies.push(Strategy({
                strategy: IStrategy(address(lido_usdc_AAVE_strategy)),
                allocation: Allocation({index: 0, amount: 10_000}) //100%
        }));


        vault.initialize(
            IERC20(USDC),
            "USDC_AAVEV3STRATEGIES_VAULT",
            "USDC_VAULT",
            strategies,
            feeRecipient,
            VaultFees({depositFee: 500, withdrawalFee: 100, protocolFee: 300, performanceFee: graduatedFees}),
            type(uint256).max,
            admin
        );

        //@audit-info => Setting up a WithdrawalQueue. This must allow to add withdrawal requests to the WithdrawalQueue when the Vaul has not enough liquidity to process withdrawals!
        WithdrawalQueue queue = new WithdrawalQueue(address(vault));
        vm.prank(admin);
        vault.setWithdrawalQueue(address(queue));

        vm.label(address(vault), "vault");
        vm.label(address(lido_usdc_AAVE_strategy), "lidoUSDCStrategy");
        vm.label(address(queue), "WithdrawalQueue");

    }

    function test_AvailableLiquidityIsComputedWrongPoC() public {
      assertEq(strategies[0].strategy.asset(), USDC, "Asset Match");

      vm.prank(USDC_WHALE);
      IERC20(USDC).transfer(jimmy, 10_000e6);

      vm.startPrank(jimmy);
      IERC20(USDC).approve(address(vault), 10_000e6);
      vault.deposit(10_000e6);
      vm.stopPrank();

      //@audit => Consume the remaining liquidity on the AavePool
      _prepareAavePool();

      //@audit => Validate the AavePool has not enough liquidity to process the withdrawal.
      //@audit-info => The Vault should add the withdraw as a new withdrawal request to the WithdrawalQueue, but, instead, the tx is reverted!
      (address USDC_lido_aToken, , ) = IProtocolDataProvider(LIDO_AaveProtocolDataProvider).getReserveTokensAddresses(address(USDC));
      uint256 availableLiquidity_USDC_LIDO = IERC20(USDC).balanceOf(address(USDC_lido_aToken));
      assertEq(availableLiquidity_USDC_LIDO, 0);

      uint256 amountToWithdraw = vault.getAvailableAssetsForWithdrawal();
      
      //@audit-issue => The user request to withdraw 1k USDC, because the Pool has not enough liquidity, the withdrawal should be added as a new withdrawal request on the WithdrawalQueue!
      vm.prank(jimmy);
      vm.expectRevert();
      vault.withdraw(amountToWithdraw / 2); //@audit => Attempt to withdraw only half of the vault's available liquidity
    }

    function _prepareAavePool() internal {
      address aaveUser = address(0x6969);

      vm.prank(WSTETH_WHALE);
      IERC20(WSTETH).transfer(aaveUser, 150e18); //transfer 150 WSTETH
      
      (address _USDC_aToken, , ) = IProtocolDataProvider(LIDO_AaveProtocolDataProvider).getReserveTokensAddresses(address(USDC));
      IAToken USDC_lido_aToken = IAToken(_USDC_aToken);
      ILendingPool USDC_lido_lendingPool = ILendingPool(USDC_lido_aToken.POOL());

      (address _WSTETH_aToken, , ) = IProtocolDataProvider(LIDO_AaveProtocolDataProvider).getReserveTokensAddresses(address(WSTETH));
      IAToken WSTETH_lido_aToken = IAToken(_WSTETH_aToken);
      ILendingPool WSTETH_lido_lendingPool = ILendingPool(WSTETH_lido_aToken.POOL());

      vm.startPrank(aaveUser);
      IERC20(WSTETH).approve(address(USDC_lido_lendingPool), 150e18);
      
      WSTETH_lido_lendingPool.supply(address(WSTETH),150e18,aaveUser,0);
      WSTETH_lido_lendingPool.setUserUseReserveAsCollateral(address(WSTETH), true);

      uint256 availableLiquidity_USDC_LIDO = IERC20(USDC).balanceOf(address(USDC_lido_aToken));
      USDC_lido_lendingPool.borrow(USDC, availableLiquidity_USDC_LIDO, 2, 0, aaveUser);

      availableLiquidity_USDC_LIDO = IERC20(USDC).balanceOf(address(USDC_lido_aToken));
      assertEq(availableLiquidity_USDC_LIDO, 0);
    
      vm.stopPrank();
    }
}
```

## Tools Used
- Manual Audit && Foundry
- Previous [Similar Vulnerability](https://github.com/code-423n4/2024-06-size-findings/issues/35) about incorrectly querying the available liquity of contracts depositing on AavePool

## Recommended Mitigation Steps
Check the available liquidity on the protocol where the assets are deposited, and:
- if the protocol's liquidity is > assets owned by the strategy, return the availableAssetsForWithdrawal as the assets owned by the strategy
- if the protocol's liquidity is < assets owned by the strategy, return the protocol's liquidity, in this way the Vault won't request to withdraw more assets than those available on the underlying protocol.

**For the [AaveV3Strategy](https://github.com/code-423n4/2024-11-concrete/blob/main/src/strategies/Aave/AaveV3Strategy.sol#L62-L64),** the pool's liquidity is dependant on the underlyingAsset held in the aToken, therefore, the correct way to fix the problem is:
```diff
function getAvailableAssetsForWithdrawal() external view returns (uint256) {
-   return aToken.balanceOf(address(this));
+   uint256 protocolAvailableLiquidity = IERC20(asset()).balanceOf(address(aToken));
+   uint256 strategyOwnedAssed = aToken.balanceOf(address(this));
+   return protocolAvailableLiquidity > strategyOwnedAssed ? strategyOwnedAssed : protocolAvailableLiquidity;
}
```

**For the [RadiantV2Strategy](https://github.com/code-423n4/2024-11-concrete/blob/main/src/strategies/Radiant/RadiantV2Strategy.sol#L69-L71),** the pool's liquidity is dependant on the underlyingAsset held in the rToken, therefore, the correct way to fix the problem is:
```diff
function getAvailableAssetsForWithdrawal() external view returns (uint256) {
-   return rToken.balanceOf(address(this));
+   uint256 protocolAvailableLiquidity = IERC20(asset()).balanceOf(address(rToken));
+   uint256 strategyOwnedAssed = rToken.balanceOf(address(this));
+   return protocolAvailableLiquidity > strategyOwnedAssed ? strategyOwnedAssed : protocolAvailableLiquidity;
}
```

**For the [CompoundV3Strategy](https://github.com/code-423n4/2024-11-concrete/blob/main/src/strategies/compoundV3/CompoundV3Strategy.sol#L69-L71),** the pool's liquidity is dependant on the underlyingAsset held in the cToken, therefore, the correct way to fix the problem is:
```diff
function getAvailableAssetsForWithdrawal() external view returns (uint256) {
-   return cToken.balanceOf(address(this));
+   uint256 protocolAvailableLiquidity = IERC20(asset()).balanceOf(address(cToken));
+   uint256 strategyOwnedAssed = cToken.balanceOf(address(this));
+   return protocolAvailableLiquidity > strategyOwnedAssed ? strategyOwnedAssed : protocolAvailableLiquidity;
}
```


**For the [SiloV1Strategy](https://github.com/code-423n4/2024-11-concrete/blob/main/src/strategies/Silo/SiloV1Strategy.sol#L112-L116),** the Silo contract offers a special [function to query the available liquidity of any asset deposited on the contract.](https://github.com/silo-finance/silo-core-v1/blob/master/contracts/BaseSilo.sol#L205-L207)
```diff
function getAvailableAssetsForWithdrawal() external view returns (uint256) {
-   return rToken.balanceOf(address(this));
+   uint256 protocolAvailableLiquidity = silo.liquidity(asset());
+   uint256 strategyOwnedAssed = rToken.balanceOf(address(this));
+   return protocolAvailableLiquidity > strategyOwnedAssed ? strategyOwnedAssed : protocolAvailableLiquidity;
}
```
- Make sure to add the `liquidity()` to the ISilo Interface.
[`ISiloV1.sol`](https://github.com/code-423n4/2024-11-concrete/blob/main/src/strategies/Silo/ISiloV1.sol)
```diff
/// @param _asset asset to get liquidity for
/// @return Silo liquidity
+ function liquidity(address _asset) external view returns (uint256);
```

This same function is used when withdrawing from the Silo protocol, if the requested amount to withdraw is more than the available liquidity, the execution is reverted.
- Execution Trace: `Silo.withdraw() => Silo._withdrawAsset() => Silo._getWithdrawAssetData() => Silo.liquidity()`

[`BaseSilo.sol`](https://github.com/silo-finance/silo-core-v1/blob/master/contracts/BaseSilo.sol)
```
/// @return burnedShare burned share based on `withdrawnAmount`
function _withdrawAsset(
    ...
)
    internal
    returns (uint256 withdrawnAmount, uint256 burnedShare)
{
    (uint256 assetTotalDeposits, IShareToken shareToken, uint256 availableLiquidity) =
        _getWithdrawAssetData(_asset, _collateralOnly);

    ...

    uint256 amountToTransfer = _applyLiquidationFee(_asset, withdrawnAmount, _protocolLiquidationFee);

    //@audit => If amount to withdraw is more than availableLiquidity, tx is reverted!
    if (availableLiquidity < amountToTransfer) revert NotEnoughLiquidity();

    ...
}

function _getWithdrawAssetData(address _asset, bool _collateralOnly)
    private
    view
    returns(uint256 assetTotalDeposits, IShareToken shareToken, uint256 availableLiquidity)
{
    AssetStorage storage _state = _assetStorage[_asset];

    if (_collateralOnly) {
        ...
    } else {
        //@audit-info => Strategies deposits with collateralOnly as false, so those deposits receives yield
        assetTotalDeposits = _state.totalDeposits;
        shareToken = _state.collateralToken;
        availableLiquidity = liquidity(_asset);
    }
}

//@audit-info => Available Liquidity on the Silo Contract for the specified asset!
/// @inheritdoc IBaseSilo
function liquidity(address _asset) public view returns (uint256) {
    return ERC20(_asset).balanceOf(address(this)) - _assetStorage[_asset].collateralOnlyDeposits;
}
```