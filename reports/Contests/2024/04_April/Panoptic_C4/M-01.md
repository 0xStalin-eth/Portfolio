# High Severity

## Title
Settling tokens on the incorrect chunk when executing the haircutPremia() will disrupt the real amount of settledTokens on that chunk for the rest of the open Options

## Impact
The `_settledTokens` will be updated incorrectly when executing the [`PanopticMath.haircutPremia() function`](), leading to OptionSellers that sold shorts on that chunk to earn less premium because there will be less settledTokens than what should really be.
- When the OptionSellers burn their short positions, the amount of `settledTokens` will be lower, thus, the `availablePremium` will be lower than what it really should be, causing the OptionSellers to earn fewer premia because the ratio of total owed premium to total settled premium in that chunk will be lower.

## Proof of Concept
When a liquidation is executed, the premium paid by the liquidatee is haircut, this process is done using the [`PanopticMath.haircutPremia() function`]().

In this function, the long premium of all the long options of the liquidatee is extracted from the `premiasByLeg` parameter, then, depending on the longPremium and the liquidatee's collateralRemaining is determined the haircut per each token which then is exercised from the liquidatee on the CollateralTrackers. After this, the `haircutPremia()` iterates one more time all the options across all the positions, and on each long leg it proceeds to compute the number of tokens to be settled on the `_settledTokens` variable

[**The problem is how the `chunkKey` is computed**](https://github.com/code-423n4/2024-04-panoptic/blob/main/contracts/libraries/PanopticMath.sol#L878-L884), it is always computing the chunkKey for the legIndex 0, no matter what is the current legIndex, the chunkKey is computed using the index 0.
- The chunkKey is used to settle the tokens on a specific chunkKey corresponding to the chunk where the liquidity of the option was bounded.

**As a result of updating the settledTokens on the incorrect chunkKey**, the real accounting of settledTokens for the computed chunkKey will be messed up.
- For example, if there is an option with 4 long legs that opened the long options on 4 different chunks, this error will update 4 times the settledTokens of the chunk corresponding to the leg 0, instead of updating the settledTokens on the corresponding chunk of each of the long options.

```
function haircutPremia(
    ...
) external returns (int256, int256) {
    unchecked {
        ...

        for (uint256 i = 0; i < positionIdList.length; i++) {
            ...
            for (uint256 leg = 0; leg < tokenId.countLegs(); ++leg) {
                if (tokenId.isLong(leg) == 1) {
                    mapping(bytes32 chunkKey => LeftRightUnsigned settledTokens)
                        storage _settledTokens = settledTokens;

                    //@audit-info => Computes the amount to settle
                    // calculate amounts to revoke from settled and subtract from haircut req
                    uint256 settled0 = Math.unsafeDivRoundingUp(
                        uint128(-_premiasByLeg[i][leg].rightSlot()) * uint256(haircut0),
                        uint128(longPremium.rightSlot())
                    );
                    uint256 settled1 = Math.unsafeDivRoundingUp(
                        uint128(-_premiasByLeg[i][leg].leftSlot()) * uint256(haircut1),
                        uint128(longPremium.leftSlot())
                    );

                    //@audit-issue => The computed `chunkKey` is for the values of the legIndex 0, regardless of the current legIndex!
                    bytes32 chunkKey = keccak256(
                        abi.encodePacked(
                            tokenId.strike(0),
                            tokenId.width(0),
                            tokenId.tokenType(0)
                        )
                    );

                    
                    ...

                    //@audit-issue => Updates the settled tokens on the chunkKey that was computed using the legIndex 0, regardless of the current legIndex!
                    _settledTokens[chunkKey] = _settledTokens[chunkKey].add(
                        LeftRightUnsigned.wrap(0).toRightSlot(uint128(settled0)).toLeftSlot(
                            uint128(settled1)
                        )
                    );
                }
            }
        }

        ...
    }
}
```


## Tools Used
Manual Audit

## Recommended Mitigation Steps
The recommendation to mitigate this issue is to compute the `chunkKey` using the current legIndex instead of always computing the chunkKey for the legIndex 0

```
function haircutPremia(
    ...
) external returns (int256, int256) {
    unchecked {
        
        ...

        for (uint256 i = 0; i < positionIdList.length; i++) {
            ...

            for (uint256 leg = 0; leg < tokenId.countLegs(); ++leg) {
                if (tokenId.isLong(leg) == 1) {
                  
                    ...

                    bytes32 chunkKey = keccak256(
                        abi.encodePacked(
-                           tokenId.strike(0),
-                           tokenId.width(0),
-                           tokenId.tokenType(0)
                            //@audit => Use the current legIndex!
+                           tokenId.strike(leg),
+                           tokenId.width(leg),
+                           tokenId.tokenType(leg)
                        )
                    );

                    ...

                }
            }
        }

        ...
    }
}
```