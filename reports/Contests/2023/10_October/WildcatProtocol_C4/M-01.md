# Medium Severity

## Title
Sanctioning mechanism will cause that borrowers end up paying interests and protocolFees on the sanctioned MarketTokens that were locked in the escrow.

## Impact
- If the sanctions on the lender are lifted, the Borrower will end up paying interest on all the time that the sanction on the Lender was active, and the lender will gain the benefit as any other Lender who was not sanctioned.
- The protocolFees will charge fees on all the locked MarketTokens in the escrows, so, the protocolFees will compound overtime considering MarketTokens that are sanctioned and not usable by anyone.

## Proof of Concept
- As for the Borrower that will end up paying interests on all the time that the sanction on the Lender was active:
    - The existing logic of the escrow when releasing the escrow is using the [`balanceOf()` function of the MarketToken contract](https://github.com/code-423n4/2023-10-wildcat/blob/main/src/market/WildcatMarketToken.sol#L15-L19), which returns the normalized balance of `account` with interest.
    - The logic is to compute how many asset's market tokens represent the whole markettoken's balance owned by the escrow, and then that amount is forwarded to the transfer() of the MarketToken contract, which will compute the scaleBalance for that amount, which it will effectively do the transfer of all the MarketTokens that were locked in the escrow, and then, when the lender receives the MarketTokens, it will be able to withdraw them and receive all the interests that were accrued during the whole time the lender was sanctioned!

> WildcatSanctionsEscrow.sol
```solidity
function releaseEscrow() public override {
    if (!canReleaseEscrow()) revert CanNotReleaseEscrow();

    //@audit-info => Will call the balanceOf() of the asset contract!
    uint256 amount = balance();

    //@audit-info => Transfers the tokens to the account out of this escrow!
    //@audit-info => When the asset is a MarketToken, the `amount` has already been accounted with all the interests that were accrued during all the time the Lender's account was sanctioned!
    IERC20(asset).transfer(account, amount);

    emit EscrowReleased(account, asset, amount);
}
```

```solidity
function balance() public view override returns (uint256) {
    //@audit-info => When the escrow is holding MarketContracts, will call the balanceOf() of the WildcatMarketToken contract!
    return IERC20(asset).balanceOf(address(this));
}
```

> WildcatMarketToken.sol
```solidity
function balanceOf(address account) public view virtual nonReentrantView returns (uint256) {
    (MarketState memory state, , ) = _calculateCurrentState();
    //@audit-info => By normalizing the amount will account for all the interests that were generated during all the time the Lender's account was sanctioned!
    return state.normalizeAmount(_accounts[account].scaledBalance);
}

function _transfer(address from, address to, uint256 amount) internal virtual {
    MarketState memory state = _getUpdatedState();
    uint104 scaledAmount = state.scaleAmount(amount).toUint104();

    if (scaledAmount == 0) {
      revert NullTransferAmount();
    }

    //@audit-info => Will transfer all the MarketTokens that were locked in the Escrow to the Account
    //@audit-info => Then, the Account will be able to withdraw those MarketTokens for the asset's market and will receive all the interest that was accrued during the sanctioned period

    Account memory fromAccount = _getAccount(from);
    fromAccount.scaledBalance -= scaledAmount;
    _accounts[from] = fromAccount;

    Account memory toAccount = _getAccount(to);
    toAccount.scaledBalance += scaledAmount;
    _accounts[to] = toAccount;

    _writeState(state);
    emit Transfer(from, to, amount);
  }

```

- As for the protocolFees charging fees on the MarketTokens that are locked in the escrow:
    - When the MarketTokens are transfered to the escrow to be locked during all the time that the Lender is sanctioned, the totalSupply of market tokens is not reduced, it's left untouched, this will cause that each time that [the protocolFees are computed](https://github.com/code-423n4/2023-10-wildcat/blob/main/src/libraries/FeeMath.sol#L40-L71), all the market tokens of all the sanction lenders that are locked in escrows are also considered when computing the protocol fees.

> FeeMath.sol
```solidity
  function applyProtocolFee(
    MarketState memory state,
    uint256 baseInterestRay,
    uint256 protocolFeeBips
  ) internal pure returns (uint256 protocolFee) {
    // Protocol fee is charged in addition to the interest paid to lenders.
    uint256 protocolFeeRay = protocolFeeBips.bipMul(baseInterestRay);

    //@audit-info => Here, the scaledTotalSupply of the MarketTokens is the base to compute the protocolFee
    //@audit-info => All the MarketTokens locked in escrows will also be accounted as part of the protocolFee
    protocolFee = uint256(state.scaledTotalSupply).rayMul(
      uint256(state.scaleFactor).rayMul(protocolFeeRay)
    );
    state.accruedProtocolFees = (state.accruedProtocolFees + protocolFee).toUint128();
  }
```

- The existing logic of how the sanctions are managed will cause the borrower to end up paying interests all the time that the sanctions are active, which doesn't sound very fair for the borrower nor for the rest of the Lenders if a Lender account is sanctioned it should not receive the same benefits as the rest of accounts that are not sanctioned.

## Tools Used
Manual Audit

## Recommended Mitigation Steps
- My recommendation to fix this problem is to tweak a little bit the existing logic when sanctioning lenders, instead of locking their MarketTokens, opt to lock the equivalent of the assets that the Lender's MarketToken represents at the time of the sanction (as if the lender would be queing and executing a withdrawl), this will correctly lock the equivalent of assets in the Escrow given the current scaleFactor at the time of the sanction, in this way, if the borrower ever lifts up the sanction on the Lender, the locked assets were not accruing interest, because they are not MarketTokens, they are normal tokens, so, at that time, the Lender can go ahead an get back the assets (not MarketTokens) that were locked in the Escrow, and the Borrower won't pay interests for all the time that the Lender was sanction (because the locked assets were payed based on the scaleFactor at the time of the sanction), and if the Borrower grants the permission to the Lender to become a depositor again, the Lender can deposit the received assets in the Market, and it will receive an amount of MarketTokens given the scaleFactor at that point in time.
    - In this way, the Borrower won't end up paying interests to the sanctioned Lender for all the time that the sanctions were active.
    - Also, by locking the asset tokens instead of the MarketTokens, the MarketTokens of the Lender will be burnt, so, the totalSupply will be reduced, which will end up fixing the problem of the protocolFees charging fees on all those locked MarketTokens, now, the locked assets are not MarketTokens, as a fact, those MarketTokens were burnt, so, the totalSupply was reduced, thus, the fees won't be charged on the assets locked in the escrow.