# High Severity

# Line with issues 
https://github.com/code-423n4/2024-04-panoptic/blob/main/contracts/PanopticPool.sol#L1632-L1640


## Title
Option Buyers can earn premium instead of paying for it.

## Impact
Loss of funds for PLPs because Option Buyers receive shares for the premia they owe instead of having their shares burnt to pay their debt.
This causes each share can claim fewer amounts of underlyingToken, thus, all the share's holders receive a haircut equivalent to the amount of premia that the Option Buyers should have paid.

## Proof of Concept
The [`PanopticPool.settleLongPremium() function`](https://github.com/code-423n4/2024-04-panoptic/blob/main/contracts/PanopticPool.sol#L1587-L1659) is meant to be used by OptionSellers to settle premium owed by OptionSellers as long as they are solvent.

The process to determine the amount of premium to settle is by determining [the amount of `realizedPremia` that should be paid by the OptionBuyer](https://github.com/code-423n4/2024-04-panoptic/blob/main/contracts/PanopticPool.sol#L1633-L1640). **The problem with the existing implementation is that the computed value of the `realizedPremia` is not inverted when is passed to the [`CollateralTracker.exercise() function`](https://github.com/code-423n4/2024-04-panoptic/blob/main/contracts/CollateralTracker.sol#L1043-L1089)**. 
- Because the `realizedPremia` is received as a positive value on the [`CollateralTracker.exercise() function`](https://github.com/code-423n4/2024-04-panoptic/blob/main/contracts/CollateralTracker.sol#L1043-L1089), the OptionBuyer will actually get shares minted instead of having his shares burnt to pay for the owedPremia.

This bug on the `settleLongPremium()` function can be abused by the same OptionBuyers, they can continuously call this function to receive free shares for the amount of premia they owe, apart from receiving free shares, they will also settle their owedPremia, meaning, they can hold long options and get paid for it, instead of they paying for the long options.
- Not only OptionBuyers get paid for holding long options (instead of paying for them), but the OptionSellers don't earn anything for selling options, and also the PLPs get the real value of their shares diluted.

### Code Walkthrough

<details>
<summary><b>Expand to see the Code Walkthrough</b></summary>
<br>

```
> PanopticPool.sol

function settleLongPremium(
    ...
) external {
    ...

    LeftRightUnsigned accumulatedPremium;
    {
        ...

        //@audit-info => If position is `long`, return the accumulated value as the `premiumOwed` for the whole chunk => Longs pays premia!
        (uint128 premiumAccumulator0, uint128 premiumAccumulator1) = SFPM.getAccountPremium(
            ...
        );

        //@audit-info => accumulatedPremia up to the current block. accumulated `premiumOwed` for the whole chunk!
        accumulatedPremium = LeftRightUnsigned
            .wrap(0)
            .toRightSlot(premiumAccumulator0)
            .toLeftSlot(premiumAccumulator1);

        ...

        //@audit-info => premia owed is the difference between the currentAccumulator on the SFPM and the last snapshot saved on the PP!
        accumulatedPremium = accumulatedPremium.sub(premiumAccumulatorsLast);
    }

    ...

    unchecked {
        //@audit-info => Calculate the exact owedPremia by the owner!
        // update the realized premia
        LeftRightSigned realizedPremia = LeftRightSigned
            .wrap(0)
            .toRightSlot(int128(int256((accumulatedPremium.rightSlot() * liquidity) / 2 ** 64)))
            .toLeftSlot(int128(int256((accumulatedPremium.leftSlot() * liquidity) / 2 ** 64)));


        //@audit => `realizedPremia` is a positive value.

        //@audit => Calls the exercise() and passes a positive value... Let's see what this does!
        // deduct the paid premium tokens from the owner's balance and add them to the cumulative settled token delta
        s_collateralToken0.exercise(owner, 0, 0, 0, realizedPremia.rightSlot());
        s_collateralToken1.exercise(owner, 0, 0, 0, realizedPremia.leftSlot());

        ...
    }

    ...
}

```

```
> CollateralTracker.sol

function exercise(
    ...
    //@audit-info => Value of the `realizedPremia` when settling long premium
    int128 realizedPremium
) external onlyPanopticPool returns (int128) {
    unchecked {
        ...

        //@audit => The value of the `realizedPremium` parameter is inverted.
          //@audit-info => If received as positive, it is converted to negative
          //@audit-info => If received as negative, it is converted to positive

        //@audit-info => Because `realizedPremium` was received as a positive value, `tokenToPay` will be a negative value!
        // add premium to be paid/collected on position close
        int256 tokenToPay = -realizedPremium;

        ...

        //@audit => Shares are burnt only if tokenToPay is a positive value!
        if (tokenToPay > 0) {
            // if user must pay tokens, burn them from user balance (revert if balance too small)
            ...
            _burn(optionOwner, sharesToBurn);
        
        //@audit-issue => Because `tokenToPay` is negative, the OptionBuyer will receive shares instead of getting some of his shares burnt!
        } else if (tokenToPay < 0) {
            // if user must receive tokens, mint them
            uint256 sharesToMint = convertToShares(uint256(-tokenToPay));
            _mint(optionOwner, sharesToMint);
        }

        ...
    }
}

```

</br>
</details>


## Tools Used
Manual Audit

## Recommended Mitigation Steps
Before calling the [`CollateralTracker.exercise() function`](https://github.com/code-423n4/2024-04-panoptic/blob/main/contracts/CollateralTracker.sol#L1043-L1089), make sure the value of the `realizedPremia` is inverted, in this way, the `exercise()` will receive a negative value, this will ensure that the OptionBuyer get his shares burnt instead of receiving free shares.
- Just for reference, inverting the `realizedPremia` of long options is a technique already done in the [`_getPremia()`](https://github.com/code-423n4/2024-04-panoptic/blob/main/contracts/PanopticPool.sol#L1566-L1568), this is why any other place on the codebase that determines the realizedPremia by calling the `_getPremia()` works fine!

```
function settleLongPremium(
    TokenId[] calldata positionIdList,
    address owner,
    uint256 legIndex
) external {
    ...
    ...
    ...

    unchecked {
        // update the realized premia
        LeftRightSigned realizedPremia = LeftRightSigned
            .wrap(0)
            .toRightSlot(int128(int256((accumulatedPremium.rightSlot() * liquidity) / 2 ** 64)))
            .toLeftSlot(int128(int256((accumulatedPremium.leftSlot() * liquidity) / 2 ** 64)));

        //@audit-info => invert the value of the `realizedPremia` and send this value to the exercise()!
+       LeftRightSigned owedPremia = LeftRightSigned.wrap(0).sub(realizedPremia);

        // deduct the paid premium tokens from the owner's balance and add them to the cumulative settled token delta
-       s_collateralToken0.exercise(owner, 0, 0, 0, realizedPremia.rightSlot());
-       s_collateralToken1.exercise(owner, 0, 0, 0, realizedPremia.leftSlot());

        //@audit-info => Send the inverted value of the `realizedPremia`, in this way, the `owner` will get his shares burnt, instead of getting shares minted for free!
+       s_collateralToken0.exercise(owner, 0, 0, 0, owedPremia.rightSlot());
+       s_collateralToken1.exercise(owner, 0, 0, 0, owedPremia.leftSlot());

        ...

        //@audit-ok => This is fine, when buyers pay long premium, settledTokens increases, thus, is required to use the `realizedPremia`
          //@audit-info => Remember, the `realizedPremia` was required to be inverted only to ensure that the `exercise()` would burn shares from the OptionBuyer.
        // commit the delta in settled tokens (all of the premium paid by long chunks in the tokenIds list) to storage
        s_settledTokens[chunkKey] = s_settledTokens[chunkKey].add(
            LeftRightUnsigned.wrap(uint256(LeftRightSigned.unwrap(realizedPremia)))
        );

        emit PremiumSettled(owner, tokenId, realizedPremia);
    }

    ...
    
}
```