# Medium Severity

## Lines with Problems
- https://github.com/code-423n4/2024-11-concrete/blob/main/src/vault/ConcreteMultiStrategyVault.sol#L590


## Title
`Vault.totalAssets()` is not EIP4626 compliant and is capable of breaking the rest of the vault functionalities.

## Finding description and impact
### Impact
- [`ConcreteMultiStrategyVault.totalAssets()`](https://github.com/code-423n4/2024-11-concrete/blob/main/src/vault/ConcreteMultiStrategyVault.sol#L573-L592) is not EIP4626 compliant because reverts under certain cirunstances.
- Vault's functionalities can be fully DoSed when there are more unfinalized withdraws than assets on the Vault and Vault's strategies.

### Finding Description
The [EIP4626 for the `totalAssets()` is that is MUST NOT revert](https://eips.ethereum.org/EIPS/eip-4626#totalassets:~:text=in%20the%20Vault.-,MUST%20NOT%20revert.,-%2D%20name%3A), but, the implementation on the ConcreteMultiStrategyVault has a scenario where it will revert.
- Such scenario that causes reverts is **when there are more unfinalized withdraws than assets on the Vault and Vault's strategies.** This can happen under the next conditions:
1. A withdraw is requested and queued on the WithdrawalQueue because the strategy has not enough available funds. 
2. The protocol where the strategy has its deposits gets bad debt and makes assets held by the strategy to be lower than they were.
3. **Any request after this point, and, until the protocol recovers from the bad debt would cause txs that calls [`ConcreteMultiStrategyVault.totalAssets()`](https://github.com/code-423n4/2024-11-concrete/blob/main/src/vault/ConcreteMultiStrategyVault.sol#L573-L592) to revert.**

## Proof of Concept
Add the below PoC on the [`ConcreteMultiStrategyVault.t.sol`](https://github.com/code-423n4/2024-11-concrete/blob/main/test/ConcreteMultiStrategyVault.t.sol) test file.

> Run the PoC with the next command `forge test --match-test test_totalAssetsNotEIP4626CompatiblePoC -vvvv`

```solidity
function test_totalAssetsNotEIP4626CompatiblePoC() public {
    WithdrawalQueue queue = new WithdrawalQueue(address(vault));
    vm.prank(admin);
    vault.setWithdrawalQueue(address(queue));

    vm.prank(admin);
    vault.toggleVaultIdle();

    asset.mint(hazel, type(uint256).max / 2);
    uint256 depositAmount = 10_000e18;
    vm.startPrank(hazel);
    asset.approve(address(vault), type(uint256).max);
    vault.deposit(depositAmount, hazel);
    vm.stopPrank();

    
    uint256 assetBalance = vault.getAvailableAssetsForWithdrawal();
    address protectStrategy = vault.protectStrategy();

    //@audit-info => Simulate the ProtectStrategy requested a borrow from the Vault for half the vault's assets.
    vm.startPrank(address(vault));
    asset.approve(address(vault), assetBalance / 2);
    MockERC4626Protect(protectStrategy).deposit(assetBalance / 2, address(vault));
    vm.stopPrank();

    address borrower = address(0x6969);
    MockERC4626Protect(protectStrategy).lendFunds(asset.balanceOf(protectStrategy));
    
    vm.prank(hazel);
    vault.withdraw(9_000e18);

    address feeRecipient = vault.feeRecipient();
    uint256 feeRecipientShares = vault.balanceOf(feeRecipient);
    vm.prank(feeRecipient);
    vault.redeem(feeRecipientShares);

    //@audit-info => Simulate the Protocol of the Strategy suffered a 10% of bad debt
    vm.prank(address(vault));
    asset.transfer(address(0x1010), 500e18);

    //@audit-info => As explained on the report, once `unfinalizedAmount` > total vault's asset balance, any function that calls `totalAssets()` will revert!
    vm.expectRevert();
    uint256 totalAssetsVault = vault.totalAssets();

    vm.startPrank(hazel);
    vm.expectRevert();
    vault.deposit(depositAmount, hazel);

    vm.expectRevert();
    vault.withdraw(100e18);

    vm.expectRevert();
    vault.maxMint(hazel);
    
    vm.stopPrank();
}
```

## Tools Used
- Manual Audit
- Foundry
- [EIP4626 documentation - totalAssets() MUST NOT revert](https://eips.ethereum.org/EIPS/eip-4626#totalassets:~:text=in%20the%20Vault.-,MUST%20NOT%20revert.,-%2D%20name%3A)

## Recommended Mitigation Steps
If there are more unfinalized withdraws than assets on the Vault and Vault's strategies, instead of reverting the tx, opt to **return 0**

**[ConcreteMultiStrategyVault.totalAssets()](https://github.com/code-423n4/2024-11-concrete/blob/main/src/vault/ConcreteMultiStrategyVault.sol#L573-L592)**
```diff
function totalAssets() public view override returns (uint256 total) {
    ...
-   if (total < unfinalized) revert InvalidSubstraction();
+   if (total < unfinalized) return 0;
    total -= unfinalized;
}
```