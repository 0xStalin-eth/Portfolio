# Medium Severity

## Lines with Problems
- https://github.com/code-423n4/2024-11-concrete/blob/main/src/vault/ConcreteMultiStrategyVault.sol#L618


## Title
`Vault.previewMint()` is not EIP4626 compatible because it rounds down when computing the amount of assets to mint the specified amount of shares, which returns a lower amount of assets than what are really needed to mint the specified amount of shares.

## Finding description and impact
### Impact
[`Vault.previewMint()`](https://github.com/code-423n4/2024-11-concrete/blob/main/src/vault/ConcreteMultiStrategyVault.sol#L616-L619) breaks EIP4626 compatibility because it rounds down when computing the amount of assets.

### Finding Description
[`Vault.previewMint()`](https://github.com/code-423n4/2024-11-concrete/blob/main/src/vault/ConcreteMultiStrategyVault.sol#L616-L619) rounds down when converting the specified amount of shares to assets.
- This not only breaks the EIP4626 specification, but causes integration problems because the returned value will be lower than the real amount of assets that required to mint the specified amount of shares.

**[`ConcreteMultiStrategyVault.sol`](https://github.com/code-423n4/2024-11-concrete/blob/main/src/vault/ConcreteMultiStrategyVault.sol)**
```solidity
function previewMint(uint256 shares_) public view override returns (uint256) {
    uint256 grossShares = shares_.mulDiv(MAX_BASIS_POINTS, MAX_BASIS_POINTS - fees.depositFee, Math.Rounding.Floor);
    //@audit-issue => rounding down the amount of assets to mint the specified amount of shares
    return _convertToAssets(grossShares, Math.Rounding.Floor);
}

function mint(
    uint256 shares_,
    address receiver_
) public override nonReentrant whenNotPaused returns (uint256 assets) {
    ...
    // Calculate the deposit fee in shares
    uint256 depositFee = uint256(fees.depositFee);
    uint256 feeShares = shares_.mulDiv(MAX_BASIS_POINTS, MAX_BASIS_POINTS - depositFee, Math.Rounding.Floor) -
        shares_;

    //@audit-info => When minting, the required amount of assets to mint the specified amount of shares is rounded up!
    // Calculate the total assets required for the minted shares, including fees
    assets = _convertToAssets(shares_ + feeShares, Math.Rounding.Ceil);

    ...
}
```

As demonstrated on the next PoC, an integrator calling the [`Vault.previewMint()`](https://github.com/code-423n4/2024-11-concrete/blob/main/src/vault/ConcreteMultiStrategyVault.sol#L616-L619) to know the required allowance (and assets) to mint a certain amount of shares would get their txs reverted because previewMint rounds down instead of up.

## Proof of Concept
Add the below PoC on the [`ConcreteMultiStrategyVault.t.sol`](https://github.com/code-423n4/2024-11-concrete/blob/main/test/ConcreteMultiStrategyVault.t.sol) test file.

> Run the PoC with the next command `forge test --match-test test_previewMintNotEIP4626CompatiblePoC -vvvv`

```solidity
function test_previewMintNotEIP4626CompatiblePoC() public {
    asset.mint(hazel, type(uint256).max / 2);

    uint256 sharesToMint = 10_000e18;
    uint256 depositAmount = vault.previewMint(sharesToMint);
    vm.startPrank(hazel);
    asset.approve(address(vault), depositAmount);
    //@audit-info => Expected to revert because the returned value from previewMint() was rounded down, causing the granted allowance to not be enough for the real amount of assets required to mint `sharesToMint`
    vm.expectRevert();
    vault.mint(sharesToMint, hazel);
    vm.stopPrank();
}
```

## Tools Used
- Manual Audit
- Foudry
- [EIP4626 documentation - previewMint()](https://eips.ethereum.org/EIPS/eip-4626#totalassets:~:text=MUST%20return%20as%20close%20to%20and%20no%20fewer%20than%20the%20exact%20amount%20of%20assets%20that%20would%20be%20deposited%20in%20a%20mint%20call%20in%20the%20same%20transaction.) 
- [EIP4626 documentation - rounding up when calculating the amount of underlying tokens a user has to provide to receive a certain amount of shares](https://eips.ethereum.org/EIPS/eip-4626#totalassets:~:text=it%E2%80%99s%20calculating%20the%20amount%20of%20underlying%20tokens%20a%20user%20has%20to%20provide%20to%20receive%20a%20certain%20amount%20of%20shares%2C%20it%20should%20round%20up.)

## Recommended Mitigation Steps
When converting the specified amount of shares to assets, round up instead of down.

**[`ConcreteMultiStrategyVault.previewMint()`](https://github.com/code-423n4/2024-11-concrete/blob/main/src/vault/ConcreteMultiStrategyVault.sol#L616-L619)**
```diff
function previewMint(uint256 shares_) public view override returns (uint256) {
    uint256 grossShares = shares_.mulDiv(MAX_BASIS_POINTS, MAX_BASIS_POINTS - fees.depositFee, Math.Rounding.Floor);
-   return _convertToAssets(grossShares, Math.Rounding.Floor);
+   return _convertToAssets(grossShares, Math.Rounding.Ceil);
}
```