# Medium Severity

## Title
Logic to wrap the native token using the Magnetar contract is broken, because the valAccumulator accrues twice the _action.value, which will cause the value of the `valAccumulator` to not be equals to the value of `msg.value` causing the tx to be reverted (or force users to pay more native token to not get the tx reverted)

## Impact
- Any process of the [`Magnetar::burst()`](https://github.com/Tapioca-DAO/tapioca-periph-audit/blob/023751a4e987cf7c203ab25d3abba58f7344f213/contracts/Magnetar/MagnetarV2.sol#L194-L715) that requires to wrap the native token before executing other actions will be completely unusable, **[always when the burst() executes the logic to wrap the native token](https://github.com/Tapioca-DAO/tapioca-periph-audit/blob/023751a4e987cf7c203ab25d3abba58f7344f213/contracts/Magnetar/MagnetarV2.sol#L232-L241), the valAccumulator will accrue twice the _action.value** of that action in the array of calls, and at the end of the loop iteration over the array of calls, the check to validate if `valAccumulator` equals to `msg.value` will fail and the tx will be reverted.

## Proof of Concept
- When the [`Magnetar::burst()`](https://github.com/Tapioca-DAO/tapioca-periph-audit/blob/023751a4e987cf7c203ab25d3abba58f7344f213/contracts/Magnetar/MagnetarV2.sol#L194-L715) functions are iterating over the array of calls and the current actionId matches the action to wrap a token **(`TOFT_WRAP`)**, and the wrap is made for the native token (_action.value > 0), [the valAccumulator (which is keeping track of the sum of all the _action.value) will add again the value of the current _action.value](https://github.com/Tapioca-DAO/tapioca-periph-audit/blob/023751a4e987cf7c203ab25d3abba58f7344f213/contracts/Magnetar/MagnetarV2.sol#L232-L241), **the problem is that the valAccumulator it was already updated with the value of the current _action.value** (to be precise, [at the beginning of the execution of each new call of the calls array, the valAccumulator adds up the value of the current _action.value](https://github.com/Tapioca-DAO/tapioca-periph-audit/blob/023751a4e987cf7c203ab25d3abba58f7344f213/contracts/Magnetar/MagnetarV2.sol#L214-L216)). 
  - So, **as a result of adding twice the _action.value, the valAccumulator won't match the value of the `msg.value` that was sent to execute the calls**, and the tx will be reverted.
    - And ***if the users consider this bug and send the `msg.value` to match the duplicate sum of the `_action.value`***, **the users will end up loosing the extra native token that they sent because the extra tokens were not used, they will be left in the Magnetar contract.**
```solidity
    function burst(
        Call[] calldata calls
    ) external payable returns (Result[] memory returnData) {
        uint256 valAccumulator;

        uint256 length = calls.length;
        returnData = new Result[](length);

        for (uint256 i = 0; i < length; i++) {
            Call calldata _action = calls[i];
            if (!_action.allowFailure) {
                require(
                    _action.call.length > 0,
                    string.concat(
                        "MagnetarV2: Missing call for action with index",
                        string(abi.encode(i))
                    )
                );
            }
            
            //@audit-info => valAccumulator adds up the value of _action.value at the begining of the execution of each new call!
            unchecked {
                valAccumulator += _action.value;
            }

            ...
            ...
            ...
            } else if (_action.id == TOFT_WRAP) {
                WrapData memory data = abi.decode(_action.call[4:], (WrapData));
                _checkSender(data.from);
                if (_action.value > 0) {
                  
                  //@audit-issue => Here the valAccumulator is adding again the _action.value, even though it was already added at the beggining of the execution of this action!
                    unchecked {
                        valAccumulator += _action.value;
                    }
                  
                    ITapiocaOFT(_action.target).wrapNative{
                        value: _action.value
                    }(data.to);
                } 
          ...
          ...
          
          //@audit-issue => When validating if valAccumulator matches the `msg.value`, the validation will fail because the `valAccumulator` accrues twice the value `_action.value` when wrapping the native token
          require(msg.value == valAccumulator, "MagnetarV2: value mismatch");
```


## Tools Used
Manual Audit

## Recommended Mitigation Steps
- The fix for this bug is pretty straightforward, don't accrue twice the value of _action.value when wrapping the native token.
  - It is not required to add twice the value of _action.value, it has already been accounted for at the beginning of the execution of that call.

- **Remove the line of code that makes the valAccumulator sum again the _action.value when the action is to wrap the native token**
```solidity
    function burst(
        Call[] calldata calls
    ) external payable returns (Result[] memory returnData) {
        uint256 valAccumulator;

        uint256 length = calls.length;
        returnData = new Result[](length);

        for (uint256 i = 0; i < length; i++) {
            Call calldata _action = calls[i];
            if (!_action.allowFailure) {
                require(
                    _action.call.length > 0,
                    string.concat(
                        "MagnetarV2: Missing call for action with index",
                        string(abi.encode(i))
                    )
                );
            }
            
            //@audit-info => valAccumulator adds up the value of _action.value at the beginning of the execution of each new call!
            unchecked {
                valAccumulator += _action.value;
            }

            ...
            ...
            ...
            } else if (_action.id == TOFT_WRAP) {
                WrapData memory data = abi.decode(_action.call[4:], (WrapData));
                _checkSender(data.from);
                if (_action.value > 0) {
-                   unchecked {
-                       valAccumulator += _action.value;
-                   }
                    ITapiocaOFT(_action.target).wrapNative{
                        value: _action.value
                    }(data.to);
                } 
          ...
          ...
          ...
```