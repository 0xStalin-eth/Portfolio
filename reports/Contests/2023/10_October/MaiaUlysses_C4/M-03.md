
## Title
The native token that is used to pay for the LayerZero fees will get stuck in the contracts if txs are reverted in the RootBridgeAgent contract.

## Impact
- Users won't get back the native token they paid to execute cross-chain txs if the txs are reverted in the RootBridgeAgent contract.

## Proof of Concept
When txs execution fails in the RootBridgeAgent contract, the current implementation in the RootBridgeAgent contract is to revert the tx (if a fallback was not set) that is initiated by the LayerZeroEndpoint contract in the Root environment (Arbitrum).
- If the tx is initiated by the LayerZeroEndpoint contract and is reverted in the RootBridgeAgent contract (and fallback was not defined), **the native token will be sent back to the LayerZeroEndpoint contract and will be left there, it is not refunded to the refundee user.**
- The execution flow in the RootBridgeAgent contract goes from the `lzReceive()` function to the `lzReceiveNonBlocking()` function to the `_execute()` function where if the tx fails and no fallback is set, the whole tx will be reverted if the call to the BridgeAgentExecutor contract fails.

Let's do a walkthrough the code of the [`LayerZeroEndpoint contract`](https://github.com/LayerZero-Labs/LayerZero/blob/main/contracts/Endpoint.sol#L118-L124) to visualize what happens to the txs when they fail.
> Endpoint.sol (LayerZero contract)
```solidity
    function receivePayload(uint16 _srcChainId, bytes calldata _srcAddress, address _dstAddress, uint64 _nonce, uint _gasLimit, bytes calldata _payload) external override receiveNonReentrant {
        ...

        //@audit-info => LayerZeroEndpoint contract sends the specified _gasLimit to the dstContract (RootBridgeAgent contract)
        try ILayerZeroReceiver(_dstAddress).lzReceive{gas: _gasLimit}(_srcChainId, _srcAddress, _nonce, _payload) {
            // success, do nothing, end of the message delivery
        } catch (bytes memory reason) {
            //@audit-info => If the execution of the lzReceive() function in the dstContract fails, the Endpoint contract doesn't refund the native tokens to the refundee address
            // revert nonce if any uncaught errors/exceptions if the ua chooses the blocking mode
            storedPayload[_srcChainId][_srcAddress] = StoredPayload(uint64(_payload.length), _dstAddress, keccak256(_payload));
            emit PayloadStored(_srcChainId, _srcAddress, _dstAddress, _nonce, _payload, reason);
        }
    }
```

Let's now visualize the [`_execute()` functions in the RootBridgeAgent contract](https://github.com/code-423n4/2023-09-maia/blob/main/src/RootBridgeAgent.sol#L749-L794)
```solidity
  function _execute(uint256 _depositNonce, bytes memory _calldata, uint16 _srcChainId) private {
      //Update tx state as executed
      executionState[_srcChainId][_depositNonce] = STATUS_DONE;

      //Try to execute the remote request
      (bool success,) = bridgeAgentExecutorAddress.call{value: address(this).balance}(_calldata);

      //@audit-info => If execution fails in the RootBridgeAgentExecutor contract, the whole tx is reverted, thus, the native token is sent back to the LayerZeroEndpoint contract, but it is not sent back to the refundee address
      // No fallback is requested revert allowing for retry.
      if (!success) revert ExecutionFailure();
  }
```

```solidity
function _execute(
    bool _hasFallbackToggled,
    uint32 _depositNonce,
    address _refundee,
    bytes memory _calldata,
    uint16 _srcChainId
) private {
    //Update tx state as executed
    executionState[_srcChainId][_depositNonce] = STATUS_DONE;

    //Try to execute the remote request
    (bool success,) = bridgeAgentExecutorAddress.call{value: address(this).balance}(_calldata);

    //Update tx state if execution failed
    if (!success) {
        //Read the fallback flag.
        if (_hasFallbackToggled) {
            // Update tx state as retrieve only
            executionState[_srcChainId][_depositNonce] = STATUS_RETRIEVE;
            // Perform the fallback call
            _performFallbackCall(payable(_refundee), _depositNonce, _srcChainId);
        } else {
            //@audit-info => If execution fails in the RootBridgeAgentExecutor contract and no fallback was set, the whole tx is reverted, thus, the native token is sent back to the LayerZeroEndpoint contract, but it is not sent back to the refundee address
            // No fallback is requested revert allowing for retry.
            revert ExecutionFailure();
        }
    }
}
```

- The purpose of reverting the tx is to prevent the nonce from the srcChain from being marked as STATUS_DONE in the executionState mapping, so the tx can be retried or retrieved and redeemed from the Source Branch
  - But, the **current implementation is flawless and will cause users to never get back the unspent native token.**

### Coded PoC
- I coded a PoC to demonstrate the problem I'm reporting, using the [`RootForkTest.t.sol` test file](https://github.com/code-423n4/2023-09-maia/blob/main/test/ulysses-omnichain/RootForkTest.t.sol) as the base to reproduce this PoC:
  - Make sure to add the new `testAddGlobalTokenNoReturnsGasPoC()` function.

- For this PoC I'm forcefully causing the tx to be reverted by trying to add a globalToken that has already been added, but **the core issue is the same regardless of what causes the tx to be reverted, the user won't get back the unspent native token that was paid for the execution of the crosschain message.**

```solidity
function testAddGlobalTokenNoReturnsGasPoC() public {
    //Add Local Token from Avax
    testAddLocalToken();

    //Switch Chain and Execute Incoming Packets
    switchToLzChain(avaxChainId);

    vm.deal(address(this), 1000 ether);

    GasParams[3] memory gasParams =
        [GasParams(15_000_000, 0.1 ether), GasParams(2_000_000, 3 ether), GasParams(200_000, 0)];
        // [GasParams(15_000_000, 0.1 ether), GasParams(1000, 3 ether), GasParams(200_000, 0)];

    //@audit-info => User1 adds the avaxGlobalToken first
    avaxCoreRouter.addGlobalToken{value: 1000 ether}(newAvaxAssetGlobalAddress, ftmChainId, gasParams);

    //Switch Chain and Execute Incoming Packets
    switchToLzChain(rootChainId);
    //Switch Chain and Execute Incoming Packets
    switchToLzChain(ftmChainId);
    //Switch Chain and Execute Incoming Packets
    switchToLzChain(rootChainId);

    newAvaxAssetFtmLocalToken = rootPort.getLocalTokenFromGlobal(newAvaxAssetGlobalAddress, ftmChainId);

    require(newAvaxAssetFtmLocalToken != address(0), "Failed is zero");

    console2.log("New Local: ", newAvaxAssetFtmLocalToken);

    require(
        rootPort.getLocalTokenFromGlobal(newAvaxAssetGlobalAddress, ftmChainId) == newAvaxAssetFtmLocalToken,
        "Token should be added"
    );

    require(
        rootPort.getUnderlyingTokenFromLocal(newAvaxAssetFtmLocalToken, ftmChainId) == address(0),
        "Underlying should not be added"
    );

    uint256 rootBridgeAgentBalanceBefore = address(coreRootBridgeAgent).balance;
    console2.log("RootBridge balance before: ", rootBridgeAgentBalanceBefore);

    //Switch Chain and Execute Incoming Packets
    switchToLzChain(avaxChainId);

    //@audit-info => User2 adds the avaxGlobalToken after, tx is reverted and native tokens is not refunded
    address user2 = vm.addr(10);
    vm.label(user2, "User2");
    vm.deal(user2, 1000 ether);
    vm.prank(user2);
    console2.log("user2 balance before: ", user2.balance);
    avaxCoreRouter.addGlobalToken{value: 1000 ether}(newAvaxAssetGlobalAddress, ftmChainId, [GasParams(15_000_000, 0.1 ether), GasParams(2_000_000, 3 ether), GasParams(200_000, 0)]);
    //Switch Chain and Execute Incoming Packets
    switchToLzChain(rootChainId);
    //Switch Chain and Execute Incoming Packets
    switchToLzChain(avaxChainId);
    
    console2.log("user2 balance after: ", user2.balance);

    switchToLzChain(rootChainId);
    uint256 rootBridgeAgentBalanceAfter = address(coreRootBridgeAgent).balance;
    console2.log("RootBridge balance after: ", rootBridgeAgentBalanceAfter);

    assertTrue(rootBridgeAgentBalanceAfter > rootBridgeAgentBalanceBefore);
}
```

- Now everything is ready to run the test and analyze the output:
> forge test --mc RootForkTest --match-test testAddGlobalTokenNoReturnsGasPoC -vvvv
- By analyzing the output below, we can see that the unspent native tokens are left in the dst contract instead of being returned back to the user (See the next paragraph below the output to understand this behavior and a comparisson against what will happen in production, where the LayerZeroEndpoint contract uses the UltraLightNodeV2 library)
```
    ├─ [0] console::log(Sending native token airdrop...) [staticcall]
    │   └─ ← ()
    ├─ [0] VM::deal(RootForkTest: [0xBb2180ebd78ce97360503434eD37fcf4a1Df61c3], 1000000000000000000000 [1e21])
    │   └─ ← ()
    ├─ [55] RootBridgeAgent::receive{value: 1000000000000000000000}()
    │   └─ ← ()
    ├─ [0] VM::deal(0x4D73AdB72bC3DD368966edD0f0b2148401A178E2, 150000000000 [1.5e11])
    │   └─ ← ()
    ├─ [0] VM::prank(0x4D73AdB72bC3DD368966edD0f0b2148401A178E2)
    │   └─ ← ()
    ├─ [51462] 0x3c2269811836af69497E5F486A85D7316753cf62::receivePayload(106, 0xcb3b263f002b8888f712ba46ebf1302e1294608cd5f23f71c860c0c261da56585d3a24c6dbfacaa1, RootBridgeAgent: [0xCB3b263f002b8888f712BA46EbF1302e1294608c], 4, 15000000 [1.5e7], 0x0100000004010000000000000000000000004cceba2d7d2b4fdce4304d3e09a1fea9fbeb1528000000000000000000000000a475b806ead7ebb851589f958fd1038fcbeec1c1000000000000000000000000000000000000000000000000000000000000007000000000000000000000000000000000000000000000000000000000001e848000000000000000000000000000000000000000000000000029a2241af62c00000000000000000000000000000000000000000000000000000000000000030d400000000000000000000000000000000000000000000000000000000000000000)
    │   ├─ [48205] RootBridgeAgent::lzReceive(106, 0xcb3b263f002b8888f712ba46ebf1302e1294608cd5f23f71c860c0c261da56585d3a24c6dbfacaa1, 4, 0x0100000004010000000000000000000000004cceba2d7d2b4fdce4304d3e09a1fea9fbeb1528000000000000000000000000a475b806ead7ebb851589f958fd1038fcbeec1c1000000000000000000000000000000000000000000000000000000000000007000000000000000000000000000000000000000000000000000000000001e848000000000000000000000000000000000000000000000000029a2241af62c00000000000000000000000000000000000000000000000000000000000000030d400000000000000000000000000000000000000000000000000000000000000000)
    │   │   ├─ [45896] RootBridgeAgent::lzReceiveNonBlocking(0x3c2269811836af69497E5F486A85D7316753cf62, 106, 0xcb3b263f002b8888f712ba46ebf1302e1294608cd5f23f71c860c0c261da56585d3a24c6dbfacaa1, 0x0100000004010000000000000000000000004cceba2d7d2b4fdce4304d3e09a1fea9fbeb1528000000000000000000000000a475b806ead7ebb851589f958fd1038fcbeec1c1000000000000000000000000000000000000000000000000000000000000007000000000000000000000000000000000000000000000000000000000001e848000000000000000000000000000000000000000000000000029a2241af62c00000000000000000000000000000000000000000000000000000000000000030d400000000000000000000000000000000000000000000000000000000000000000)
    │   │   │   ├─ [12638] RootBridgeAgentExecutor::executeNoDeposit{value: 1000000000000000000000}(CoreRootRouter: [0x32e03c6b1b446C7a4381852A82F7Cd4BEB00B17d], 0x0100000004010000000000000000000000004cceba2d7d2b4fdce4304d3e09a1fea9fbeb1528000000000000000000000000a475b806ead7ebb851589f958fd1038fcbeec1c1000000000000000000000000000000000000000000000000000000000000007000000000000000000000000000000000000000000000000000000000001e848000000000000000000000000000000000000000000000000029a2241af62c00000000000000000000000000000000000000000000000000000000000000030d400000000000000000000000000000000000000000000000000000000000000000, 106)
    │   │   │   │   ├─ [4395] CoreRootRouter::execute{value: 1000000000000000000000}(0x010000000000000000000000004cceba2d7d2b4fdce4304d3e09a1fea9fbeb1528000000000000000000000000a475b806ead7ebb851589f958fd1038fcbeec1c1000000000000000000000000000000000000000000000000000000000000007000000000000000000000000000000000000000000000000000000000001e848000000000000000000000000000000000000000000000000029a2241af62c00000000000000000000000000000000000000000000000000000000000000030d400000000000000000000000000000000000000000000000000000000000000000, 106)
    │   │   │   │   │   ├─ [605] RootPort::isGlobalAddress(ERC20hTokenRoot: [0xa475b806eaD7ebB851589f958fD1038fcbEEC1c1]) [staticcall]
    │   │   │   │   │   │   └─ ← true
    │   │   │   │   │   ├─ [742] RootPort::isGlobalToken(ERC20hTokenRoot: [0xa475b806eaD7ebB851589f958fD1038fcbEEC1c1], 112) [staticcall]
    │   │   │   │   │   │   └─ ← true
    │   │   │   │   │   └─ ← "TokenAlreadyAdded()"
    │   │   │   │   └─ ← "TokenAlreadyAdded()"
    │   │   │   └─ ← "ExecutionFailure()"
    │   │   └─ ← ()
    │   └─ ← ()
    ├─ [0] VM::selectFork(0)
    │   └─ ← ()
    ├─ [0] VM::getRecordedLogs()
    │   └─ ← []
    ├─ [0] console::log(Events caugth:, 0) [staticcall]
    │   └─ ← ()
    ├─ [0] VM::resumeGasMetering()
    │   └─ ← ()
    ├─ [0] console::log(user2 balance after: , 978780807362276784517 [9.787e20]) [staticcall]
    │   └─ ← ()
    ├─ [0] VM::selectFork(1)
    │   └─ ← ()
    ├─ [0] VM::getRecordedLogs()
    │   └─ ← []
    ├─ [0] console::log(Events caugth:, 0) [staticcall]
    │   └─ ← ()
    ├─ [0] VM::resumeGasMetering()
    │   └─ ← ()
    ├─ [0] console::log(RootBridge balance after: , 1000000000000000000000 [1e21]) [staticcall]
    │   └─ ← ()
    └─ ← ()

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 2.37s
```

![Unspent Native Tokens Lost PoC](https://res.cloudinary.com/djt3zbrr3/image/upload/v1696472520/MaiaUlysses/unspentNativeTokensLostPoC.jpg)

- The difference between the logic implemented on the [`LzForkTest.t.sol` file](https://github.com/code-423n4/2023-09-maia/blob/main/test/test-utils/fork/LzForkTest.t.sol) that is executed in these tests and the actual library that is used by the LayerZeroEndpoint contract is that, in the LzForkTest.t.sol implementation, [the native tokens are airdroped in a separate tx before actually running the payload on the dstContract.lzReceive() function](https://github.com/code-423n4/2023-09-maia/blob/main/test/test-utils/fork/LzForkTest.t.sol#L522-L524), that's why if the tx is reverted in the dstContract, the native tokens are left in the dstContract itself, but in production, the tokens will be left in the LayerZero contract, regardless of this difference, **the user is not getting back any of the unspent native tokens.**
> LzForkTest.t.sol contract
```solidity
function executePacket(Packet memory packet) public {
    ...
    //@audit-info => handleAdapterParams() airdrops the native tokens to the dstContract in a separate tx
    uint256 gasLimit = handleAdapterParams(adapterParams);

    // Acquire gas, Prank into Library and Mock LayerZeroEndpoint.receivePayload call
    vm.deal(receivingLibrary, gasLimit * tx.gasprice);
    vm.prank(receivingLibrary);
    ILayerZeroEndpoint(receivingEndpoint).receivePayload(
        packet.originLzChainId,
        abi.encodePacked(packet.destinationUA, packet.originUA), //original
        // abi.encodePacked(packet.originUA, packet.destinationUA), //fixed
        // abi.encodePacked(address(1), address(1)),
        packet.destinationUA,
        packet.nonce,
        gasLimit,
        packet.payload
    );
}

function handleAdapterParams(AdapterParams memory params) internal returns (uint256 gasLimit) {
    ...
    ...
    ...
            //@audit-info => Tokens are airdroped to the dstContract!
            console2.log("Sending native token airdrop...");
            deal(address(this), nativeForDst * tx.gasprice);
            addressOnDst.call{value: nativeForDst * tx.gasprice}("");
    ...
    ...
    ...
}
```


## Tools Used
Manual Audit & [LayerZeroEndpoint Message Library](https://github.com/LayerZero-Labs/LayerZero/blob/main/contracts/UltraLightNodeV2.sol)

## Recommended Mitigation Steps
- The recommendation is to implement a mechanism to refund the received ETH from the LayerZeroEndpoint contract to the refundee user in case the execution in the RootBridgeAgent executor fails, and, instead of forcefully reverting the tx in the RootBridgeAgent contract, first, either refund or credit the total of the unspent ETH to the refundee, and secondly, mark the nonce on the srcChain in the executionState mapping as STATUS_READY, so the nonce can be retried or retrieved and retried.
- By making sure that the nonce is set as STATUS_READY in case the tx execution in the RootBridgeAgent executor fails and no fallback mechanism is set, the contracts will allow that users can retry the same nonce or retrieve and redeem them, and the users will get back the unspent native token of the tx that failed.

- Apply the below changes to the _execute() functions in the RootBridgeAgent contract

```solidity
function _execute(uint256 _depositNonce, bytes memory _calldata, uint16 _srcChainId) private {
    //Update tx state as executed
    executionState[_srcChainId][_depositNonce] = STATUS_DONE;

    //Try to execute the remote request
    (bool success,) = bridgeAgentExecutorAddress.call{value: address(this).balance}(_calldata);

    // No fallback is requested revert allowing for retry.
-     if (!success) revert ExecutionFailure();
+     if (!success) {
+     executionState[_srcChainId][_depositNonce] = STATUS_READY;
      //@audit-info => Make sure that the refundee gets back the unspent ETH
+     <decodeRefundeeFromCalldata>.call{value: address(this).balance}();
    }
}

function _execute(
    bool _hasFallbackToggled,
    uint32 _depositNonce,
    address _refundee,
    bytes memory _calldata,
    uint16 _srcChainId
) private {
    //@audit-ok => Sets the [_srcChainId][nonce] as STATUS_DONE, it can't be re-executed!
    //Update tx state as executed
    executionState[_srcChainId][_depositNonce] = STATUS_DONE;

    //Try to execute the remote request
    (bool success,) = bridgeAgentExecutorAddress.call{value: address(this).balance}(_calldata);

    //Update tx state if execution failed
    if (!success) {
        //Read the fallback flag.
        if (_hasFallbackToggled) {
            // Update tx state as retrieve only
            executionState[_srcChainId][_depositNonce] = STATUS_RETRIEVE;
            // Perform the fallback call
            _performFallbackCall(payable(_refundee), _depositNonce, _srcChainId);
        } else {
-           // No fallback is requested revert allowing for retry.
-           revert ExecutionFailure();
+           executionState[_srcChainId][_depositNonce] = STATUS_READY;
            //@audit-info => Make sure that the refundee gets back the unspent ETH
+           <decodeRefundeeFromCalldata>.call{value: address(this).balance}();
        }
    }
}
```