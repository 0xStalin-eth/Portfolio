# High Severity

## Title
CurveTokenSubject can set the referralFeeDestination's address to a contract that either can't receive ETH or returns bombs to DoS any buy/sell operation.

## Impact
CurveTokenSubjects can disrupt the purchase and selling of their curveTokens by updating/setting the address of the referralFeeDestination to a contract that either can't receive ETH or returns bombs to DoS any buy/sell operation, this will cause all holder of the tokenSubject's curveToken can't sell their curveTokens in exchange for ETH.

## Proof of Concept
The tokenSubjects are capable of setting the address of the `referralFeeDestination` variable at any time to any address they want to. The tokenSubject needs to call the [`Curves::setReferralFeeDestination() function`](https://github.com/code-423n4/2024-01-curves/blob/main/contracts/Curves.sol#L155-L160) and pass the new address that will be assigned to the `referralFeeDestination` variable.

When users buy or sell curveTokens, the [`Curves::_transferFees() function`](https://github.com/code-423n4/2024-01-curves/blob/main/contracts/Curves.sol#L218-L261) computes the amount of fees that the referralFeeDestination address should receive, and it executes a low-level call to the address that the `referralFeeDestination` variable is pointing to.
- **The problem is** ***if the referralFeeDestination variable points to a contract that can't receive ETH or that returns a bomb*** (A return bomb is a large bytes array that expands the memory so much that any attempt to execute the transaction will lead to an out-of-gas exception.), the whole user's operation will be reverted because of this call.

> Curves.sol
```solidity

    function setReferralFeeDestination(
        address curvesTokenSubject,
        address referralFeeDestination_
    ) public onlyTokenSubject(curvesTokenSubject) {
        //@audit-info => subjectToken can update at any time this address and point it to any address he wants to
        referralFeeDestination[curvesTokenSubject] = referralFeeDestination_;
    }

    function _transferFees(
        address curvesTokenSubject,
        bool isBuy,
        uint256 price,
        uint256 amount,
        uint256 supply
    ) internal {
        ...
          ...

          {
              //@audit-issue => If referralFeeDestination points to a contract that either can't receive ETH or returns bombs, the whole tx will be reverted!
              (bool success3, ) = referralDefined
                  ? referralFeeDestination[curvesTokenSubject].call{value: referralFee}("")
                  : (true, bytes(""));
              if (!success3) revert CannotSendFunds();
          }

          ...
        ...
    }

    function _buyCurvesToken(address curvesTokenSubject, uint256 amount) internal {
        ...
        //@audit-issue => If referralFeeDestination points to a contract that either can't receive ETH or returns bombs, buy tx will revert
        _transferFees(curvesTokenSubject, true, price, amount, supply);

        ...
    }

    function sellCurvesToken(address curvesTokenSubject, uint256 amount) public {
        ...
        //@audit-issue => If referralFeeDestination points to a contract that either can't receive ETH or returns bombs, sell tx will revert
        _transferFees(curvesTokenSubject, false, price, amount, supply);
    }
```

A subjectToken can enable the purchasing of his curvesTokens, at this time, the `referralFeeDestination` can not be set at all (and the referral fees will be given to the protocol), or it could be pointing to a correct contract or to an EOA. After some time passes and users have bought the curvesTokens of the subjectToken, for X|Y reason, the subjectToken acts maliciously against all the holders of his curveToken and updates the `referralFeeDestination` variable to point to a contract that can't receive ETH, or that returns a bomb

A medium severity it's reasonable because the likelihood is something between medium and low since it requires the tokenSubject to be willing to ruin his reputation, but the impact is high since the users won't be able to sell their curveTokens for ETH, thus, all the ETH that belongs to those holders will get stuck in the Curves contract.

## Tools Used
Manual Audit

## Recommended Mitigation Steps
The most straightforward mitigation to prevent the DoS caused by any of the two reasons mentioned in this report it is by implementing a pull pattern for the referralFees.
Instead of using a push pattern where the Curves contract pushes the fees to the `referralFeeDestination` address, keep track of how much fees the `referralFeeDestination` has earned, and create a function that will allow the `referralFeeDestination` address or the tokenSubject itself to reclaim those fees. In this way, even if the tokenSubject sets the `referralFeeDestination` variable to a malicious contract, the buy and sell operations won't be disrupted.