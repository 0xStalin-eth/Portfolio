# Medium Severity

## Title
The logic to compute the DebtCeiling can erroneously DoS transactions that involves decrementing the weight in a gauge

## Impact
- Operations such as [applying gauge losses](https://github.com/code-423n4/2023-12-ethereumcreditguild/blob/main/src/tokens/GuildToken.sol#L133-L161), [transferring GAUGE tokens](https://github.com/code-423n4/2023-12-ethereumcreditguild/blob/main/src/tokens/GuildToken.sol#L288-L300), [unstaking CreditTokens](https://github.com/code-423n4/2023-12-ethereumcreditguild/blob/main/src/loan/SurplusGuildMinter.sol#L158-L212), and [updating the mintRatio in the SurplusGuildMinter contract](https://github.com/code-423n4/2023-12-ethereumcreditguild/blob/main/src/loan/SurplusGuildMinter.sol#L293-L315) can erroneously be DoSed by a buggy logic in the way of how the DebtCeiling is computed when decrementing weight from a gauge.
- Apart from erroneously DoS the above operations, a side effect of this bug is that loans would need to be called, which means that the term would need to be offboarded, when in reality there is no need to do it.


## Proof of Concept
- Any operation that as part of its execution needs to decrement the weight on a gauge will call the [`ERC20Gauge::decrementGauge() function`](https://github.com/code-423n4/2023-12-ethereumcreditguild/blob/main/src/tokens/ERC20Gauges.sol#L301-L312), this function as part of its execution will internally run the [`GuildToken::_decrementGaugeWeight() function`](https://github.com/code-423n4/2023-12-ethereumcreditguild/blob/main/src/tokens/GuildToken.sol#L207-L234), and this function will call the [`LendingTerm::debtCeiling()` function](https://github.com/code-423n4/2023-12-ethereumcreditguild/blob/main/src/loan/LendingTerm.sol#L270-L331) and will forward the amount of weight that is being decremented so that the debtCeiling() can compute the new weight after decreasing the specified weight, now, in the LendingTerm::debtCeiling(), a number of different computations are executed throughout the execution of this function (will see them more in detail in a code walkthrough), but basically, the function will validate that the issuance doesn't exceed the debtCeilingBefore, which this variable represents the debtCeiling after decrementing the specified weight and including the gaugeWeightTolerance, if the debtCeilingBefore is larger than the issuance, the function will proceed to check if the gague weight is above 100%, that would means that the gaugeWeightTolerance is being used, thus, the debtCeiling would be the min value between the harcap and the creditMinterBuffer, but, if the gaugeWeight is under the gaugeWeightTolerance, that means that the debtCeiling must be the issuance + maxBorrow because the gaugeWeight is by far bigger than the existing issuance that it it not out of its normal bounds (no need to use the gaugeWeightTolerance), so, if the execution flow reached the point where the debtCeiling is computed by the issuance + maxBorrow it means that decrementing the specified weight from the gauge won't cause that the gaugeWeight can't cover the existing issuance. **The problem** is that the debtCeiling() will still compare if the creditMinterBuffer is < than the final value of the debtCeiling, if so, it will return the value of the creditMinterBuffer, (will do the same for the hardcap), and, only if the final value of the debtCeiling is < than the creditMinterBuffer and the hardCap variables, only then, the debtCeiling() would actually return the final value that was computed for the debtCeiling that correctly reflects the debtCeiling after decrementing the specified weight.
  - The above explanation of the logic about how the debtCeiling is computed is totally correct in the context when a new borrow is trying to be opened, and the borrow function requests the existing debtCeiling, if the creditMinterBuffer or if the hardCap are lower than the value of the debtCeiling that is computed based on the gaugeWeight, then, the debtCeiling for that borrow should be either the creditMinterBuffer or the hardcap, ***but***, **when decrementing the gauge's weight**, if the debtCeiling is returned as the value of the creditMinter or the hardCap, even though the debtCeiling computed based on the new gaugeWeight is larger than the existing issuance, this will cause that the [`GuildToken::_decrementGaugeWeight() function`](https://github.com/code-423n4/2023-12-ethereumcreditguild/blob/main/src/tokens/GuildToken.sol#L207-L234) reverts because there is a check that blows up the execution if the existing issance surpasses the new debtCeiling after decrementing the weight from the gauge. Let's run some number for this to make more sense.

For example, when a loss is being applied to a user (by running the applyGaugeLoss()), or when users are transferring their tokens, and the _decrementWeightUntilFree() is executed.
- let's suppose the existing gauge's weight is 1k in total, which that computes a ~1.2k of debtCeiling.
- The user's weight in this gauge is 200. That means that after decreasing this weight from the total gauge's weight, the gauge weight now would be 800, which could compute ~1k for the new debtCeiling.
- Let's assume that the existing issuance is 900 CreditTokens, that means that the new debtCeiling (after deducting the amount of weight that the user wants to remove from the gauge) can cover that issuance, buuuut, what if the current creditMinterBuffer is at 400, because recently some borrows were recently opened.... The debtCeiling() will return that the debtCeiling() is 400, which is correct in the context when asking for new borrows, but, when reducing the user's gauge weight, this would be interpreted as if because the user removed his 200 votes from the gauge, now the debtCeiling is lower than the issuance, which that will cause the tx to be reverted and won't allow the execution to decrease the user weight, thus, the loss won't be applied or the transfer of tokens won't be possible. The only solution for this is to call loans to increase the creditMinterBuffer until it grows bigger than the real debtCeiling computed by the gauge's weight

### Code Walkthrough


> GuildToken.sol
```solidity
function _decrementGaugeWeight(
    address user,
    address gauge,
    uint256 weight
) internal override {
    ...

    //@audit-info => If the debtCeilingAfterDecrement is < than the issuance, it will be required to call some loans, some debt shall be repaid
    // check if gauge is currently using its allocated debt ceiling.
    // To decrement gauge weight, guild holders might have to call loans if the debt ceiling is used.
    uint256 issuance = LendingTerm(gauge).issuance();
    if (issuance != 0) {
        //@audit-info => Will compute the new debtCeiling, and will forward the amount of weight that is being decremented
        uint256 debtCeilingAfterDecrement = LendingTerm(gauge).debtCeiling(-int256(weight));
        //@audit-info => Reverts if the new debtCeiling can't cover the existing issuance
        require(
            issuance <= debtCeilingAfterDecrement,
            "GuildToken: debt ceiling used"
        );
    }

    ...
}
```

> LendingTerm.sol

```solidity
function debtCeiling(
    int256 gaugeWeightDelta
) public view returns (uint256) {
  address _guildToken = refs.guildToken; // cached SLOAD
  //@audit-info => The current total gauge's votes!
  uint256 gaugeWeight = GuildToken(_guildToken).getGaugeWeight(
      address(this)
  );
  //@audit-info => Computes the gaugeWeight after adding/reducing the value of the received parameter!
      //@audit-info => When decrementing the gague weight, gaugeWeightDelta will be a negative value!
  gaugeWeight = uint256(int256(gaugeWeight) + gaugeWeightDelta);
  
  ....
  ....

  //@audit-info => Computes the toleratedGaugeWeight using the new value of the gaugeWeight
  uint256 toleratedGaugeWeight = (gaugeWeight * gaugeWeightTolerance) /
      1e18;
  //@audit-info => The debtCeiling with the new value of gaugeWeight but without considering the otherGaugesWeight!
  uint256 debtCeilingBefore = (totalBorrowedCredit *
      toleratedGaugeWeight) / totalWeight;

  //@audit-info => If issuance exceeds the debtCeilingBefore, no more borrows!
      //@audit-info => If a gauge is decrementing gauge, this means that some loans must be repaid or called!
  //@audit-info => If issuance > debtCeilingBefore, when removing weight, it means the weight can't be removed, the issuance will exceed the debt ceiling because of decreasing the weight!
  if (_issuance >= debtCeilingBefore) {
      return debtCeilingBefore; // no more borrows allowed
  }

  //@audit-info => If the debtCeilingBefore is > issuance it means that decrementing the weight from the gauge is still larger than the existing issuance 
  uint256 remainingDebtCeiling = debtCeilingBefore - _issuance; // always >0
  if (toleratedGaugeWeight >= totalWeight) {
      // if the gauge weight is above 100% when we include tolerance,
      // the gauge relative debt ceilings are not constraining.
      return
          _hardCap < creditMinterBuffer ? _hardCap : creditMinterBuffer;
  }
  uint256 otherGaugesWeight = totalWeight - toleratedGaugeWeight; // always >0

  uint256 maxBorrow = (remainingDebtCeiling * totalWeight) /
      otherGaugesWeight;
  //@audit-info => The new debt ceiling computed considering the reduction of the user's weight from the total gauge's weight
  uint256 _debtCeiling = _issuance + maxBorrow;

  //@audit-issue => if creditMinterBuffer < _debtCeiling, the debtCeiling is the creditMinterBuffer, even though the gauge has many votes and the value of the debtCeiling is greater, the creditMinterBuffer will determine the debtCeliling
  //@audit-issue => creditMinterBuffer is a value that changes a lot over time, it depends on new borrows, loans being repaid, and auctions being completed. The buffer is also refilled over time by itself if it's not continuously depleted!
  // return min(creditMinterBuffer, hardCap, debtCeiling)

  //@audit-issue => If the creditMinterBuffer is less than the new value of the _debtCeiling, the returned value will be creditMinterBuffer, which that value doesn't represent the change of the user reducing his weight from the gauge
  if (creditMinterBuffer < _debtCeiling) {
      return creditMinterBuffer;
  }
  if (_hardCap < _debtCeiling) {
      return _hardCap;
  }
  
  //@audit-info => Only if the execution reaches this point, the returned value will be the real debtCeiling computed using the gauge's weight without the user's weight that is being reduced
  return _debtCeiling;
}
```


## Tools Used
Manual Audit

## Recommended Mitigation Steps
- The most straightforward mitigation for this issue is to add a bool variable as a parameter to the [`LendingTerm::debtCeiling()` function](https://github.com/code-423n4/2023-12-ethereumcreditguild/blob/main/src/loan/LendingTerm.sol#L270-L331), and send the bool variable accordingly depending on which function calls it. For example, if the debtCeiling() is called when taking a borrow, set the value of the bool variable as false, and when the debtCeiling() is called by the _decrementGauge() set it to true.
  - Make sure to update accordingly all the functions that calls the debtCeiling()

```solidity
function debtCeiling(
    int256 gaugeWeightDelta,
+   bool decrementingGauge    
) public view returns (uint256) {
    ...
    ...
    ...

    uint256 _debtCeiling = _issuance + maxBorrow;

    //@audit-info => The new bool variable plays its role at this point, once the new debtCeiling that is computed using the gauge's weight after decrementing the specified weight
+   if(decrementingGauge) return _debtCeiling;

    //@audit-info => If decrementingGauge variable is false, it means that the debtCeiling() was called when asking for a borrow, thus, the debtCeiling in these cases could be the current creditMinterBuffer or the hardCap!

    // return min(creditMinterBuffer, hardCap, debtCeiling)
    if (creditMinterBuffer < _debtCeiling) {
        return creditMinterBuffer;
    }
    if (_hardCap < _debtCeiling) {
        return _hardCap;
    }
    return _debtCeiling;
}
```