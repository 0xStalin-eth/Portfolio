# High Severity

## Lines with Problems
- https://github.com/code-423n4/2024-12-secondswap/blob/main/contracts/SecondSwap_StepVesting.sol#L230

## Title
Incorrect calculation of the new releaseRate for the grantor when transfering vested tokens.

## Finding description and impact
### Impact
The incorrect new releaseRate set for the grantor when transfering vestedTokens causes the grantor to be able to claim more tokens on each step based on the new totalAmount owned by the grantor.

### Finding Description
When transfering vested tokens from one account to another, the grantor (account where tokens are taken from) gets the amount of tokens being transferred deducted from its totalAmount, and because of the new totalAmount, a new releaseRate needs to be computed to determine how many vested tokens can the grantor claim from that point onwards.
- **The problem is the formula used to compute the new releaseRate assumes that the grantors have not claimed any of their tokens yet (stepsClaimed == 0).**
  - For example, ***a User with 1_000 tokens, and a total numOfSteps of 5***.
    - ***This user can claim 200 tokens on each step***. In this way, after claiming on each step 200 tokens, in the end, the user will get its 1_000 tokens,
  - Now, ***a User with 1_000 tokens, numOfSteps = 5, but, the user has already claimed 2 steps, meaning, the user has claimed 400 tokens.***
    - This ***user can claim another 3 steps, each step for 200 tokens***, meaning, the user can still claim 600 tokens, plus the 400 tokens that had already claimed, in the end, the user claims all its tokens.
  - Now, **same example as the previous one, but, this time, on the step 3, 200 tokens are transferred from this user to another user.**
    - As a result of the transfer, ***the user now has a total of 800 tokens, minus the 400 tokens already claimed,*** **the user has only 400 claimable tokens.**
      - **The release rate for this user is computed as `160` per each step ((because of the current formula [`totalAmount / numOfSteps`])(https://github.com/code-423n4/2024-12-secondswap/blob/main/contracts/SecondSwap_StepVesting.sol#L230) === [`800 / 5`] === 160)**
        - With the new releaseRate, the user will be able to claim on each step the next amounts:
          - step3 => 160
          - step4 => 160
          - step5 => Only 80 (The last step claims what's left) - At this point, the user has claimed `400 + 160 + 160 === 720`, therefore it only has 80 claimable tokens left!
        - As we can see, **the rate at which the user can claim the remaining claimable tokens is bigger than what it should be for the tokens to be evenly claimable on all the remaining steps**

**The releaseRate at which the remaining claimable tokens can be claimed must consider how much tokens have been claimed, and, how many steps have been claimed**, ***in this way, the releaseRate will correctly compute the rate to claim the remaining claimable tokens for all the remaining steps.***

**[`SecondSwap_StepVesting.transferVesting()`](https://github.com/code-423n4/2024-12-secondswap/blob/main/contracts/SecondSwap_StepVesting.sol#L216-L235)**
```solidity
function transferVesting(address _grantor, address _beneficiary, uint256 _amount) external {
    ...

    grantorVesting.totalAmount -= _amount;
    //@audit-issue => new releaseRate does not consider how many tokens and steps have already been claimed!
    grantorVesting.releaseRate = grantorVesting.totalAmount / numOfSteps;

    ...
}
```

## Proof of Concept
This PoC is runnable using foundry, therefore, let's install and setup foundry.
1. Go to the section ["Adding Foundry to a Hardhat project"](https://book.getfoundry.sh/config/hardhat#adding-foundry-to-a-hardhat-project), and make sure to do the 3 steps listed in there.
2. Open the `foundry.toml` file and replace its contents for the next data:
```
[profile.default]
src = 'contracts'
out = 'out'
libs = ['node_modules', 'lib']
test = 'test'
cache_path  = 'cache_forge'
remappings = [
  '@openzeppelin/contracts/=node_modules/@openzeppelin/contracts',
  '@openzeppelin/contracts-upgradeable/=node_modules/@openzeppelin/contracts-upgradeable'
]
```

3. Now, we are ready to test using foundry. **Create a new folder under the [`tests/ folder`](https://github.com/code-423n4/2024-12-secondswap/tree/main/test),** create a new file on that folder and add the next PoC.
4. Run the PoC with the command: `forge test --match-test test_IncorrectReleaseRatePoC -vvv`
5. Notice on the console.log output of the PoC how the step3 && step4 allows the user to claim more vested tokens than on the last step.
- This exactly demonstrates the problem caused by the new releaseRate, allowing grantors to claim more tokens on each step than what they should.

```solidity
pragma solidity ^0.8.24;

import {Test, console2} from "forge-std/Test.sol";

import { ERC20, IERC20 } from "@openzeppelin/contracts/token/ERC20/ERC20.sol";

import {SecondSwap_StepVesting} from "../../contracts/SecondSwap_StepVesting.sol";

contract MockERC20 is ERC20 {
  constructor(string memory name_, string memory symbol_) ERC20(name_, symbol_) {}

  function mint(address receiver, uint256 amount) external {
    _mint(receiver, amount);
  }
}

contract TransferVestingError is Test {
    uint256 constant DURATION = 5 days;
    uint256 constant NUM_STEPS = 5;

    SecondSwap_StepVesting vestingContract;
    MockERC20 vestingToken;

    address tokenIssuer = makeAddr("tokenIssuer");

    address manager = makeAddr("manager");
    address vestingDeployer = makeAddr("vestingDeployer");
    

    function setUp() public {
        vestingToken = new MockERC20("VestingToken", "LOCK");
        vestingToken.mint(tokenIssuer, 20e18);

        vestingContract = new SecondSwap_StepVesting(
          tokenIssuer,
          manager,
          IERC20(address(vestingToken)),
          block.timestamp,
          block.timestamp + DURATION,
          NUM_STEPS,
          vestingDeployer
        );

        vm.prank(tokenIssuer);
        vestingToken.approve(address(vestingContract), type(uint256).max);

        vm.label(tokenIssuer, "tokenIssuer");
        vm.label(address(vestingContract), "vestingContract");
        vm.label(address(vestingToken), "vestingToken");
    }

    function test_DeployVestingContract() public {
      assertEq(vestingContract.tokenIssuer(), tokenIssuer);
    }

    function test_IncorrectReleaseRatePoC() public {
      uint256 ORIGINAL_VESTING = 10e18;
      address beneficiary1 = makeAddr("beneficiary1");
      address beneficiary2 = makeAddr("beneficiary2");

      vm.startPrank(tokenIssuer);
      vestingContract.createVesting(beneficiary1, ORIGINAL_VESTING);
      vestingContract.createVesting(beneficiary2, ORIGINAL_VESTING);
      vm.stopPrank();

      skip(2 days);
      vm.prank(beneficiary1);
      vestingContract.claim();
      assertEq(vestingContract.available(beneficiary1) + vestingToken.balanceOf(beneficiary1), ORIGINAL_VESTING);

      uint256 TRANSFER_VESTING_AMOUNT = 1e18;
      vm.prank(tokenIssuer);
      vestingContract.transferVesting(beneficiary1, beneficiary2, TRANSFER_VESTING_AMOUNT);
      uint256 beneficiary1_balanceBeforeTransferVesting = vestingToken.balanceOf(beneficiary1);

      //@audit-info => Intermediate Steps
      skip(1 days);
      vm.prank(beneficiary1);
      vestingContract.claim();
      uint256 beneficiary1_balanceAtStep3 = vestingToken.balanceOf(beneficiary1);
      uint256 claimedAmount_Step3 = vestingToken.balanceOf(beneficiary1) - beneficiary1_balanceBeforeTransferVesting;

      //@audit-info => Intermediate Steps
      skip(1 days);
      vm.prank(beneficiary1);
      vestingContract.claim();
      uint256 beneficiary1_balanceAtStep4 = vestingToken.balanceOf(beneficiary1);
      uint256 claimedAmount_Step4 = vestingToken.balanceOf(beneficiary1) - beneficiary1_balanceAtStep3;

      //@audit-info => Last step
      skip(1 days);
      vm.prank(beneficiary1);
      vestingContract.claim();
      uint256 claimedAmount_LastStep = vestingToken.balanceOf(beneficiary1) - beneficiary1_balanceAtStep4;

      console2.log("======================================");
      assertEq(claimedAmount_Step4,claimedAmount_Step3);
      assertNotEq(claimedAmount_LastStep, claimedAmount_Step4);
      console2.log("claimedAmount_Step3: ", claimedAmount_Step3);
      console2.log("claimedAmount_Step4: ", claimedAmount_Step4);
      console2.log("claimedAmount_LastStep: ", claimedAmount_LastStep);
      console2.log("======================================");
    }
}
```

## Tools Used
Manual Audit && Foundry

## Recommended Mitigation Steps
Use the same formula as in [`SecondSwap_StepVesting_createVesting()`](https://github.com/code-423n4/2024-12-secondswap/blob/main/contracts/SecondSwap_StepVesting.sol#L298-L300) -> (newTotal - amountClaimed) / (numSteps - stepsClaimed).

**[`SecondSwap_StepVesting.transferVesting()`](https://github.com/code-423n4/2024-12-secondswap/blob/main/contracts/SecondSwap_StepVesting.sol#L216-L235)**
```diff
function transferVesting(address _grantor, address _beneficiary, uint256 _amount) external {
    ...
-   grantorVesting.releaseRate = grantorVesting.totalAmount / numOfSteps;
+   grantorVesting.releaseRate = (grantorVesting.totalAmount - grantorVesting.amountClaimed) / (numOfSteps - grantorVesting.stepsClaimed);

    ...
}
```