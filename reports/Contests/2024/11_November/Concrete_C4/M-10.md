# High Severity

## Lines with Problems
- https://github.com/code-423n4/2024-11-concrete/blob/main/src/vault/ConcreteMultiStrategyVault.sol#L464


## Title
Computing amountToWithdraw based on the full amount to withdraw instead of the withdrawable assets from the vault's strategies (diff) causes withdraws to revert

## Finding description and impact
### Impact
Withdraws won't be processed because due to an error when computing the `amountToWithdraw` from the strategies, `amountToWithdraw` will be computed bigger than what it shuold be, causing the Vault to request more assets to be withdrawn from the strategies than the assets the strategy owns, as a consequence the tx is reverted.

### Finding Description
When a Vault process withdrawals/redemptions, it calculates the [available assets that can be withdrawn as the sum of the underlyingAsset held by the Vault itself, plus, all the available assets that can be withdrawn from all the vault's strategies.](https://github.com/code-423n4/2024-11-concrete/blob/main/src/vault/ConcreteMultiStrategyVault.sol#L511-L524) If the vault's `availableAssetsForWithdrawal` are enough to process the requested withdrawal amount, then, it proceeds to transfer the requested amount to the receiver, either directly from the vault's underlying balance (if enough), or, by withdrawing liquidity from all the vault's strategies, or a combination of the two, withdrawing from the vault's strategies and transfering from the vault's balance.

**Note, this is a totally different problem than the ones described on the next reports**
- `Withdraws from Vaults can fall into DoS when a single strategy has not enough withdrawable liquidity, even though the rest of the strategies have more than enough liquidity to process the requested withdrawal`
- `Strategies that interacts with protocols like Morpho where loses are evenly distributed when bad debt occurs will cause user funds to get stuck because the logic on the withdrawStrategyFunds()`

**The problem of this report still happens even though the other two issues would not exist (even after those two problems are solved, this issue would still occur).**

So, in this case we are going to dig on the problem about computing the `amountToWithdraw` from the vault's strategies when the vault itself has some balance of the underlying asset.

On the below PoC is demonstrated how the withdrawal process will revert on a Vault that has assets distributed across its own balance and two strategies, the steps on the PoC are as follows:
- One strategy has a 30% allocation, and the other one has a 70%.

1. User1 deposits 5k USDC on the vault while the vault is not idle, meaning, those 5k will be deposited on the strategies.
- Strategy1 (30% allocation) will receive 1_500 USDC
- Strategy1 (70% allocation) will receive 3_500 USDC

2. The Vault is set as idle, meaning, all new deposits won't be deposited on the strategies, the assets will be left on the Vault's balance.

3. User1 deposits another 5k USDC on the Vault, but, this time, those 5k USDC are left on the Vault's balance. So, now the assets are distributed as follows:
- 5k on the two strategies, which are distributed as explained on point 1.
- 5k on the Vault's balance.

4. Now, the User requests to withdraw 9k. As we can clearly see, the Vault has enough liquidity (10k) to process the withdrawal request.
- Here is where **the problem occurs, [at the moment of computing the `amountToWithdraw`](https://github.com/code-423n4/2024-11-concrete/blob/main/src/vault/ConcreteMultiStrategyVault.sol#L464-L468)**, ***it uses the full amount that the user requested to withdraw (9k), instead of using the availableAssetsForWithdrawal from the vault's strategies (5k).***
  - [`amountToWithdraw` is computed as shown in the next code snipped](https://github.com/code-423n4/2024-11-concrete/blob/main/src/vault/ConcreteMultiStrategyVault.sol#L464-L468):
    - `amount_` is 9k
    - `strategy.allocation.amount` is 3_000 (30%)
    - MAX_BASIS_POINTS is 10_000
  - `9_000.mulDiv(3_000, 10_000, RoundUp)` ===> `amountToWithdraw == 2_700`

- **This is the problem, `amountToWithdraw` is computed as `2_700`, when in reality, that strategy only owns `1_500`.**
  - **Requesting to withdraw 2.7k USDC from a Strategy that only owns 1.5k will cause a revert with the error `ERC4626ExceededMaxWithdraw()`.** ***It is attempting to withdraw more assets than those owned by the strategy***

```solidity
uint256 amountToWithdraw = amount_.mulDiv(
    strategy.allocation.amount,
    MAX_BASIS_POINTS,
    Math.Rounding.Ceil
);
```

## Proof of Concept
Create a new file under the [`test/`](https://github.com/code-423n4/2024-11-concrete/tree/main/test) and paste the below code in it:
Run the PoC with the next command `forge test --match-test test_WithdrawRevertsBadComputationOfAmountToWithdrawPoC --evm-version cancun -vvvv`
- Imporant to specify the evm-version, the forge compiler reverts everything if not specified ==> https://github.com/foundry-rs/foundry/issues/6228

```solidity
//SPDX-License-Identifier: MIT
pragma solidity 0.8.24;

import {Test} from "forge-std/Test.sol";
import {IERC20, IERC4626} from "@openzeppelin/contracts/token/ERC20/extensions/ERC4626.sol";
import {Math} from "@openzeppelin/contracts/utils/math/Math.sol";
import {Clones} from "@openzeppelin/contracts/proxy/Clones.sol";
import {ConcreteMultiStrategyVault} from "../src/vault/ConcreteMultiStrategyVault.sol";
import {Strategy, VaultFees, Allocation, GraduatedFee} from "../src/interfaces/IConcreteMultiStrategyVault.sol";
import {IStrategy} from "../src/interfaces/IStrategy.sol";

import {AaveV3Strategy} from "../src/strategies/Aave/AaveV3Strategy.sol";

contract IncorrectComputationOfAmountToWithdrawTest is Test {
    using Math for uint256;

    Strategy[] strategies;
    ConcreteMultiStrategyVault vault;

    GraduatedFee[] graduatedFees;
    GraduatedFee[] zeroFees;

    address implementation;

    address feeRecipient = address(0x1111);
    address jimmy = address(0x2222);
    address admin = address(0x4444);

    address public constant USDC = 0xA0b86991c6218b36c1d19D4a2e9Eb0cE3606eB48;
    address public constant LIDO_AaveProtocolDataProvider = 0x08795CFE08C7a81dCDFf482BbAAF474B240f31cD;
    address public constant MAIN_AaveProtocolDataProvider = 0x41393e5e337606dc3821075Af65AeE84D7688CBD;

    address public constant USDC_WHALE = 0x37305B1cD40574E4C5Ce33f8e8306Be057fD7341;

    function setUp() public {
        uint256 forkId = vm.createFork(
            "https://eth-mainnet.g.alchemy.com/v2/THuQ6WJZ6orCusOEvVFb9baHWzk_KG_D",
            21_244_615 // @audit => Date: Nov-22-2024 04:58:59 PM +UTC
        );
        vm.selectFork(forkId);

        vm.label(feeRecipient, "feeRecipient");
        vm.label(jimmy, "jimmy");
        vm.label(USDC, "USDC");
        // Example performanceFee: [{0000, 500, 300}, {501, 2000, 1000}, {2001, 5000, 2000}, {5001, 10000, 5000}]

        graduatedFees.push(GraduatedFee({lowerBound: 0, upperBound: 500, fee: 300}));
        graduatedFees.push(GraduatedFee({lowerBound: 501, upperBound: 2000, fee: 1000}));
        graduatedFees.push(GraduatedFee({lowerBound: 2001, upperBound: 5000, fee: 2000}));
        graduatedFees.push(GraduatedFee({lowerBound: 5001, upperBound: 10000, fee: 5000}));

        zeroFees.push(GraduatedFee({lowerBound: 0, upperBound: 10000, fee: 0}));

        implementation = address(new ConcreteMultiStrategyVault());
        address vaultAddress = Clones.clone(implementation);
        vault = ConcreteMultiStrategyVault(vaultAddress);

        AaveV3Strategy lido_usdc_AAVE_strategy = new AaveV3Strategy(IERC20(USDC), feeRecipient, admin, 1000, LIDO_AaveProtocolDataProvider, vaultAddress);
        AaveV3Strategy main_usdc_AAVE_strategy = new AaveV3Strategy(IERC20(USDC), feeRecipient, admin, 1000, MAIN_AaveProtocolDataProvider, vaultAddress);

        strategies.push(Strategy({
                strategy: IStrategy(address(lido_usdc_AAVE_strategy)),
                allocation: Allocation({index: 0, amount: 3_000}) //30%
        }));

        strategies.push(Strategy({
                strategy: IStrategy(address(main_usdc_AAVE_strategy)),
                allocation: Allocation({index: 0, amount: 7_000}) //70%
        }));

        vault.initialize(
            IERC20(USDC),
            "USDC_AAVEV3STRATEGIES_VAULT",
            "USDC_VAULT",
            strategies,
            feeRecipient,
            VaultFees({depositFee: 500, withdrawalFee: 100, protocolFee: 300, performanceFee: graduatedFees}),
            type(uint256).max,
            admin
        );

        vm.label(address(vault), "vault");
        vm.label(address(lido_usdc_AAVE_strategy), "lidoUSDCStrategy");
        vm.label(address(main_usdc_AAVE_strategy), "mainUSDCStrategy");

    }

    function test_WithdrawRevertsBadComputationOfAmountToWithdrawPoC() public {
      assertEq(strategies[0].strategy.asset(), USDC, "Asset Match");
      assertEq(strategies[1].strategy.asset(), USDC, "Asset Match");

      vm.prank(USDC_WHALE);
      IERC20(USDC).transfer(jimmy, 10_000e6);

      vm.startPrank(jimmy);
      IERC20(USDC).approve(address(vault), 10_000e6);
      //@audit => Deposits 5k that will be distributed to the strategies!
      vault.deposit(5_000e6);
      vm.stopPrank();

      vm.prank(admin);
      //@audit => set the vault as idle, so the next deposit is left on the vault's balance
      vault.toggleVaultIdle();

      vm.prank(jimmy);
      vault.deposit(5_000e6);


      uint256 amountToWithdraw = 9_000e6;
      
      //@audit-issue => The user request to withdraw 9k USDC, but the tx is reverted because the `amountToWithdraw` is incorrectly computed using the full requestedAmountToWithdraw instead of using the availableAssetsToWithdraw from the strategies.
      vm.prank(jimmy);
      vm.expectRevert();
      vault.withdraw(amountToWithdraw);
    }
}
```

## Tools Used
Manual Audit && Foundry

## Recommended Mitigation Steps
When computing the `amountToWithdraw`, use `diff` instead of `amount_`.
- The [difference between amount and diff will be sent from the Vault's balance to the receiver](https://github.com/code-423n4/2024-11-concrete/blob/main/src/vault/ConcreteMultiStrategyVault.sol#L477-L480), right after the liquidity has been withdrawn from the strategies.

```diff
function _withdrawStrategyFunds(uint256 amount_, address receiver_) private {
    ...
    //@audit => Vault's underlyingAsset balance
    uint256 float = asset_.balanceOf(address(this));
    // If there is enough in the vault, send the full amount
    if (amount_ <= float) {
        asset_.safeTransfer(receiver_, amount_);
    } else {
        //@audit => `diff` is the assets that can be withdrawn from the vault's strategies
        uint256 diff = amount_ - float;
        ...

        for (uint256 i; i < len; ) {
            ...

-           uint256 amountToWithdraw = amount_.mulDiv(
+           uint256 amountToWithdraw = diff.mulDiv(
                strategy.allocation.amount,
                MAX_BASIS_POINTS,
                Math.Rounding.Ceil
            );
            ...
        }
        ...
    }
}
```