# High Severity

## Lines with Problems
- https://github.com/code-423n4/2024-11-concrete/blob/main/src/vault/ConcreteMultiStrategyVault.sol#L104-L120


## Title
For Vaults working with strategies that mints rebasing tokens for deposits, the performance fee will be incorrectly charged to all new deposits that are made in between two calls to the takeFees() modifier.

## Finding description and impact
### Impact
yield/profits are not counted for performanceFee because of calling takeFees to soon, or, all deposits in between two calls to takeFees are counted for performanceFee (See more details at the end of the Proof of Concept)

### Finding Description
AaveV3Strategies, RadiantV2Strategies && MorphoStrategies deposits on the underlying protocols, which, mints shares of the Vault/Pool where the deposits are made to. All these shares are rebasing tokens (aToken, rTokens), it basically means that their balance is updated throughout the time. This is the mechanism used to reflect the earned yield for the deposits.
- This also means that **the Vault's shareValue will be growing all the time**, (because the vault's balance will be growing as yield is earned for the deposits).

**The problem with the current implementation to take fees is that all deposits made between one call to takeFees() and another call to takeFees() will be considered as if they were interest/yield earned by the Vault** (a.k.a performance), therefore, ***the performanceFee will be applied to those deposits too, as opposed to only be applied to the real earned yield/profits.***

On the next section there are two PoCs:
- The first PoC demonstrates how all the deposits made between two calls to [takeFees()](https://github.com/code-423n4/2024-11-concrete/blob/main/src/vault/ConcreteMultiStrategyVault.sol#L104-L120) are counted for the performance fee.
- The second PoC demonstrates how attempting to call takeFees each time before a deposit is made will cause the minted rewards to be lost because those rewards are not big enough to make performanceFee > 0 (as seen in the PoC, the takeFees is called each time before a deposit, and, in the end, the feeRecipient did not receive any shares for the performance fee, even though the vault generated some yield.)
  - This second PoC also addresses a potential problem with the [`Vault.takePortfolioAndProtocolFees`](https://github.com/code-423n4/2024-11-concrete/blob/main/src/vault/ConcreteMultiStrategyVault.sol#L735-L737) being permissionless, where malicious users could continously call this function to call takeFees before the earned fees are big enough to compute a value for the performanceFee, in this way, the vault can earn yield without paying a performanceFee. I'm adding this second attack vector in this report because once fixed the problem on the takeFees() modifier itself, it won't matter if the [`Vault.takePortfolioAndProtocolFees`](https://github.com/code-423n4/2024-11-concrete/blob/main/src/vault/ConcreteMultiStrategyVault.sol#L735-L737) is permissionless or not, takeFees() will handle correctly the scenario when the earned yield is not yet enough to compute a value for performanceFee.

## Proof of Concept
Add the below PoCs on the [`ConcreteMultiStrategyVault.t.sol`](https://github.com/code-423n4/2024-11-concrete/blob/main/test/ConcreteMultiStrategyVault.t.sol) test file, additionally, help me to make the next two changes on the test file itself:
1. (Make sure to import `console2.log` from the `"forge-std/Test.sol"`)
- Just modifiy [this line](https://github.com/code-423n4/2024-11-concrete/blob/main/test/ConcreteMultiStrategyVault.t.sol#L4C1-L4C41) `import {Test} from "forge-std/Test.sol";` **for => `import {Test,console2} from "forge-std/Test.sol";`**

2. Comment the [line to push the ProtectStrategy to the Vault.](https://github.com/code-423n4/2024-11-concrete/blob/main/test/ConcreteMultiStrategyVault.t.sol#L70)

3. Set the strategy's allocation to a 100%, change [this line](https://github.com/code-423n4/2024-11-concrete/blob/main/test/ConcreteMultiStrategyVault.t.sol#L2088) for => `allocation: Allocation({index: 0, amount: 10_000}) //100%`. [_createMockStrategy()](https://github.com/code-423n4/2024-11-concrete/blob/main/test/ConcreteMultiStrategyVault.t.sol#L2084-L2090) should looks like:
```
function _createMockStrategy(IERC20 asset_, bool decreased_) internal returns (Strategy memory) {
    return
        Strategy({
            strategy: IStrategy(address(new MockERC4626(asset_, "Mock Shares", "MS"))),
            // allocation: Allocation({index: 0, amount: decreased_ ? 1000 : 3333})
            allocation: Allocation({index: 0, amount: 10_000}) //100%
        });
}
```

> Run the first PoC with the next command `forge test --match-test test_DepositsCountsForPerformanceFeePoC -vvv`

> Run the second PoC with the next command `forge test --match-test test_PerformanceFeeIsLostPoC -vvv`

**First PoC - Deposits are counted for the performanceFee**
```solidity
function test_DepositsCountsForPerformanceFeePoC() public {
    uint128 rewardAmount = 1e18;
    uint256 depositAmount = 5_000e18;

    uint256 shareValueStart = vault.convertToAssets(1e18);
    uint256 expectedPerformanceFeeStart = vault.accruedPerformanceFee();

    address vaultFeeRecipient = vault.feeRecipient();
    uint256 feeRecipientSharesOriginal = vault.balanceOf(vaultFeeRecipient);
    assertEq(feeRecipientSharesOriginal, 0);
    
    vm.prank(admin);
    vault.setVaultFees(VaultFees({depositFee: 0, withdrawalFee: 0, protocolFee: 0, performanceFee: graduatedFees}));

    {
        asset.mint(hazel, type(uint256).max / 2);
        vm.startPrank(hazel);
        asset.approve(address(vault), type(uint256).max);
        uint256 shares = vault.deposit(depositAmount, hazel);
        vm.stopPrank();
    }
    uint256 shareValueBeforeRewards = vault.convertToAssets(1e18);
    uint256 expectedPerformanceFeeInBeforeRewards = vault.accruedPerformanceFee();

    //@audit => Take fees before rewards are minted
    vault.takePortfolioAndProtocolFees();
    uint256 feeRecipientSharesBeforeRewards = vault.balanceOf(vaultFeeRecipient);
    assertEq(feeRecipientSharesBeforeRewards, 0);

    //@audit-info => minting 1e18 of rewards demonstrates how the next deposits will be considered for the performanceFee
    //@audit-info => mints 1USDS of rewards
    asset.mint(address(strategies[0].strategy), rewardAmount);
    uint256 shareValueAfterEarningRewards = vault.convertToAssets(1e18);
    uint256 expectedPerformanceFeeInAssetAfterRewards = vault.accruedPerformanceFee();

    {
        vm.prank(hazel);
        vault.deposit(depositAmount, hazel);
    }
    uint256 expectedPerformanceFeeInAssetAfterDeposit2 = vault.accruedPerformanceFee();
    uint256 shareValueAfterSecondDeposit = vault.convertToAssets(1e18);
    
    {
        vm.prank(hazel);
        vault.deposit(depositAmount, hazel);
    }
    uint256 expectedPerformanceFeeInAssetAfterDeposit3 = vault.accruedPerformanceFee();
    uint256 shareValueAfterThirdDeposit = vault.convertToAssets(1e18);

    //@audit => Take fees after rewards have been minted, and some deposits were made afterwards
    vault.takePortfolioAndProtocolFees();
    uint256 feeRecipientSharesFinal = vault.balanceOf(vaultFeeRecipient);
    assertApproxEqAbs(feeRecipientSharesFinal, vault.convertToShares(expectedPerformanceFeeInAssetAfterDeposit3), 10);
    //@audit => Validate the earned performance fee is bigger than what it should be if only the real earned rewards would be counted for the performanceFee
    assertGt(expectedPerformanceFeeInAssetAfterDeposit3, expectedPerformanceFeeInAssetAfterRewards);

    console2.log("==============================================================");
    console2.log("shareValueStart: ", shareValueStart);
    console2.log("shareValueBeforeRewards: ", shareValueBeforeRewards);
    console2.log("####################### minting rewards ######################");
    console2.log("shareValueAfterEarningRewards: ", shareValueAfterEarningRewards);
    console2.log("shareValueAfterSecondDeposit: ", shareValueAfterSecondDeposit);
    console2.log("shareValueAfterThirdDeposit: ", shareValueAfterThirdDeposit);

    console2.log("==============================================================");
    console2.log("==============================================================");

    console2.log("expectedPerformanceFeeStart: ", expectedPerformanceFeeStart);
    console2.log("expectedPerformanceFeeInBeforeRewards: ", expectedPerformanceFeeInBeforeRewards);
    console2.log("####################### minting rewards ######################");
    console2.log("expectedPerformanceFeeInAssetAfterRewards: ", expectedPerformanceFeeInAssetAfterRewards);
    console2.log("expectedPerformanceFeeInAssetAfterDeposit2: ", expectedPerformanceFeeInAssetAfterDeposit2);
    console2.log("expectedPerformanceFeeInAssetAfterDeposit3: ", expectedPerformanceFeeInAssetAfterDeposit3);
    console2.log("==============================================================");
}
```


**Second PoC - Earned yield is not counted for performanceFee because takeFee is called before enough rewards have been earned**
```solidity
function test_PerformanceFeeIsLostPoC() public {
    //@audit-info => For example, a Vault with 10k USDS earning a 5% yield would be prone to lose those generated rewards to count for the performanceFee for at least a period of 12 hours.
    //@audit-info => 10k at a 5% are ~1.3USDS daily, if the takeFees are called in less than 12 hours, the fees generated on those 12 hours would not be counted for the performanceFee.
      //@audit-info => This also means that deposits made during a period of 12 hours would be counted for the performanceFee,
    //@audit-info => We are in a situation were either yield is not counted for performanceFee because of calling takeFees to soon, or all deposits in between two calls to takeFees are counted for performanceFee
    // uint128 rewardAmount = 1e18;
    uint256 depositAmount = 10_000e18;

    uint256 shareValueStart = vault.convertToAssets(1e18);
    uint256 expectedPerformanceFeeStart = vault.accruedPerformanceFee();

    address vaultFeeRecipient = vault.feeRecipient();
    uint256 feeRecipientSharesOriginal = vault.balanceOf(vaultFeeRecipient);
    assertEq(feeRecipientSharesOriginal, 0);
    
    vm.prank(admin);
    vault.setVaultFees(VaultFees({depositFee: 0, withdrawalFee: 0, protocolFee: 0, performanceFee: graduatedFees}));

    asset.mint(hazel, type(uint256).max / 2);
    vm.startPrank(hazel);
    asset.approve(address(vault), type(uint256).max);
    uint256 shares = vault.deposit(depositAmount, hazel);
    vm.stopPrank();
    uint256 shareValueBeforeRewards = vault.convertToAssets(1e18);
    uint256 expectedPerformanceFeeInBeforeRewards = vault.accruedPerformanceFee();

    uint256 shareValueAfterEarningRewards = vault.convertToAssets(1e18);
    uint256 expectedPerformanceFeeInAssetAfterRewards = vault.accruedPerformanceFee();

    //@audit-info => mints some rewards to the vault
    asset.mint(address(strategies[0].strategy), 0.6e18);
    vault.takePortfolioAndProtocolFees();

    vm.prank(hazel);
    vault.deposit(depositAmount, hazel);
    uint256 expectedPerformanceFeeInAssetAfterDeposit2 = vault.accruedPerformanceFee();
    uint256 shareValueAfterSecondDeposit = vault.convertToAssets(1e18);

    //@audit-info => mints some rewards to the vault    
    asset.mint(address(strategies[0].strategy), 0.6e18);
    vault.takePortfolioAndProtocolFees();

    vm.prank(hazel);
    vault.deposit(depositAmount, hazel);
    vm.stopPrank();
    uint256 expectedPerformanceFeeInAssetAfterDeposit3 = vault.accruedPerformanceFee();
    uint256 shareValueAfterThirdDeposit = vault.convertToAssets(1e18);

    //@audit-info => Because takeFees was called when the earned rewards were not big enough, in the end, no performanceFee is earned!
    uint256 feeRecipientSharesFinal = vault.balanceOf(vaultFeeRecipient);
    assertEq(feeRecipientSharesFinal, feeRecipientSharesOriginal);

    console2.log("==============================================================");
    console2.log("shareValueStart: ", shareValueStart);
    console2.log("shareValueBeforeRewards: ", shareValueBeforeRewards);
    console2.log("####################### minting rewards ######################");
    console2.log("shareValueAfterEarningRewards: ", shareValueAfterEarningRewards);
    console2.log("shareValueAfterSecondDeposit: ", shareValueAfterSecondDeposit);
    console2.log("shareValueAfterThirdDeposit: ", shareValueAfterThirdDeposit);

    console2.log("==============================================================");
    console2.log("==============================================================");

    console2.log("expectedPerformanceFeeStart: ", expectedPerformanceFeeStart);
    console2.log("expectedPerformanceFeeInBeforeRewards: ", expectedPerformanceFeeInBeforeRewards);
    console2.log("####################### minting rewards ######################");
    console2.log("expectedPerformanceFeeInAssetAfterRewards: ", expectedPerformanceFeeInAssetAfterRewards);
    console2.log("expectedPerformanceFeeInAssetAfterDeposit2: ", expectedPerformanceFeeInAssetAfterDeposit2);
    console2.log("expectedPerformanceFeeInAssetAfterDeposit3: ", expectedPerformanceFeeInAssetAfterDeposit3);
    console2.log("==============================================================");
}
```

**Conclusions based on the two previous PoCs**
For a Vault with 10k USDS earning a 5% yield would be prone to lose those generated rewards to count for the performanceFee for at least a period of 12 hours.
- 10k at a 5% are ~1.3USDS daily, if the takeFees are called in less than 12 hours, the fees generated on those 12 hours would not be counted for the performanceFee.
  - This also means that deposits made during a period of 12 hours would be counted for the performanceFee,
**We are in a situation were either yield is not counted for performanceFee because of calling takeFees to soon, or, all deposits in between two calls to takeFees are counted for performanceFee**

## Tools Used
Manual Audit && Foundry

## Recommended Mitigation Steps
The best mitigation for this problem is to make the logic of the [takeFees() modifier](https://github.com/code-423n4/2024-11-concrete/blob/main/src/vault/ConcreteMultiStrategyVault.sol#L104-L120) capable of updating the highWaterMark only when the generated fees are enough to compute a value for the performanceFee, and, also, **make sure that fees are taken before any deposits/mints/withdraws and redeems.**

```diff
modifier takeFees() {
-   uint256 totalFee = accruedProtocolFee() + accruedPerformanceFee();
+   uint256 accruedProtocolFee = accruedProtocolFee();
+   uint256 accruedPerformanceFee = accruedPerformanceFee();
+   if(accruedPerformanceFee == 0) return;
+   uint256 totalFee = accruedProtocolFee + accruedPerformanceFee;
    uint256 shareValue = convertToAssets(1e18);
    uint256 _totalAssets = totalAssets();
    if (shareValue > highWaterMark) highWaterMark = shareValue;
    ...
}
```