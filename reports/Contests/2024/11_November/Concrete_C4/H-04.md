# High Severity

## Lines with Problems
- https://github.com/code-423n4/2024-11-concrete/blob/main/src/strategies/StrategyBase.sol#L347


## Title
Users loses a portion of their deposits when harvesting rewards for Strategies where the rewardToken is the same as the asset minted by the protocol where the strategy deposits funds to. 

## Finding description and impact
### Impact
- Users loses a portion of their deposits.
- Vault's accounting is messed up, not able to process withdrawals.
  - Strategies transfers all the deposits to the vault as if the deposits would be rewards.

### Finding Description
All strategies are capable of claiming rewards that are generated on the third party protocols where the strategy's funds gets deposited.
- For example, for an AaveV3Strategy, there are times when the AavePools offers extra rewards on top of the normal deposit's interest in order to incentivize deposits.
  - Same story for other Strategies, for example, MorphoStrategies.

The Rewards can be claimed via the [`ConcreteMultiStrategyVault.harvestRewards()`](https://github.com/code-423n4/2024-11-concrete/blob/main/src/vault/ConcreteMultiStrategyVault.sol#L989-L1025), which it basically iterates over all the vault's strategies, and, attempts to claim all the rewardTokens from each strategy.
- In summary, each Strategy claims the earned rewards, pays a portion of those earned rewards as a fee to the feeRecipient, and the rest of the rewards are sent to the Vault in order for the Vault's depositors to claim them.

**The problem is when the rewardToken is the same token as the one that the Strategy receives for its deposits on the third party protocols**
- For example, on Aave, when the rewardToken is the same aToken as the aToken that the AavePool mints to the strategy for the deposited assets.
  - When the rewardToken is the same aToken as the one for the deposits, the [`StrategyBase.harvestRewards()`](https://github.com/code-423n4/2024-11-concrete/blob/main/src/strategies/StrategyBase.sol#L337-L367) will consider all the existing deposits as if they were rewards, as such (as demonstrated in the PoC of this report):
1. The computed fee for the feeRecipient will be way bigger than what it should be, also
2. All the remaining aTokens will be sent from the Strategy to the Vault.
- This means that the Strategy will be left without any deposits, completely messing up the withdrawal process.

On the below PoC is demonstrated how harvesting rewards for Strategies where the rewardToken is the same token used to track the deposits messes up the Vault's accounting and causes users to lose a portion of their deposits.
- In this case, we are using the [USDS AavePool from the Ethereum Main Market](https://app.aave.com/reserve-overview/?underlyingAsset=0xdc035d45d973e3ec169d2276ddab16f1e407384f&marketName=proto_mainnet_v3) as an example, which, at the moment of coding the PoC is offering as rewardToken the same aToken used to track the deposits (aEthUSDS), the steps on the PoC are as follows:

- A vault with two AaveV3Strategies, one strategy has a 10% allocation and the other one has a 90% allocation.
  - The Strategy with the 90% allocation is the one that works with the USDS AavePool mentioned above. (The rewardToken is the same as the aToken used to track the deposits).

1. Two users deposits on the Vault and their assets are deposited onto the Strategies, which, ends up being deposited on the AavePools.
2. 3 days passes and the rewards are harvested on the Vault.
- **At this point is where the problem occurs.** During the process to harvest the rewards, on the Strategy with the 90% allocation, the rewardToken is the same aToken as the one that the Strategy is holding for all the deposited assets on the AavePool (aEthUSDS), and, ***because the claimed rewards are not tracked, but, rather, [the Strategy only checks its own balance of the rewardToken](https://github.com/code-423n4/2024-11-concrete/blob/main/src/strategies/StrategyBase.sol#L347),*** **all the aTokens the strategy had for the deposited assets are considered as if they were claimed rewards.**
  - As a result of this problem, `claimedBalance` will be equals to all the deposited assets + the claimed rewards, this causes two problems:
    - `collectedFee` is computed based on all the strategy's deposits + the earned rewards (As opposed to only using the earned rewards). This means, the `feeRecipient` will receive more than x100 times the assets that it should receive. (All those extra assets are actually taken from the depositor's assets, meaning, the vault's depositors will end up losing those assets)
    - All the aTokens are going to be transferred from the Strategy to the Vault, which, it will mess up the accounting because the strategy will no longer have any deposits to process request withdrawals.

3. After harvesting the rewards, the accounting is messed up, and the vault won't be able to process withdrawals because the strategy with the 90% allocation was left without deposits.

**[`StrategyBase.harvestRewards()`](https://github.com/code-423n4/2024-11-concrete/blob/main/src/strategies/StrategyBase.sol#L337-L367)**
```solidity
function harvestRewards(
    bytes memory data
) public virtual override(IStrategy) onlyVault returns (ReturnedRewards[] memory) {
    _getRewardsToStrategy(data);
    uint256 len = rewardTokens.length;
    ReturnedRewards[] memory returnedRewards = new ReturnedRewards[](len);
    for (uint256 i = 0; i < len; ) {
        IERC20 rewardAddress = rewardTokens[i].token;
        ...
        //@audit-issue => `rewardAddress` is the same aToken the Strategy has for the deposits on the AavePool!
        uint256 claimedBalance = rewardAddress.balanceOf(address(this));
        if (claimedBalance != 0) {
            //@audit-issue => `collectedFee` is computed based on all the strategy's deposits + the earned rewards (As opposed to only using the earned rewards)!
            uint256 collectedFee = claimedBalance.mulDiv(rewardTokens[i].fee, 10000, Math.Rounding.Ceil);

            if (TokenHelper.attemptSafeTransfer(address(rewardAddress), feeRecipient, collectedFee, false)) {
                rewardTokens[i].accumulatedFeeAccounted += collectedFee;
                netReward = claimedBalance - collectedFee;
                emit Harvested(_vault, netReward);
            }

            //@audit-issue => All the remaining aTokens will be transferred to the Vault, leaving the strategy with no deposits.
            // rewardAddress.safeTransfer(_vault, netReward);
            TokenHelper.attemptSafeTransfer(address(rewardAddress), _vault, netReward, false);
        }
        ...
    }
}
```


## Proof of Concept
Create a new file under the [`test/`](https://github.com/code-423n4/2024-11-concrete/tree/main/test) and paste the below code in it:
Run the PoC with the next command `forge test --match-test test_HarvestingRewardsMessesDepositsPoC --evm-version cancun -vvvv`

```solidity
//SPDX-License-Identifier: MIT
pragma solidity 0.8.24;

import {Test, console2} from "forge-std/Test.sol";
import {IERC20, IERC4626} from "@openzeppelin/contracts/token/ERC20/extensions/ERC4626.sol";
import {Math} from "@openzeppelin/contracts/utils/math/Math.sol";
import {Clones} from "@openzeppelin/contracts/proxy/Clones.sol";
import {ConcreteMultiStrategyVault} from "../src/vault/ConcreteMultiStrategyVault.sol";
import {Strategy, VaultFees, Allocation, GraduatedFee} from "../src/interfaces/IConcreteMultiStrategyVault.sol";
import {IStrategy} from "../src/interfaces/IStrategy.sol";

import {StrategyBase} from "../src/strategies/StrategyBase.sol";
import {AaveV3Strategy} from "../src/strategies/Aave/AaveV3Strategy.sol";
import {IAToken, ILendingPool, IProtocolDataProvider, IAaveIncentives} from "../src/strategies/Aave/IAaveV3.sol";

contract HarvestRewardsMessesDepositsPoC is Test {
    using Math for uint256;

    Strategy[] strategies;
    ConcreteMultiStrategyVault vault;

    GraduatedFee[] graduatedFees;
    GraduatedFee[] zeroFees;

    address implementation;

    address feeRecipient = address(0x1111);
    address jimmy = address(0x2222);
    address hazel = address(0x3333);
    address admin = address(0x4444);

    address public constant USDS = 0xdC035D45d973E3EC169d2276DDab16f1e407384F;
    address public constant LIDO_AaveProtocolDataProvider = 0x08795CFE08C7a81dCDFf482BbAAF474B240f31cD;
    address public constant MAIN_AaveProtocolDataProvider = 0x41393e5e337606dc3821075Af65AeE84D7688CBD;

    address public constant MAIN_IncentivesRewards = 0x8164Cc65827dcFe994AB23944CBC90e0aa80bFcb;

    address public constant USDS_WHALE = 0x0650CAF159C5A49f711e8169D4336ECB9b950275;

    AaveV3Strategy lido_usds_AAVE_strategy;
    AaveV3Strategy main_usds_AAVE_strategy;

    function setUp() public {
        uint256 forkId = vm.createFork(
            "https://eth-mainnet.g.alchemy.com/v2/THuQ6WJZ6orCusOEvVFb9baHWzk_KG_D",
            21_252_808 // @audit => Date: Nov-23-2024 08:24:35 PM +UTC
        );
        vm.selectFork(forkId);

        vm.label(feeRecipient, "feeRecipient");
        vm.label(jimmy, "jimmy");
        vm.label(hazel, "hazel");
        vm.label(USDS, "USDS");
        // Example performanceFee: [{0000, 500, 300}, {501, 2000, 1000}, {2001, 5000, 2000}, {5001, 10000, 5000}]

        graduatedFees.push(GraduatedFee({lowerBound: 0, upperBound: 500, fee: 300}));
        graduatedFees.push(GraduatedFee({lowerBound: 501, upperBound: 2000, fee: 1000}));
        graduatedFees.push(GraduatedFee({lowerBound: 2001, upperBound: 5000, fee: 2000}));
        graduatedFees.push(GraduatedFee({lowerBound: 5001, upperBound: 10000, fee: 5000}));

        zeroFees.push(GraduatedFee({lowerBound: 0, upperBound: 10000, fee: 0}));

        implementation = address(new ConcreteMultiStrategyVault());
        address vaultAddress = Clones.clone(implementation);
        vault = ConcreteMultiStrategyVault(vaultAddress);

        lido_usds_AAVE_strategy = new AaveV3Strategy(IERC20(USDS), feeRecipient, admin, 1000, LIDO_AaveProtocolDataProvider, vaultAddress);
        main_usds_AAVE_strategy = new AaveV3Strategy(IERC20(USDS), feeRecipient, admin, 1000, MAIN_AaveProtocolDataProvider, vaultAddress);

        strategies.push(Strategy({
                strategy: IStrategy(address(lido_usds_AAVE_strategy)),
                allocation: Allocation({index: 0, amount: 1_000}) //10%
        }));

        strategies.push(Strategy({
                strategy: IStrategy(address(main_usds_AAVE_strategy)),
                allocation: Allocation({index: 0, amount: 9_000}) //90%
        }));

        vault.initialize(
            IERC20(USDS),
            "USDS_AAVEV3STRATEGIES_VAULT",
            "USDS_VAULT",
            strategies,
            feeRecipient,
            VaultFees({depositFee: 500, withdrawalFee: 100, protocolFee: 300, performanceFee: graduatedFees}),
            type(uint256).max,
            admin
        );

        vm.label(address(vault), "vault");
        vm.label(address(lido_usds_AAVE_strategy), "lidoUSDSStrategy");
        vm.label(address(main_usds_AAVE_strategy), "mainUSDSStrategy");

    }

    function test_HarvestingRewardsMessesDepositsPoC() public {
      assertEq(strategies[0].strategy.asset(), USDS, "Asset Match");
      assertEq(strategies[1].strategy.asset(), USDS, "Asset Match");

      (address USDS_lido_aToken, , ) = IProtocolDataProvider(LIDO_AaveProtocolDataProvider).getReserveTokensAddresses(address(USDS));
      uint256 availableLiquidity_USDS_LIDO = IERC20(USDS).balanceOf(address(USDS_lido_aToken));

      (address USDS_main_aToken, , ) = IProtocolDataProvider(MAIN_AaveProtocolDataProvider).getReserveTokensAddresses(address(USDS));
      uint256 availableLiquidity_USDS_MAIN = IERC20(USDS).balanceOf(address(USDS_main_aToken));

      address[] memory rewardTokensUSDSPool = IAaveIncentives(MAIN_IncentivesRewards).getRewardsByAsset(USDS_main_aToken);
      address rewardToken_USDS_main_aToken = rewardTokensUSDSPool[0];

      //@audit-info => RewardToken and aToken for USDS_Pool on Main_Market is the same asset (aEthUSDS)
      assertEq(rewardToken_USDS_main_aToken, USDS_main_aToken);

      uint256 hazelInitialUSDSBalance = 1_000e18;

      {
        vm.startPrank(USDS_WHALE);
        IERC20(USDS).transfer(jimmy, 100_000e18);
        IERC20(USDS).transfer(hazel, hazelInitialUSDSBalance);
        vm.stopPrank();


        vm.startPrank(jimmy);
        IERC20(USDS).approve(address(vault), 100_000e18);
        vault.deposit(99_000e18);
        vm.stopPrank();

        vm.startPrank(hazel);
        IERC20(USDS).approve(address(vault), hazelInitialUSDSBalance);
        vault.deposit(hazelInitialUSDSBalance);
        vm.stopPrank();
      }

      skip(3 days);

      uint256 depositsInUSDSMainMarketStrategy = IAToken(USDS_main_aToken).balanceOf(address(main_usds_AAVE_strategy));
      assertGt(depositsInUSDSMainMarketStrategy, 0);

      uint256 main_usds_AAVE_strategyRewards = IAaveIncentives(MAIN_IncentivesRewards).getUserRewards(rewardTokensUSDSPool, address(main_usds_AAVE_strategy), rewardToken_USDS_main_aToken);
      console2.log("main_usds_AAVE_strategyRewards: ", main_usds_AAVE_strategyRewards);

      uint256 aUSDCVaultBalanceBefore = IAToken(USDS_main_aToken).balanceOf(address(vault));
      assertEq(aUSDCVaultBalanceBefore, 0);

      //@audit => After harvestingRewards, the `main_usds_AAVE_strategyRewards` will send all the aTokens to the Vault
      vm.prank(admin);
      vault.harvestRewards("");

      //@audit-issue => The Vault received all the aTokens from the `main_usds_AAVE_strategy` (minus the fees paid to the feeReceiver)
      uint256 aUSDCVaultBalanceAfter = IAToken(USDS_main_aToken).balanceOf(address(vault));
      assertGt(aUSDCVaultBalanceAfter, aUSDCVaultBalanceBefore);
      
      //@audit-issue => The Strategy where the rewards is the same aToken as the one minted for the deposits is left with 0 aTokens after harvesting the rewards.
      uint256 depositsInUSDSMainMarketStrategyAfter = IAToken(USDS_main_aToken).balanceOf(address(main_usds_AAVE_strategy));
      assertEq(depositsInUSDSMainMarketStrategyAfter, 0);

      vm.prank(hazel);
      vault.claimRewards();

      uint256 hazel_USDS_BalanceFinal = IAToken(USDS_main_aToken).balanceOf(hazel) + IERC20(USDS).balanceOf(hazel);
      
      //@audit-issue => Hazel can't withdraw from the Vault because the main_usds_AAVE_strategy has 0 funds left after the rewards were harvest
      uint256 hazelWithdraw = vault.maxWithdraw(hazel);
      vm.prank(hazel);
      vm.expectRevert();
      vault.withdraw(hazelWithdraw - 1e18);

      //@audit => Hazel lost aprox a 30% from the original deposit because the error when harvesting the rewards
        //@audit => The `feeRecipient` received way more fees when the rewards were harvested && the Vault can't process withdrawals (assets on the other strategy are stuck)
      assertGt(hazelInitialUSDSBalance, hazel_USDS_BalanceFinal);
    }
}
```

## Tools Used
Manual Audit && Foundry

## Recommended Mitigation Steps
The recommendation to mitigate this problem is to track all the claimed rewards, **do not rely on the strategy balanceOf() the rewardToken**
- On the `_getRewardsToStrategy()`, track all the new claimed rewards, and when the rewards are harvested, update the tracking of the rewards accordingly (substract all the harvested rewards from the tracker).