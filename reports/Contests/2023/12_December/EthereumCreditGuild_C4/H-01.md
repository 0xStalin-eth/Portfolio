# Results => Duplicate of 1069

# High Severity

## Title
Biding on auctions can cause the protocol to accrue bad debt because the repaid amount by the bidder is not recomputed with the current value of creditMultiplier

## Impact
- The biggest impact is that the protocol can accrue large chunks of bad debt because of not recomputing the total amount that the bidders need to repay when bidding on an auction.
- It is also possible that the protocol doesn't accrue bad debt if the value of the creditMultiplier didn't change so drastically, but as long as the value of the creditMultiplier changes, a side effect of not recomputing the total amount to be repaid during the auction will be that the interests accrued by the protocol are far less than what they should really be. 
We'll go through some examples in the Proof of Concept section.


## Proof of Concept
- When a [loan is called, an auction is started for the loan's collateral](https://github.com/code-423n4/2023-12-ethereumcreditguild/blob/main/src/loan/LendingTerm.sol#L634-L675). The collateral will be auctioned for the total loan's debt including the accrued interests for the duration of the loan, the total debt + interests is computed in the [`LendingTerm::getLoanDebt()` function](https://github.com/code-423n4/2023-12-ethereumcreditguild/blob/main/src/loan/LendingTerm.sol#L200-L233), where it reads the total amount that was originally borrowed, computes the accrued interests, charges openFees (if any), and finally it computes the total loanDebt by adding the previous values and normalizing the value using the creditMultiplier, if bad debt was accrued while the loan was active, the value of the [`ProfitManager::creditMultiplier()`](https://github.com/code-423n4/2023-12-ethereumcreditguild/blob/main/src/governance/ProfitManager.sol#L79-L87) will be different than the value of the [`loan.borrowCreditMultiplier`](https://github.com/code-423n4/2023-12-ethereumcreditguild/blob/main/src/loan/LendingTerm.sol#L79), this will cause that the final value of the loanDebt to be bigger.

> LendingTerm.sol
```solidity
function _call(
    address caller,
    bytes32 loanId,
    address _auctionHouse
) internal {
    ...
    
    // update loan in state
    //@audit-info => Computes the total amount of CreditTokens that represents the total loan's debt + all the accrued interests for the duration of the loan!
    uint256 loanDebt = getLoanDebt(loanId);

    loans[loanId].callTime = block.timestamp;
    
    //@audit-info => The amount of debt the collateral will auction for!
    loans[loanId].callDebt = loanDebt;

    loans[loanId].caller = caller;

    //@audit-info => Initiates the collateral auction for the computed value of loanDebt!
    // auction the loan collateral
    AuctionHouse(_auctionHouse).startAuction(loanId, loanDebt);

    ...
}

function getLoanDebt(bytes32 loanId) public view returns (uint256) {
    ...
    
    //@audit-info => Computes the total amount of debt that will be repaid, including interests for the duration of the loan and opening fees (if any)
    // compute interest owed
    uint256 borrowAmount = loan.borrowAmount;
    uint256 interest = (borrowAmount *
        params.interestRate *
        (block.timestamp - borrowTime)) /
        YEAR /
        1e18;
    uint256 loanDebt = borrowAmount + interest;
    uint256 _openingFee = params.openingFee;
    if (_openingFee != 0) {
        loanDebt += (borrowAmount * _openingFee) / 1e18;
    }

    //@audit-info => The final value is normalized to match the current value of the creditMultiplier. If bad debt was accrued while the loan was open, the current creditMultiplier will be different than the loan.borrowCreditMultiplier, which will cause the final value of the loanDebt to be bigger to compensate for that difference!

    uint256 creditMultiplier = ProfitManager(refs.profitManager)
        .creditMultiplier();
    loanDebt = (loanDebt * loan.borrowCreditMultiplier) / creditMultiplier;

    return loanDebt;
}
```

> AuctionHouse.sol
```solidity
function startAuction(bytes32 loanId, uint256 callDebt) external {
    ...

    // save auction in state
    auctions[loanId] = Auction({
        startTime: block.timestamp,
        endTime: 0,
        lendingTerm: msg.sender,
        //@audit-info => The total collateral that will be auctioned
        collateralAmount: loan.collateralAmount,
        //@audit-info => The value of the `loanDebt` variable that was computed in the LendingTerm::getLoanDebt() function, which is the loan's debt + interests normalized based on the value of the creditMultiplier
        callDebt: callDebt
    });

    ...
}
```

- So, at this point, a new auction was initiated for the collateral's loan in exchange for repaying the loan's owed debt (including interests!). Now, let's analyze what happens when a bidder bids in an auction.
- When a bidder wants to bid in an auction needs to call the [`AuctionHouse::bid()` function](https://github.com/code-423n4/2023-12-ethereumcreditguild/blob/main/src/loan/AuctionHouse.sol#L166-L196), this function will call the [`AuctionHouse::getBidDetail()` function](https://github.com/code-423n4/2023-12-ethereumcreditguild/blob/main/src/loan/AuctionHouse.sol#L118-L161) to determine the amount of collateral that the bidder can receive and the amount of the debt that needs to be repaid. These 2 values are computed using a Dutch auction consisting of 2 phases, during the first phase, the bidder must pay all the auction.callDebt for a % of the loan's collateral (which will be slowly incrementing as time passes), and during the second phase, the bidder can receive all the loan's collateral in exchange for repaying a % of the auction.callDebt (which will be slowly decreasing as time passes).
  - The key here is to emphasize that the amount the bidder needs to repay is the same that it was computed when the auction was initiated, **it is not recomputed using the current value of the creditMultiplier**, will see in a second why this is a problem.
- So, with the returned values from the getBidDetail(), the bid() process to call the [`LendingTerm::onBid() function`](https://github.com/code-423n4/2023-12-ethereumcreditguild/blob/main/src/loan/LendingTerm.sol#L725-L825) and forwards the amount of collateral the borrower will receive back (if any), the amount of collateral the bidder will get, and the total amount of debt the bidder will repay. Now, in the `LendingTerm::onBid()`, the function computes the value of the `principal`, which is determined by the total loan.borrowAmount (the original loan's debt, without interests), and then it normalizes this value using the current value of the `creditMultiplier`. Then the value of the `principal` is compared against the value of the `creditFromBidder` variable, which is the exact same value as the `auction.callDebt` that was forwarded from the `AuctionHouse::bid()`. If the creditFromBidder >= principal, the auction will accrue interests from that loan because the bidder is repaying the full principal + the interests of the debt, but if the creditFromBidder is not enough to cover the principal, the auction will generate bad debt for the protocol.
  - Here is the problem, the value of the principal is computed based on the current value of the `creditMultiplier`, whilst the auction.callDebt was not recomputed, auction.callDebt was forwarded exactly as how it was calculated when the auction was initiated. The problem is that if the value of the `creditMultiplier` changes between the time when the auction was initiated, and when someone actually bids in the auction, this will cause the principal is computed using a different ratio than the ratio that was used when the auction.callDebt was computed.
    - This difference, depending on how much the `creditMultiplier` changed, can cause either the accrued interest to be less than what it should be, or even worse, it could cause the protocol generate bad debt when it should not.

- Before doing a code walkthrough, let's see an example with a number about how this problem plays out.

- Let's suppose that there is a loan for 1000gUSDC that is called, the total debt with interests is computed, and is determined that the total debt with interests is 1020 gUSDC (loan.borrowAmount + interests), so, the loan's collateral is auctioned and the auction.callDebt is set to be 1020gUSDC. While this auction is active, other auctions generated bad debt and caused the `creditMultiplier` changes by /2, now, when someone finally bids on our auction, when the `principal` is computed in the `LendingTerm::onBid()` function, the principal will be worth 2000gUSDC(without interests), why? Because the `loan.borrowAmount` of this loan is 1000gUSDC, if `creditMultiplier` goes /2, the computed principal will be double. (As an example, loan.borrowCreditMultiplier is 1, and then the bad debt is generated that causes the credMultiplier to go to 0.5, so, when the principal is computed when someone bids on the auction we'll have: [(1,000 * 1) / 0.5] = 2,000).
  - Let's suppose that the bid happens during phase 1, that means the bidder is willing to repay the full auction.callDebt for a portion of the collateral.
    - So, we have that the value of the `auction.callDebt` was computed as 1020gUSDC (loan.borrowAmount + interests), and this value if forwarded to the `LendingTerm::onBid()` function, which will be used as the value of the `creditFromBidder`. And, when the principal is computed in this function, we'll have that the `principal` is computed as 2,000gUSDC (loan.borrowAmount without including interests). This will cause the function consider that this auction is generating bad debt because the `creditFromBidder` is not enough to cover the `principal`, even though the bidder is willing to repay the full debt.
      - So, as a consequence of this bug, the protocol will accrue bad debt, and the bidder will end up paying less than the total loan's debt and will receive the expected amount of collateral.
  - The worst thing is that once this happens once it will affect all the rest of the active auctions because each time an auction is closed and generates bad debt, it will end up updating the value of the `creditMultiplier`, which slowly but surely it will go down and down, widening the ratios that were used to compute the values of the `auction.callDebt` from all the active auctions, and the actual ratio that will be used to compute the value of the principal when these auctions are bid.

- Let's analyze the code to spot where this issue happens.

> AuctionHouse.sol
```solidity
function bid(bytes32 loanId) external {
    //@audit-info => computes the collateral the bidder will receive and the amount of CreditTokens it needs to repay for that collateral!
    // this view function will revert if the auction is not started,
    // or if the auction is already ended.
    //@audit-issue => See the comments in the below function, in short, the creditAsked is not recomputed using the current ratio, if `creditMultiplier` changed while the auction was active, such a change is not taken into account, and the creditAsked is just taken as how it was computed when the auction was initiated!
    (uint256 collateralReceived, uint256 creditAsked) = getBidDetail(
        loanId
    );
    ...

    // notify LendingTerm of auction result
    address _lendingTerm = auctions[loanId].lendingTerm;
    LendingTerm(_lendingTerm).onBid(
        loanId,
        msg.sender,
        auctions[loanId].collateralAmount - collateralReceived, // collateralToBorrower
        collateralReceived, // collateralToBidder
        //@audit-info => Forwards the value of the amount to be repaid
        //@audit-info => If the bid is during phase1, this value will be the full debt + interests, which is the value stored in the `auction.callDebt`
        //@audit-info => If the bid is during phase2, the debt to be repay will slowly be less than the total debt (`auction.callDebt`) as time passes
        creditAsked // creditFromBidder
    );
    ...
}

function getBidDetail(
    bytes32 loanId
) public view returns (uint256 collateralReceived, uint256 creditAsked) {
    ...

    //@audit-info => As we can see in the below code's snippet, the `creditAsked` is calculated based on the value of the auctions.callDebt
    //@audit-info => The value of auctions.callDebt was computed when the auction was initiated, this value was computed based on the ratio of the `creditMultiplier` at that point in time
    
    //@audit-issue => But here, when someone finally bids on the auction, this value is just taken as it is, it doesn't consider the fact that the `creditMultiplier` could've changed while the auction was active!
    
    //@audit-info => The `creditAsked` variable of this function will be the value of the variable `creditFromBidder` in the LendingTerm::onBid()!
    // first phase of the auction, where more and more collateral is offered
    if (block.timestamp < _startTime + midPoint) {
        // ask for the full debt
        creditAsked = auctions[loanId].callDebt;

        ...
    }
    // second phase of the auction, where less and less CREDIT is asked
    else if (block.timestamp < _startTime + auctionDuration) {
        // receive the full collateral
        collateralReceived = auctions[loanId].collateralAmount;

        // compute amount of CREDIT to ask
        uint256 PHASE_2_DURATION = auctionDuration - midPoint;
        uint256 elapsed = block.timestamp - _startTime - midPoint; // [0, PHASE_2_DURATION[
        uint256 _callDebt = auctions[loanId].callDebt; // SLOAD
        creditAsked = _callDebt - (_callDebt * elapsed) / PHASE_2_DURATION;
    }
    ...
}
```

> LendingTerm.sol
```solidity
function onBid(
        bytes32 loanId,
        address bidder,
        uint256 collateralToBorrower,
        uint256 collateralToBidder,
        //@audit-info => This value is forwarded from the AuctionHouse::bid(), this is the value of the `creditAsked` variable, which is computed based on the `auction.callDebt` variable!
        uint256 creditFromBidder
    ) external {
        ...

        // compute pnl
        //@audit-info => Reading the current value of the creditMultiplier from the ProfitManager
        uint256 creditMultiplier = ProfitManager(refs.profitManager)
            .creditMultiplier();
        uint256 borrowAmount = loans[loanId].borrowAmount;

        //@audit-issue => The principal is computed based on the ratio of the current value for the `creditMultiplier`, if creditMultiplier changed while this auction was active, the principal will be computed using a different ratio than the ratio thas was used to compute the value of auction.callDebt, which its value is received in this function as `creditFromBidder`
        uint256 principal = (borrowAmount *
            loans[loanId].borrowCreditMultiplier) / creditMultiplier;
        int256 pnl;
        uint256 interest;
        
        //@audit-issue => If the principal is computed with a different ratio than the one that was used to compute the auction.callDebt, the below calculations will either cause the accrued interest is less than what it should really be, or it could even generate bad debt for the protocol when it should not!
        if (creditFromBidder >= principal) {
            interest = creditFromBidder - principal;
            pnl = int256(interest);
        } else {
            pnl = int256(creditFromBidder) - int256(principal);
            principal = creditFromBidder;
            require(
                collateralToBorrower == 0,
                "LendingTerm: invalid collateral movement"
            );
        }

        ...

        //@audit-info => If ratios are different, the actual repay amount will be less than what it should really be, thus, the bidder will repay less for the same amount of collateral!
        // pull credit from bidder
        if (creditFromBidder != 0) {
            CreditToken(refs.creditToken).transferFrom(
                bidder,
                address(this),
                creditFromBidder
            );
        }

        //@audit-info => If ratios are different, the actual amount of repaid amount will be less, thus, burning less principal than what it should really be burnt, the rest of the principal will be repaid as bad debt (if generated!)
        // burn credit principal, replenish buffer
        if (principal != 0) {
            CreditToken(refs.creditToken).burn(principal);
            RateLimitedMinter(refs.creditMinter).replenishBuffer(principal);
        }

        //@audit-info => If ratios are different, the accrued interests can be less than what they should really be, or even worse, the protocol can generate bad debt when it should not!
        // handle profit & losses
        if (pnl != 0) {
            // forward profit, if any
            if (interest != 0) {
                CreditToken(refs.creditToken).transfer(
                    refs.profitManager,
                    interest
                );
            }
            ProfitManager(refs.profitManager).notifyPnL(address(this), pnl);
        }

       ...
    }
```


## Tools Used
Manual Audit

## Recommended Mitigation Steps
- The most straightforward mitigation for this issue would be to, always recompute the value of the `auction.callDebt` using the current value of the `creditMultiplier` before forwarding the total amount of debt the bidder needs to repay. In this way, in the LendingTerm::onBid(), the received value of the `creditFromBidder` variable will be in the same ratio as the principal that is computed in that function.


- Modify the [`LendingTerm::getLoanDebt()` function](https://github.com/code-423n4/2023-12-ethereumcreditguild/blob/main/src/loan/LendingTerm.sol#L200-L233), the idea is to allow this function to return the loanDebt without being normalized using the creditMultiplier when an auction is initiated, and also allow it to return the loanDebt normalized with the current value of the creditMultiplier when a loan is being repaid, add a bool var, if the call is made to initiate an auction, return the value before normalizing it by applying the creditMultiplier, save the loanDebt as it is in the auction.callDebt, and in the getBidDetail(), make sure to normalize the auction.callDebt with the current value of the creditMultiplier, in this way, we can mitigate this issue because the final auction.callDebt is computed with the value of the current creditMultiplier at the exact time when the auction is being bidded
    - Make sure to update accordingly all the functions that call the `LendingTerm::getLoanDebt()` function, double-check that the correct value of the boolean variable is forwarded.
        - When starting an auction, the bool value shall be true, and when repaying or partially repaying a loan, the bool value shall be false.
            - The key to knowing what should be the value of the bool variable is: If the value of the loanDebt will be used within the same tx (like when repaying a loan), the bool should be false. But if the value of the loanDebt will be used in a different transaction (like when auctioning the collateral of a loan), the bool variable should be true, because if the value of the loanDebt is meant to be used in a separate transaction, the value of the creditMultiplier could change between the moment the loanDebt is computed and when it is finally used!

> LendingTerm.sol
```solidity
+ function getLoanDebt(bytes32 loanId, bool startingAuction) public view returns (uint256) {
    Loan storage loan = loans[loanId];
    uint256 borrowTime = loan.borrowTime;

    if (borrowTime == 0) {
        return 0;
    }

    if (loan.closeTime != 0) {
        return 0;
    }

    if (loan.callTime != 0) {
        return loan.callDebt;
    }

    // compute interest owed
    uint256 borrowAmount = loan.borrowAmount;
    uint256 interest = (borrowAmount *
        params.interestRate *
        (block.timestamp - borrowTime)) /
        YEAR /
        1e18;
    uint256 loanDebt = borrowAmount + interest;
    uint256 _openingFee = params.openingFee;
    if (_openingFee != 0) {
        loanDebt += (borrowAmount * _openingFee) / 1e18;
    }

    //@audit-info => If an auction is being initiated because a loan was called, return the loanDebt before normalizing by using the creditMultiplier
+   if(startingAuction) return loanDebt;

    //@audit-info => If this function is not called to initiate an auction, i.e. when repaying or partially repaying a loan, normalize the loanDebt with the creditMultiplier since this operation will be made in the same tx, the creditMultiplier won't change since the moment is computed in this function and when the computed value is actually used
    uint256 creditMultiplier = ProfitManager(refs.profitManager)
        .creditMultiplier();
    loanDebt = (loanDebt * loan.borrowCreditMultiplier) / creditMultiplier;

    return loanDebt;
}

```


> AuctionHouse.sol
```solidity
    function getBidDetail(
        bytes32 loanId
    ) public view returns (uint256 collateralReceived, uint256 creditAsked) {
        ...

+       uint256 _callDebt = auctions[loanId].callDebt;
+       uint256 creditMultiplier = ProfitManager(refs.profitManager).creditMultiplier();
        //@audit-info => This mitigation might require creating a getter in the LendingTerm contract that allows to pull the value of the loans[loanId].borrowCreditMultiplier!!!
+       _callDebt = (_callDebt * loans[loanId].borrowCreditMultiplier) / creditMultiplier;

        // first phase of the auction, where more and more collateral is offered
        if (block.timestamp < _startTime + midPoint) {
            // ask for the full debt
-           creditAsked = auctions[loanId].callDebt;
            //@audit-info => If in phase1, the creditAsked must be the full callDebt recomputed with the current ratio for the creditMultiplier
+           creditAsked = _callDebt;

            // compute amount of collateral received
            uint256 elapsed = block.timestamp - _startTime; // [0, midPoint[
            uint256 _collateralAmount = auctions[loanId].collateralAmount; // SLOAD
            collateralReceived = (_collateralAmount * elapsed) / midPoint;
        }
        // second phase of the auction, where less and less CREDIT is asked
        else if (block.timestamp < _startTime + auctionDuration) {
            // receive the full collateral
            collateralReceived = auctions[loanId].collateralAmount;

            // compute amount of CREDIT to ask
            uint256 PHASE_2_DURATION = auctionDuration - midPoint;
            uint256 elapsed = block.timestamp - _startTime - midPoint; // [0, PHASE_2_DURATION[
-           uint256 _callDebt = auctions[loanId].callDebt; // SLOAD
            //@audit-info => Use the recomputed value of the callDebt using the current ratio for the creditMultiplier
            creditAsked = _callDebt - (_callDebt * elapsed) / PHASE_2_DURATION;
        }
        ...
    }
```