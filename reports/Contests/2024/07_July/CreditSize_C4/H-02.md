# High Severity

## Lines with Problems
- https://github.com/code-423n4/2024-06-size/blob/main/src/libraries/actions/Liquidate.sol#L98

## Title
Liquidator's reward are wrongly computed because they are not converted to its value worth in collateral.

## Impact
Liquidators will be under-rewarded, and, liquidated borrowers will keep more collateral because of incorrectly rewarding the liquidators.

## Proof of Concept
When executing a profitable liquidation, [the `liquidationReward` is computed](https://github.com/code-423n4/2024-06-size/blob/main/src/libraries/actions/Liquidate.sol#L96-L99) to be the minimum between the leftover collateral after covering a 100% of the debt (`assignedCollateral - debtInCollateralToken`), or, a percentage (`liquidationRewardPercent`) of the amount of liquidated debt.

The computed value based on the `liquidationRewardPercent` is nominated in the Debt Value (USDC), which it is scaled by 6 decimals, and, the Collateral value is scaled by 18 decimals, apart from the different market value of each of those two assets, Debt is nominated in USDC, and Collateral is nominated in ETH/WETH.

**The problem is caused due to not converting the computed value to its worth in Collateral Token.** For example:
- If the `liquidationReward` is computed to be 1k USDC. 1k USDC is expressed as 1000e6, but, because the `liquidationReward` is paid in collateral, `1000e6` in collateral value is not worth the 1k USDC that the liquidator should receive.
  - For example, if ETH is worth 2k USDC. Assume a liquidationReward of 1k USDC. **The liquidator should receive 0.5 ETH (5e17) of collateral**, ***but instead, it would receive 1000e6 === 1e9 of collateral***

As explained in the previous example, the liquidator's are under-rewarded by orders of magnitudes, and the liquidated borrower gets beneffited from this because the liquidators are going to seize less collateral than what they should be able to.

```
function executeLiquidate(State storage state, LiquidateParams calldata params)
    ...
{
    ...

    // profitable liquidation
    if (assignedCollateral > debtInCollateralToken) {
        uint256 liquidatorReward = Math.min(
            assignedCollateral - debtInCollateralToken,
            //@audit-issue => Value is not computed to its worth in Collateral.
            Math.mulDivUp(debtPosition.futureValue, state.feeConfig.liquidationRewardPercent, PERCENT)
        );
        //@audit => Liquidators are paid with collateral, including their rewards
        liquidatorProfitCollateralToken = debtInCollateralToken + liquidatorReward;

        ...
    } else {
        ..
    }

    ...

    //@audit => Liquidators are paid with collateral, including their rewards
    state.data.collateralToken.transferFrom(debtPosition.borrower, msg.sender, liquidatorProfitCollateralToken);

    ...
}
```

## Tools Used
Manual Audit

## Recommended Mitigation Steps
Convert the computed `liquidationReward` based on the `liquidationRewardPercent` to its equivalent in collateral using the [`AccountingLibrary.debtTokenAmountToCollateralTokenAmount() function`](https://github.com/code-423n4/2024-06-size/blob/main/src/libraries/AccountingLibrary.sol#L26-L35)
- Take as example [how the fragmentation fee is computed when compensating.](https://github.com/code-423n4/2024-06-size/blob/main/src/libraries/actions/Compensate.sol#L148-L149)

```
function executeLiquidate(State storage state, LiquidateParams calldata params)
    ...
{
    ...

    // profitable liquidation
    if (assignedCollateral > debtInCollateralToken) {
        uint256 liquidatorReward = Math.min(
            assignedCollateral - debtInCollateralToken,
            //@audit => Convert the value to its worth in Collateral!
-           Math.mulDivUp(debtPosition.futureValue, state.feeConfig.liquidationRewardPercent, PERCENT)
+           state.debtTokenAmountToCollateralTokenAmount(Math.mulDivUp(debtPosition.futureValue, state.feeConfig.liquidationRewardPercent, PERCENT))
        );

        ...
    } else {
        ..
    }

    ...
}
```