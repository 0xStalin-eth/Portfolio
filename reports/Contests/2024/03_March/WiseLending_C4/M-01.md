# Line of Code with the Problem
https://github.com/code-423n4/2024-02-wise-lending/blob/main/contracts/WiseSecurity/WiseSecurityHelper.sol#L895-L899

# Medium Severity

## Title
Borrowers can DoS liquidations by repaying as little as 1 share.

## Impact
Liquidations can be DoSed which increments the risk of bad debt being generated on position.

## Proof of Concept
When a liquidator is liquidating a position in WiseLending, the liquidator needs to specify the amount of shares to be repaid. The liquidation logic checks if the positions are indeed liquidable, if so, it validates if the number of shares to be liquidated exceeds the total amount of shares that can be liquidated by using the [`WiseSecurityHelper.checkMaxShares() function`](https://github.com/code-423n4/2024-02-wise-lending/blob/main/contracts/WiseSecurity/WiseSecurityHelper.sol#L876-L900). **If the amount of shares the liquidator intends to liquidate exceeds the maximum amount of liquidable shares, the execution is reverted**
  - When the position has generated bad debt, the liquidation can liquidate all the user's borrowShares on the pool been liquidated
  - When the position has not generated bad debt, the maximum amount of liquidable shares is 50% of the existing user's borrowShares on the pool being liquidated

The problem with this approach is that borrowers can frontrun the liquidation and repay as little as 1 share of the existing debt on the same pool that the liquidator decided to liquidate the debt from. This will cause when the liquidation is executed, the total borrow shares of the user on the pool being liquidated to be lower. If the liquidator was trying to liquidate the maximum possible amount of shares, now, the maxShares that can be liquidated will be slightly less than the amount of shares that the liquidator specified, which will cause the tx to revert.

```
function checkMaxShares(
    uint256 _nftId,
    address _tokenToPayback,
    uint256 _borrowETHTotal,
    uint256 _unweightedCollateralETH,
    uint256 _shareAmountToPay
)
    public
    view
{
    //@audit-ok => total borrowShares a position owns for a _tokenToPayback pool
    uint256 totalSharesUser = WISE_LENDING.getPositionBorrowShares(
        _nftId,
        _tokenToPayback
    );

    //@audit-info => If baddebt, maxShares that can be liquidated are the totalSharesUser
    //@audit-info => If not baddebt, maxShares can be 50% of the total borrowShares
    uint256 maxShares = checkBadDebtThreshold(_borrowETHTotal, _unweightedCollateralETH)
        ? totalSharesUser
        : totalSharesUser * MAX_LIQUIDATION_50 / PRECISION_FACTOR_E18;

    if (_shareAmountToPay <= maxShares) {
        return;
    }

    //@audit-issue => reverts if the amount of shares to be repaid exceeds maxShares!
    revert TooManyShares();
}
```

For example, if there is a position in a liquidable state that has 100 borrowShares on the PoolA, and, a liquidator decides to liquidate the maximum possible amount of shares from this position it will send a tx to liquidate 50 shares from that position on the PoolA. The position's owner can use the [`WiseLending.paybackExactShares() function`](https://github.com/code-423n4/2024-02-wise-lending/blob/main/contracts/WiseLending.sol#L1204-L1238) to repay 1 share and frontrun the liquidator's tx. Now, when the liquidator's tx is executed, the position is still liquidable, but, it only has 99 borrowShares on the PoolA, as a result of this, the [`WiseSecurityHelper.checkMaxShares() function`](https://github.com/code-423n4/2024-02-wise-lending/blob/main/contracts/WiseSecurity/WiseSecurityHelper.sol#L876-L900) will determine that the maximum possible amount of liquidable shares is 49.5, and, because the liquidator specified that he intended to liquidate 50 shares, the tx will be reverted.

## Tools Used
Manual Audit

## Recommended Mitigation Steps
In the [`WiseSecurityHelper.checkMaxShares() function`](https://github.com/code-423n4/2024-02-wise-lending/blob/main/contracts/WiseSecurity/WiseSecurityHelper.sol#L876-L900), if the `_shareAmountToPay` exceeds `maxShares`, don't revert, re-adjust the number of shares that can be liquidated. Return the final value of `_shareAmountToPay` and forward it back to the [`WiseLending.liquidatePartiallyFromTokens() function`](https://github.com/code-423n4/2024-02-wise-lending/blob/main/contracts/WiseLending.sol#L1250-L1309). Then, use the final value of `shareAmountToPay` to compute the exact amount of tokens to be repaid in the [`WiseLending.liquidatePartiallyFromTokens() function`](https://github.com/code-423n4/2024-02-wise-lending/blob/main/contracts/WiseLending.sol#L1250-L1309).

> WiseSecurity.sol
```
function checksLiquidation(
    ...
    uint256 _shareAmountToPay
)
    external
    view
+   returns (uint256)
{
    ...


-   checkMaxShares(
+   return checkMaxShares( 
        _nftIdLiquidate,
        _tokenToPayback,
        borrowETHTotal,
        unweightedCollateralETH,
        _shareAmountToPay
    );
}
```

> WiseSecurityHelper.sol
```
function checkMaxShares(
    ...
    uint256 _shareAmountToPay
)
    public
    view
+   returns (uint256)    
{
    ...

    uint256 maxShares = checkBadDebtThreshold(_borrowETHTotal, _unweightedCollateralETH)
            ? totalSharesUser
            : totalSharesUser * MAX_LIQUIDATION_50 / PRECISION_FACTOR_E18;

    if (_shareAmountToPay <= maxShares) {
-       return;
+       return _shareAmountToPay;
+   } else {
+       return maxShares;
+   }

-   revert TooManyShares();
}

```

> WiseLending.sol
```
function liquidatePartiallyFromTokens(
    ...
    uint256 _shareAmountToPay
)
    ...
{
    ...

-   data.shareAmountToPay = _shareAmountToPay;

    ...

    //@audit-info => First, determine the maximum amount of liquidable shares
+   data.shareAmountToPay = WISE_SECURITY.checksLiquidation(
+     _nftId,
+     _paybackToken,
+     _shareAmountToPay
+   );

    //@audit-info => Then, compute the exact amount of tokens required to liquidate the final amount of liquidable shares
    data.paybackAmount = paybackAmount(
        _paybackToken,
-       _shareAmountToPay
+       data.shareAmountToPay
    );

    ...

-   WISE_SECURITY.checksLiquidation(
-       _nftId,
-       _paybackToken,
-       _shareAmountToPay
-   );

    ...
}
```